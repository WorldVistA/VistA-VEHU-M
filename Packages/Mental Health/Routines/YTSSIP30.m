YTSSIP30 ;SLC/BLD- MHAX ANSWERS SPECIAL HANDLING FOR SHORT INVENTORY OF PROBLEMS - AD (SIP-AD-30)
 ;;5.01;MENTAL HEALTH;**150**;DEC 30,1994;Build 210
 ;
 ;Public, Supported ICRs
 ; #2056 - Fileman API - $$GET1^DIQ
 ;
 ;
DLLSTR(YSDATA,YS,YSTRNG) ;
 ;  YSTRNG = 1 Score Instrument
 ;  YSTRNG = 2 get Report Answers and Text
 N DATA,YSSCALIEN,TOTSCORE,YSINSNAM,STRING,TOTSCORE,PHYSICAL,INTERP,INTRAP,IMPULSE,SOCIAL
 ;
 ; CMQ returns a scale score which is calculated and stored, no special text in report
 I YSTRNG=1 D SCORESV Q
 I YSTRNG=2 Q  ; D
 ;
 Q
 ;
STRING ;
 ;
 ;
 Q
 ;
DATA1 ;
 ;
 N I,II
 S TOTSCORE=0
 F I=3:1:17 Q:'$D(YSDATA(I))  S TOTSCORE=$G(TOTSCORE)+$$GET1^DIQ(601.75,$P(YSDATA(I),"^",3)_",",4,"I")
 F I=2,7,9 S PHYSICAL=$G(PHYSICAL)+$$GET1^DIQ(601.75,$P(YSDATA(I+2),"^",3)_",",4,"I")
 F I=10,11,13 S INTERP=$G(INTERP)+$$GET1^DIQ(601.75,$P(YSDATA(I+2),"^",3)_",",4,"I")
 F I=1,4,12 S INTRAP=$G(INTRAP)+$$GET1^DIQ(601.75,$P(YSDATA(I+2),"^",3)_",",4,"I")
 F I=5,6,15 S IMPULSE=$G(IMPULSE)+$$GET1^DIQ(601.75,$P(YSDATA(I+2),"^",3)_",",4,"I")
 F I=3,8,14 S SOCIAL=$G(SOCIAL)+$$GET1^DIQ(601.75,$P(YSDATA(I+2),"^",3)_",",4,"I")
 Q
 ;
SCORESV ;
 N YSSCGROUP,I
 D DATA1
 I $D(^TMP($J,"YSG",1)),^TMP($J,"YSG",1)="[ERROR]" D  Q  ;-->out
 .K ^TMP($J,"YSCOR")
 .S ^TMP($J,"YSCOR",1)="[ERROR]"
 .S ^TMP($J,"YSCOR",2)=$G(YSINSNAM)_" Scale not found"
 ;
 K ^TMP($J,"YSCOR")
 ;
 S ^TMP($J,"YSCOR",1)="[DATA]"
 F I=3:1:8 D
 .S YSSCALIEN=$P($P(^TMP($J,"YSG",I),"^",1),"=",2)
 .S ^TMP($J,"YSCOR",I)=$$GET1^DIQ(601.87,YSSCALIEN_",",3,"I")_"="_$S(I=3:TOTSCORE,I=4:PHYSICAL,I=5:INTERP,I=6:INTRAP,I=7:IMPULSE,1:SOCIAL)
 ;S YSSCALIEN=$P($P(^TMP($J,"YSG",3),"^",1),"=",2)
 ;S ^TMP($J,"YSCOR",2)=$$GET1^DIQ(601.87,YSSCALIEN_",",3,"I")_"="_TOTSCORE
 Q
 ;
