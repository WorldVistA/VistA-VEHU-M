YTSEAT26 ;SLC/BLD- MHAX ANSWERS SPECIAL HANDLING FOR EAT-26
 ;;5.01;MENTAL HEALTH;**150**;DEC 30,1994;Build 210
 ;
 ;Public, Supported ICRs
 ; #2056 - Fileman API - $$GET1^DIQ
 ;
 Q
 ;
DLLSTR(YSDATA,YS,YSTRNG) ;
 ;  YSTRNG = 1 Score Instrument
 ;  YSTRNG = 2 get Report Answers and Text
 N DATA,DES,LEG,NODE,YSQN,YSSCALIEN,TOTSCORE,QUES,BASIS,TOTSCR
 N YSCDA,YSSCNAM,YSINSNAM,STRING,STRING1,TOTSCORE,STRING,STRING2
 ;
 ; Basis-24 Psychosis returns a scale score which is calculated and stored, no special text in report
 I YSTRNG=1 D SCORESV Q
 I YSTRNG=2 D
 .D STRING
 .S YSDATA(N)="7772^9999;1^"_STRING S N=N+1
 .S YSDATA(N)="7773^9999;1^"_STRING2 S N=N+1
 ;
 Q
 ;
STRING ;
 N CURWT,HT,BMI,YSALERT
 S CURWT=$P(YSDATA(37),"^",3)
 S HT=($P(YSDATA(36),"^",3)*12)+($P(YSDATA(41),"^",3))
 S BMI=((CURWT*703)/HT)/HT
 S STRING=$FN(BMI,"",2)
 ;
 S STRING2="NO"
 D YSALERT(.YSALERT)
 I 2345[YSALERT("A") S STRING2="YES" Q
 I 12345[YSALERT("B") S STRING2="YES" Q
 I 12345[YSALERT("C") S STRING2="YES" Q
 I YSALERT("D")=5 S STRING2="YES" Q
 I YSALERT("E")=1 S STRING2="YES" Q
 ;
 Q
 ;
DATA1 ;
 ;
 N I,II
 S TOTSCORE=0
 F I=3:1:28 Q:'$D(YSDATA(I))  S TOTSCORE=TOTSCORE+$$GET1^DIQ(601.75,$P(YSDATA(I),"^",3)_",",4,"I")
 Q
 ;
SCORESV ;
 N YSSCGROUP,I
 D DATA1
 I $D(^TMP($J,"YSG",1)),^TMP($J,"YSG",1)="[ERROR]" D  Q  ;-->out
 .K ^TMP($J,"YSCOR")
 .S ^TMP($J,"YSCOR",1)="[ERROR]"
 .S ^TMP($J,"YSCOR",2)=$G(YSINSNAM)_" Scale not found"
 ;
 K ^TMP($J,"YSCOR")
 ;
 S ^TMP($J,"YSCOR",1)="[DATA]"
 S YSSCALIEN=$P($P(^TMP($J,"YSG",3),"^",1),"=",2)
 S ^TMP($J,"YSCOR",2)=$$GET1^DIQ(601.87,YSSCALIEN_",",3,"I")_"="_TOTSCORE
 Q
 ;
YSALERT(YSALERT) ;ARRAY FOR BEHAVIORAL ALERTS
 ;
 S YSALERT("A")=$$GET1^DIQ(601.75,$P(YSDATA(29),"^",3)_",",4,"I")
 S YSALERT("B")=$$GET1^DIQ(601.75,$P(YSDATA(30),"^",3)_",",4,"I")
 S YSALERT("C")=$$GET1^DIQ(601.75,$P(YSDATA(31),"^",3)_",",4,"I")
 S YSALERT("D")=$$GET1^DIQ(601.75,$P(YSDATA(32),"^",3)_",",4,"I")
 S YSALERT("E")=$$GET1^DIQ(601.75,$P(YSDATA(33),"^",3)_",",4,"I")
 ;
 Q
