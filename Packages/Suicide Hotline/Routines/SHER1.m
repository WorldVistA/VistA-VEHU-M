QIP3NUR1 ;B'HAM ISC/SAB,WRT-QUIC Neuroleptic Meds/Outpatient ; [ 10/25/93  9:48 AM ]
V ;;2.1;**1**;;SEP 28,1993
 K QIPD S QFLG=0 D INTRO^QIP3NUR2 D ONEDT^QIPEXTU3 G:$D(DIRUT) EX S QIPDT=QIPADT
DEV W ! K %ZIS,IO("Q"),IOP,ZTSK S QIPION=ION,%ZIS="QM",%ZIS("B")="" D ^%ZIS I POP W !,"NO DEV SELECTED OR REPORT PRINTED!" G EX
 S QIPVAMC=$G(QIPVAMC)
 I $D(IO("Q")) S ZTSAVE("QIPVAMC")="",ZTSAVE("QIPDT")="",ZTSAVE("QIPD(")="",ZTDESC="QIP NEUROLEPTIC MEDICINE REPORT",ZTRTN="EN^QIP3NUR1" D ^%ZTLOAD D HOME^%ZIS K ZTSK S QFLG=1 G EX
EN ;enter here from taskman
 D NOW^%DTC S Y=% X ^DD("DD") S RT=Y
 K ^TMP($J) S (PG,QIPT4,QIPT44,QFLG)=0,QIPDTQ=QIPDT,QIPDT0=QIPDT-1
 ;
PLOOP ; get patients
 F QIPDFN=0:0 S QIPDFN=$O(^PS(55,QIPDFN)) Q:'QIPDFN  I $O(^PS(55,QIPDFN,"P","A",QIPDT0)) S QIPNEW=0,QIPNCT=0,QIPRLCT=0 F QIPDT=QIPDT0:0 S QIPDT=$O(^PS(55,QIPDFN,"P","A",QIPDT)) Q:'QIPDT  D RXLOOP
 ;
OUT D SUM G:QFLG EX D HDR G:QFLG EX
 W !!,"[25] Of outpatients who have prescribed neuroleptic medications (or drugs with",!,"similar pharmaceutical action), what percent are receiving two or more?",!!?5,"# of outpatients",?30,"total # of outpatients"
 W !?5,"receiving 2 or more",?30,"receiving neuroleptics",?60,"percent"
 W !?5,"-------------------",?30,"----------------------",?60,"-------"
 W !?13,QIPT44,?40,QIPT4,?61,$S(QIPT4:$J(QIPT44/QIPT4*100,3,2),1:0)
 W:$E(IOST)'="C" @IOF
EX I $E(IOST)="C"&('QFLG) S DIR(0)="E" W !! D ^DIR K DIR
 K ^TMP($J),%,%I,%T,C,DTOUT,DUOUT,POP,QIP,QIPD,QIPDFN,QIPDR,QIPDRG,QIPDRUG,QIPDT,QIPDT0,QIPDTQ,QIPNCT,QIPNEW,QIPRXN,I,J,PG,Q,QIPN,QIPRX,X,QIPT4,QIPT44,X,Y,ZTDESC,ZTRTN,ZTSAVE
 K RT,DIR,ORD,QIPRLCT,QIPGN,QIPVAGN,RUNDT,BEGDT,SS,JJ,QFLG,VA,VAERR,VADM,DFN
 D ^%ZISC K QIPION,TAB,TB1,TB3,TB4,ZTSK S:$D(ZTQUEUED) ZTREQ="@"
 Q
 ;
RXLOOP ; got patient - get Rxs
 F QIPRXN=0:0 S QIPRXN=$O(^PS(55,QIPDFN,"P","A",QIPDT,QIPRXN)) Q:'QIPRXN  I 134'[$E(+$P($G(^PSRX(QIPRXN,0)),"^",15)) S QIPRX=^PSRX(QIPRXN,0),QIPDRG=+$P(QIPRX,"^",6) I $D(^PSDRUG(QIPDRG,0)) S QIPDRUG=^(0) I $P(QIPDRUG,"^",3)'["S" D GET
 Q
 ;
GET ; got drug in QIPDRUG check data
 S DFN=QIPDFN D DEM^VADPT
 S QIPN=VADM(1)_"^"_QIPDFN
 I $D(^TMP($J,"QIPNUR",QIPN,$P(^PSDRUG(QIPDRG,0),"^"))) Q
 S QIPDRUG=$P(QIPDRUG,"^",2) ; CLASS
 D ISIT^QIP3NUR2 I QIPDRUG="CN701"!(QIPDRUG="CN709")!($D(QIPD(QIPDRG))) D GET1 I 'QIPNEW S QIPNEW=1,QIPT4=QIPT4+1
 Q
GET1 ;
 S:'$D(^TMP($J,"QIPNUR",QIPN,0)) ^TMP($J,"QIPNUR",QIPN,0)="0^"_VA("PID")
 S $P(^TMP($J,"QIPNUR",QIPN,0),"^")=$P(^TMP($J,"QIPNUR",QIPN,0),"^")+1,^TMP($J,"QIPNUR",QIPN,$P(^PSDRUG(QIPDRG,0),"^"),0)=$P(^PSRX(QIPRXN,0),"^") D ^QIP3NUR2
 Q
SUM ;output detailed data
 ; REORDER ^TMP TO SORT IN DESCENDING ORDER BY NUMBER OF SCRIPTS
 S QIPN="" F  S QIPN=$O(^TMP($J,"QIPNUR",QIPN)) Q:QIPN=""  S ORD=999-$P(^TMP($J,"QIPNUR",QIPN,0),"^",3) S ^TMP($J,ORD,QIPN)="" I $P(^TMP($J,"QIPNUR",QIPN,0),"^",3)>1 S QIPT44=QIPT44+1
 D HDR,HDR1
 F ORD=0:0 S ORD=$O(^TMP($J,ORD)) Q:'ORD  D SUM1 Q:QFLG
 Q
SUM1 S QIPN="" F  S QIPN=$O(^TMP($J,ORD,QIPN)) Q:QIPN=""  I $P(^TMP($J,"QIPNUR",QIPN,0),"^",3)>1 S QIPD=1 D:$Y+5>IOSL HDR,HDR1 Q:QFLG  W !,$P(QIPN,"^"),?50,$P(^TMP($J,"QIPNUR",QIPN,0),"^",2),?70,$P(^(0),"^",3) D OUT1 Q:QFLG  W !
 W:'$G(QIPD) !!!?15,"**** NO OUTPATIENTS FOUND ON THIS DAY ****"
 Q
OUT1 S QIPDRG=0 F J=0:0 S QIPDRG=$O(^TMP($J,"QIPNUR",QIPN,QIPDRG)) Q:QIPDRG=""  D:$Y+5>IOSL HDR,HDR1 Q:QFLG  W !?5,$P(^TMP($J,"QIPNUR",QIPN,QIPDRG,0),"^"),?20,QIPDRG
 Q
HDR ; HEADER
 I $E(IOST)="C" S SS=22-$Y F JJ=1:1:SS W !
 I $E(IOST)="C",PG>0 S DIR(0)="E" W ! D ^DIR K DIR I 'Y S QFLG=1 Q
 S TAB=(80-$L($G(QIPVAMC)))/2,TB1=(80-$L("QUALITY IMPROVEMENT REPORT: NEUROLEPTIC MEDS/OUTPATIENT"))/2,Y=QIPDTQ X ^DD("DD") S TB3=(80-$L("AS OF "_Y))/2,TB4=(80-$L("DATE PRINTED: "_RT))/2
 U IO S PG=PG+1 W:$Y!($E(IOST)="C") @IOF W !?TAB,$G(QIPVAMC),?72,"PAGE "_PG,!?TB1,"QUALITY IMPROVEMENT REPORT: NEUROLEPTIC MEDS/OUTPATIENT ",!?TB3,"AS OF " S Y=QIPDTQ X ^DD("DD") W Y,!?TB4,"DATE PRINTED: "_RT,!
 F Q=1:1:80 W "-"
 Q
HDR1 Q:QFLG  W !,"OUTPATIENT NAME",?50,"PID",?64,"TOTAL MEDS FOUND",!?5,"RX NUMBER",?20,"DRUG",! F Q=1:1:80 W "="
 Q
