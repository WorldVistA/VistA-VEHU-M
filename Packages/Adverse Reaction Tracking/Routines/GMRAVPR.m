GMRAVPR ;ISP/RFR - VPR CALLS FOR ART ;Oct 29, 2018@12:42
 ;;4.0;Adverse Reaction Tracking;**53**;Mar 29, 1996;Build 306
 Q
ASSESS(MODE) ;NOTIFY SUBSCRIBERS OF ASSESSMENT CHANGES
 N SUB,REF,ORDER,ZTRTN,ZTDESC,ZTDTH,ZTIO,ZTSAVE,ZTSK,ADD,NODE,OUTNODE
 S ADD=1,OUTNODE=$J_";"_DA
 F NODE=2:1:4  I X1(NODE)'="" S ADD=0
 I MODE="KILL" Q:ADD  D
 .N DELETE S DELETE=1
 .F NODE=1:1:4  I X2(NODE)'="" S DELETE=0
 .I DELETE S ^XTMP("GMRAVPR",OUTNODE,"AFTER",0)=""
 .S REF="X1",SUB="BEFORE"
 I MODE="SET" D
 .I ADD S ^XTMP("GMRAVPR",OUTNODE,"BEFORE",0)=""
 .S REF="X2",SUB="AFTER"
 S ^XTMP("GMRAVPR",0)=$$FMADD^XLFDT(DT,1)_U_$P($$NOW^XLFDT,".")_U_"Notify Subscribers of ART Assessment Change"
 S ^XTMP("GMRAVPR",OUTNODE)=DA
 S ORDER=0 F  S ORDER=$O(@REF@(ORDER)) Q:'+ORDER  D
 .I $G(@REF@(ORDER))'="" S $P(^XTMP("GMRAVPR",OUTNODE,SUB,0),U,ORDER)=$G(@REF@(ORDER))
 S ZTRTN="ASSESSDQ^GMRAVPR"
 S ZTDESC="GMRA ADVERSE REACTION ASSESSMENT CHANGE NOTIFIER"
 S ZTDTH=$$HADD^XLFDT($H,,,2),ZTIO="",ZTSAVE("OUTNODE")=""
 D ^%ZTLOAD
 Q
ASSESSDQ ;SEND ASSESSMENT CHANGE NOTIFICATION
 N DIC,X,GMRAL
 M GMRAL=^XTMP("GMRAVPR",OUTNODE)
 K ^XTMP("GMRAVPR",OUTNODE)
 S DIC=101,X="GMRA ASSESSMENT CHANGE"
 D EN^XQOR
 I $O(^XTMP("GMRAVPR",0))="" K ^XTMP("GMRAVPR")
 Q
