ORSMART ;SLC/JMH - SMART UTILITY FUNCTIONS ;12/18/16
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**377**;Dec 17, 1997;Build 582
 ;
SMLABOI(ORLAB) ;RETURN 1 IF ORLAB IS A LAB OI THAT SMART IS INTERESTED IN
 N ORRET S ORRET=0
 ; GET REMINDER TERM LIST FOR RADIOLOGY PROCEDURES
 N ORTERM D TERM^PXRMLDR("VA-SMART LAB ORDERABLE ITEMS",.ORTERM)
 I $D(ORTERM("E","ORD(101.43,",ORLAB)) S ORRET=1
 Q ORRET
 ;
SMLABTE(ORLAB) ;RETURN 1 IF ORLAB IS A LAB TEST THAT SMART IS INTERESTED IN
 N ORRET S ORRET=0
 ; GET REMINDER TERM LIST FOR RADIOLOGY PROCEDURES
 N ORTERM D TERM^PXRMLDR("VA-SMART LAB ORDERABLE ITEMS",.ORTERM)
 I $D(ORTERM("E","LAB(60,",ORLAB)) S ORRET=1
 Q ORRET
 ;
SMRADPR(ORRAD) ;RETURN 1 IF ORRAD IS A RADIOLOGY PROCUEDURE SMART IS INTERESTED IN
 N CODE,ORARR,ORERR,ORRET S ORRET=0
 ; GET REMINDER TERM LIST FOR RADIOLOGY PROCEDURES
 S CODE=$$GET1^DIQ(71,ORRAD_",",9,"")
 I CODE'="" S ORARR("CODESYS")="CPT",ORARR("CODE")=CODE
 S ORARR("ITEMTYPE")="RAMIS(71,",ORARR("ITEM")=ORRAD
 S ORRET=+$$CHCKPROC^PXRMPRAD(.ORARR,.ORERR)
 Q ORRET
 ;
SMRADOI(OROI) ;RETURN 1 IF OROI IS A RADIOLOGY OI SMART IS INTERESTED IN
 N ORRET S ORRET=0
 ; GET REMINDER TERM LIST FOR RADIOLOGY PROCEDURES
 N ORTERM D TERM^PXRMLDR("VA-WH BR IMAGES PROCEDURES",.ORTERM)
 I $D(ORTERM("E","ORD(101.43,",OROI)) S ORRET=1
 Q ORRET
 ;
SMLFIRE(ORIEN,ORMSG) ;INPUT IS FILE 100 IEN FROM ALERT.  OUTPUT IS ORFIRE
 S ORFIRE=0
 I $$SMLABTE(+$$OR2PKG(+ORIEN)) S ORFIRE=1
 I $$SMLABOI(+$$OR2OI(+ORIEN)) S ORFIRE=1
 I ORFIRE S ORNEWMSG="SMART: "_ORMSG
 Q
 ;
SMRFIRE(ORIEN,ORMSG) ;INPUT IS FILE 100 IEN FROM ALERT.  OUTPUT IS ORFIRE
 S ORFIRE=0
 I $$SMRADPR(+$$OR2PKG(+ORIEN)) S ORFIRE=1
 I $$SMRADOI(+$$OR2OI(+ORIEN)) S ORFIRE=1
 I ORFIRE S ORNEWMSG="SMART: "_ORMSG
 Q
 ;
OR2PKG(ORIEN) ;TURN FILE 100 IEN INTO PACKAGE REFERENCE
 N ORRET S ORRET=""
 N ORI S ORI=$O(^OR(100,ORIEN,4.5,"ID","ORDERABLE",0)) I ORI D
 .N ORJ S ORJ=$G(^OR(100,ORIEN,4.5,ORI,1),0) I ORJ D
 ..S ORRET=$P($G(^ORD(101.43,ORJ,0)),U,2)
 Q ORRET
 ;
OR2OI(ORIEN) ;TURN FILE 100 IEN INTO ORDERABLE ITEM
 N ORRET S ORRET=""
 N ORI S ORI=$O(^OR(100,ORIEN,4.5,"ID","ORDERABLE",0)) I ORI D
 .N ORJ S ORJ=$G(^OR(100,ORIEN,4.5,ORI,1),0) I ORJ S ORRET=ORJ
 Q ORRET
 ;
