PXRMP85I ;ISP/AGP - PATCH 85 INSTALLATION ;Oct 24, 2023@15:37:23
 ;;2.0;CLINICAL REMINDERS;**85**;Feb 04, 2005;Build 7
 ;
BACKUP ;
 N DESCTXT,EXNAME,INPUTS,NOTINLM
 S INPUTS(801.41,"VA-WH GP BR BI-RAD SELECTOR: BIRAD 0")=""
 S INPUTS(801.41,"VA-WH GP BR BI-RAD SELECTOR: BIRAD 0 DENSE")=""
 S INPUTS(801.41,"VA-WH GP SMART BR OUTSIDE BI-RAD SELECTOR: BIRAD 0")=""
 S INPUTS(801.41,"VA-WH GP SMART BR OUTSIDE BI-RAD SELECTOR: BIRAD 0 DENSE")=""
 S INPUTS(801.41,"VA-WH GP SMART BR MALE OUTSIDE BI-RAD SELECTOR: BIRAD 0")=""
 S DESCTXT(1)="Exchange file built when patch PXRM*2.0*85"
 S DESCTXT(2)="was installed on "_$$FMTE^XLFDT($$NOW^XLFDT())
 S NOTINLM=1,EXNAME="PXRM*2.0*85 AUTOMATIC BACKUP"
 D CRE^PXRMEXSP(EXNAME,.INPUTS,.DESCTXT,NOTINLM)
 Q
 ;
EXARRAY(MODE,ARRAY) ;List of exchange entries used by delete and install
 ;MODE values: I for include in build, A for include action.
 N LN
 S LN=0
 ;
 S LN=LN+1
 S ARRAY(LN,1)="PXRM*2.0*85 BI-RAD 0 UPDATES"
 I MODE["I" S ARRAY(LN,2)="11/01/2023@11:58:21"
 I MODE["A" S ARRAY(LN,3)="O"
 ;
 Q
 ;
PRE ;Pre-init
 ;Disable options and protocols
 D OPTIONS^PXRMUTIL("DISABLE","Install of PXRM*2.0*85")
 D PROTCOLS^PXRMUTIL("DISABLE","Install of PXRM*2.0*85")
 D DELEXE^PXRMEXSI("EXARRAY","PXRMP85I")
 Q
 ;
POST ;Post-init
 ;Install Exchange File entries.
 N DIALOGS
 D BACKUP
 D SRCFINDS(.DIALOGS)
 D SMEXINS^PXRMEXSI("EXARRAY","PXRMP85I")
 D SETFINDS(.DIALOGS)
 ;Enable options and protocols
 D OPTIONS^PXRMUTIL("ENABLE","Install of PXRM*2.0*85")
 D PROTCOLS^PXRMUTIL("ENABLE","Install of PXRM*2.0*85")
 Q
 ;
SRCFINDS(DIALOGS) ;
 N INPUTS
 S INPUTS("DIALOG NAME","VA-WH GP BR BI-RAD SELECTOR: BIRAD 0")=""
 S INPUTS("DIALOG NAME","VA-WH GP BR BI-RAD SELECTOR: BIRAD 0 DENSE")=""
 S INPUTS("DIALOG NAME","VA-WH GP SMART BR OUTSIDE BI-RAD SELECTOR: BIRAD 0")=""
 S INPUTS("DIALOG NAME","VA-WH GP SMART BR OUTSIDE BI-RAD SELECTOR: BIRAD 0 DENSE")=""
 S INPUTS("DIALOG NAME","VA-WH GP SMART BR MALE OUTSIDE BI-RAD SELECTOR: BIRAD 0")=""
 S INPUTS("FILES","ORD(101.41,")=""
 D BMES^XPDUTL("Searching for dialogs for order dialogs")
 D SRC4FTYPE^PXRMDUTL1(.DIALOGS,.INPUTS)
 I '$D(DIALOGS) D BMES^XPDUTL("  No order dialogs found") Q
 D BMES^XPDUTL("  Done")
 Q
 ;
SETFINDS(DIALOGS) ;
 I '$D(DIALOGS) Q
 N FDA,FIND,IEN,IENS,ID,ITEM,MSG
 D BMES^XPDUTL("Setting order dailogs")
 S IEN=0
 F  S IEN=$O(DIALOGS(IEN)) Q:IEN'>0  D
 .K FDA,MSG
 .S FIND="",ID=IEN
 .F  S FIND=$O(DIALOGS(IEN,FIND)) Q:FIND=""  D
 ..S ITEM=$G(DIALOGS(IEN,FIND))
 ..;check to make sure existing finding item is empty, if not add to additional findings
 ..I FIND=0,$P($G(^PXRMD(801.41,IEN,1)),U,5)="" S FDA(801.41,IEN_",",15)=ITEM Q
 ..S IENS="+"_(ID+1)_","_IEN_","
 ..S FDA(801.4118,IENS,.01)=ITEM
 .D UPDATE^DIE("","FDA",,"MSG")
 .I $D(MSG) D  Q
 ..D BMES^XPDUTL(" Error updating dialog: "_$P($G(^PXRMD(801.41,IEN,0)),U))
 ..D AWRITE^PXRMUTIL("MSG")
 .D SETHIST^PXRMDUTL1(IEN,"Set Order Dialogs for patch 85")
 D BMES^XPDUTL("  Done")
 Q
 ;
