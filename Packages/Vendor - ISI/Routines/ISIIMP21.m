ISIIMP21 ;ISI GROUP/MLS -- RAD ORDERS IMPORT CONT.
 ;;1.0;;;Jun 26,2012;Build 30
 Q
 ;
VALIDATE()      ;
 ; Validate import array contents
 S ISIRC=$$VALRADO^ISIIMPUC(.ISIMISC)
 Q ISIRC
 ;
MAKERADO() ;
 ; Create patient(s)
 S ISIRC=$$RADO(.ISIMISC)
 Q ISIRC
 ;
RADO(ISIMISC) ;Create Radiology Order
 ; Input - ISIMISC(ARRAY)
 ; Format:  ISIMISC(PARAM)=VALUE
 ;     eg:  ISIMISC("MAG_LOC")= 23 
 ;
 ; Output - ISIRC [return code]
 ;          ISIRESUL(0)=1 [if successful]
 ;          ISIRESUL(1)=RAOIFN [if successful] 
 ;
 N RADFN,RAPROC,RAMLC,RADTE,RACAT,REQLOC,REQPHYS,RAREASON,RAMISC,RAOIFN
 ;
 S ISIRC=$$PREP()
 I +ISIRC<0 Q ISIRC
 ;
 S ISIRC=$$CREATE()
 I +ISIRC<0 Q ISIRC
 ;
 ; S ISIRC=$$REGISTER()
 ; I +ISIRC<0 Q ISIRC
 ;
 ; S ISIRC=$$COMPLETE()
 ; I +ISIRC<0 Q ISIRC
 ;
 S ISIRESUL(0)=1
 S ISIRESUL(1)=RAOIFN
 Q ISIRC
 ;
PREP()
 ;
 S RADFN=$G(ISIMISC("DFN")) I RADFN="" Q "-1^Missing RADFN (PREP ISIIMP21)."
 S RAPROC=$G(ISIMISC("RAPROC")) I RAPROC="" Q "-1^Missing RAPROC (PREP ISIIMP21)."
 S RAMLC=$G(ISIMISC("MAGLOC")) I RAMLC="" Q "-1^Missing MAGLOC (PREP ISIIMP21)."
 S RADTE=$G(ISIMISC("RADTE")) I RADTE="" Q "-1^Missing RADTE (PREP ISIIMP21)."
 S RACAT=$G(ISIMISC("EXAMCAT")) I RACAT="" Q "-1^Missing EXAMCAT (PREP ISIIMP21)."
 S REQLOC=$G(ISIMISC("REQLOC")) I REQLOC="" Q "-1^Missing REQLOC (PREP ISIIMP21)."
 S REQPHYS=$G(ISIMISC("PROV")) I REQPHYS="" Q "-1^Missing PROV (PREP ISIIMP21)."
 S RAREASON=$G(ISIMISC("REASON")) I RAREASON="" Q "-1^Missing REASON (PREP ISIIMP21)."
 K RAMISC
 S RAMISC("ACLHIST",1)=ISIMISC("HISTORY") I RAMISC("ACLHIST",1)="" Q "-1^Missing HISTORY (PREP ISIIMP21)."
 ;S RAMISC("PREGNANT")="N" 
 Q 1
 ;
CREATE()
 ;
 S RAOIFN=$$ORDER^RAMAG02(.RAMAG,RADFN,RAMLC,RAPROC,RADTE,RACAT,REQLOC,REQPHYS,RAREASON,.RAMISC)
 I +RAOIFN<0 Q "-1^Unable to create Radiology Order (CREATE ISIIMP21)"
 Q 1
 ;
REGISTER()
 ;
 ;Check requirements
 ; N RESULTS K RESULTS
 ; D EXMSTREQ^RAMAGU06(EXMSTIEN,RAPROC)
 ;
 N RAMISC
 K RAMISC
 S RAMISC("EXAMCAT")="O" ;Outpatient CATEGORY OF EXAM field (4) of sub-file #70.03
 S RAMISC("PRINCLIN")=REQLOC ; LOCATION file (#44)
 S RAMISC("CLINHIST",1)=ISIMISC("HISTORY")
 S RAMISC("SERVICE")=$O(^DIC(49,"B","RADIOLOGY","")) ;IEN of SERVICE/SECTION (#49)
 S RAMISC("TECH")=ISIMISC("TECH") ; Technologist
 S RAMISC("TECHCOMM")=ISIMISC("TECHCOM")  ; Tech comments Captured."
 S RAMISC("PRIMINTSTF")=REQPHYS
 S X=$$REGISTER^RAMAG03(.RAMAG,.OUT,RAOIFN,RADTE,.RAMISC)
 I X<0 Q "-1^Unable to register rad exam (ISIIMP21)"
 ; S RADFN=$P(OUT(1),"^",1)
 S RADTI=$P(OUT(1),"^",2)
 S RACNI=$P(OUT(1),"^",3)
 S RACASE=$P(OUT(1),"^",4)
 S ACNUMB=$P(OUT(1),"^",5)
 S RAINTDT=$P(OUT(1),"^",6)
 Q 1
 ;
COMPLETE()
 ;
 ;Check requirements
 ; N RESULTS K RESULTS
 ; D EXMSTREQ^RAMAGU06(EXMSTIEN,RAPROC)
 ;
 S RACASE=RADFN_"^"_RADTI_"^"_RACNI
 S RAMISC("REPORT",1)=ISIMISC("REPORT")
 S RAMISC("RPTDTE")=RADTI ;Reported Date field (8) of File #74
 S RAMISC("IMPRESSION",1)=ISIMISC("IMPRESSION")
 S RAMISC("CLINHIST",1)=ISIMISC("HISTORY")
 S RAMISC("VERDTE")=$P(RADTE,".",1)
 S RAMISC("VERPHYS")=REQPHYS
 S RAMISC("PRIMDXCODE")=4
 S RAMISC("ELSIG")=""
 S X=$$COMPLETE^RAMAG06(.RAMAG,RACASE,RPTDTE,.RAMISC)
 I +X<0 Q "-1^Unable to complete rad exam (ISIIMP21)"
 Q 1
