DSIROIAH ;EWL - Document Storage System Inc - AD HOC REPORTING ;09/23/2011 11:18
 ;;8.2;RELEASE OF INFORMATION - DSSI;;Apr 14, 2011;Build 25
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;
 ;EWL - Document Storage System Inc - Gets Items to populate selection lists
 Q
GETSELS(AXY) ;RPC - DSIR GET SELECTION ITEMS
 ;Input Parameter(s) - NONE
 ;Return Value
 ; ARRAY:
 ;  IEN^FIELD NAME^X1^X2^X3^X4^X5^X6
 ;   WHERE X1=INCLUDE IN SELECT LISTS (Y/N)
 ;         X2=INCLUDE IN DATE LISTS (Y/N)
 ;         X3=INCLUDE IN PRINT FIELD LIST (Y/N)
 ;         X4=INCLUDE IN SORT BY LISTS (Y/N)
 ;         X5=SORT RANKING (0-999 LOWEST IS PRIORITY)
 ;         X6=IS DATE (Y/N)
 ;  -1 ^ UNABLE TO BUILD LISTMissing Provider Pointer!
 N IEN,INX,FNAME,FIL S IEN=0,FIL=19620.87
 F  S IEN=$O(^DSIR(FIL,IEN)) Q:'IEN  D
 .S FNAME=^DSIR(FIL,IEN,0)
 .S AXY(FNAME)=IEN_U_FNAME_U_^DSIR(FIL,IEN,1)
 Q
RPTLIST(AXY,RNAME) ; RPC - DSIR GET REPORT LIST
 ;INPUT PARAMETERS 
 ;  RNAME - (OPTIONAL) REPORT NAME FOR A SPECIFIC REPORT
 ;
 ;RETURN VALUE - ARRAYGET
 ; EACH SUBSCRIPT CONTAINS - "IEN^REPORT_NAME"
 ; OR -1^REPORT NOT FOUND  
 N IEN,RPTNAME,FI S FI=19620.871,IEN=0
 F  S IEN=$O(^DSIR(FI,IEN)) Q:'IEN  D
 .S RPTNAME=$P(^DSIR(FI,IEN,0),"^")
 .I '$D(RNAME) S AXY(RPTNAME)=IEN_U_RPTNAME
 .I ($D(RNAME)) D
 ..I RNAME=RPTNAME S AXY(RPTNAME)=IEN_U_RPTNAME
 I ($D(RNAME))&('$D(AXY)) S AXY="-1^REPORT NOT FOUND"
 Q
 ;
GETRPT(AXY,RPT) ; RPC - DSIR GET AD HOC REPORT
 ;Input Parameter(s) - RPT - IEN TO RETRIEVE
 ;Return Value
 ;ARRAY:- AS FOLLOWS:
 ; [1]  RPT NAME^RPT AUTHOR^RPT CREATION DATE^RPT LAST UPDATED
 ; [2]  RPT SELECTION FIELD 1^RPT SELECTION VALUE 1
 ; [3]  RPT SELECTION FIELD 2^RPT SELECTION VALUE 2
 ; [4]  RPT SELECTION FIELD 3^RPT SELECTION VALUE 3
 ; [5]  RPT DATE RANGE FIELD 1^RPT DATE RANGE FROM 1^
 ;      ===>RPT DATE RANGE TO 1
 ; [6]  RPT DATE RANGE FIELD 2^RPT DATE RANGE FROM 2^
 ;      ===>RPT DATE RANGE TO 2
 ; [7]  RPT SORT BY FIELD 1^ (#7.02) RPT SORT ASCENDING 1^
 ;      ===>RPT SORT GROUP 1^
 ;      ===>RPT SORT COUNT 1^RPT SORT SUM 1
 ; [8]  RPT SORT BY FIELD 2^RPT SORT ASCENDING 2^RPT SORT GROUP 2^
 ;      ===>RPT SORT COUNT 2 RPT SORT SUM 2
 ; [9]  RPT SORT BY FIELD 3^RPT SORT ASCENDING RPT SORT GROUP 3^
 ;      ===>RPT SORT COUNT 3 RPT SORT SUM 3
 ; [10] RPT TITLE^RPT TITLE REPEAT^RPT TITLE FONT^RPT TITLE SIZE^
 ;      ===>RPT TITLE BOLD^RPT TITLE ITALICS^RPT TITLE UNDERLN
 ; [11] RPT SUB TITLE^RPT SUB TITLE REPEAT^RPT SUB TITLE FONT^
 ;      ===>RPT SUB TITLE SIZE^RPT SUB TITLE BOLD^
 ;      ===>RPT SUB TITLE ITALICS^RPT SUB TITLE UNDERLN 
 ; [12] RPT FOOTER^^RPT FOOTER FONT^RPT FOOTER SIZE^
 ;      ===>RPT FOOTER BOLD^RPT FOOTER ITALICS^RPT FOOTER UNDERLN
 ; [13] RPT GROUP TITLE 1^RPT GROUP TITLE REPEAT 1^
 ;      ===>RPT GROUP TITLE FONT 1^RPT GROUP TITLE SIZE 1^
 ;      ===>RPT GROUP TITLE BOLD 1^RPT GROUP TITLE ITALICS 1^
 ;      ===>RPT GROUP TITLE UNDERLN 1
 ; [14] RPT GROUP FOOTER 1^^RPT GROUP FOOTER FONT 1^
 ;      ===>RPT GROUP FOOTER SIZE 1^RPT FOOTER BOLD 1^
 ;      ===>RPT GROUP FOOTER ITALACS 1^RPT GROUP FOOTER UNDERLN 1
 ; [15} RPT GROUP TITLE 2^RPT GROUP TITLE REPEAT 2
 ; [16} RPT GROUP FOOTER 2
 ; [17} RPT GROUP TITLE 3^RPT GROUP TITLE REPEAT 3
 ; [18} RPT GROUP FOOTER 3
 ; [19] RPT ORIENTATION
 ; [20] RPT BODY^RPT BODY MULTI LINE^RPT BODY FONT^RPT BODY SIZE^
 ;      ==>RPT BODY BOLD^RPT BODY ITALICS^RPT BODY UNDERLN
 ;  OR
 ; -1 ^ UNABLE TO GET SELECTED REPORT
 N IEN,FIL S FIL=19620.871,IEN=""
 I '$D(^DSIR(FIL,RPT,0)) S AXY="-1 ^ UNABLE TO GET SELECTED REPORT" Q 
 F INX=0:1:5 S:$D(^DSIR(FIL,RPT,INX)) AXY(INX)=^DSIR(FIL,RPT,INX) S:'$D(AXY(INX)) AXY(INX)=U
 F INX=7:1:20 S:$D(^DSIR(FIL,RPT,INX)) AXY(INX)=^DSIR(FIL,RPT,INX) S:'$D(AXY(INX)) AXY(INX)=U
 Q
SETAPT(PTR,REC) ; THIS ROUTINE IS USED TO SET THE "APT" INDEX ON 19620 FOR LOOKUPS
 ; THIS IS A MUMPS INDEX THAT IS PROGRAMMATICALLY UPDATED BASED ON THE VALUE IN THE .01 FIELD
 ; THE INDEX VALUES ARE AS FOLLOWS: 
 ; P - REGISTERED PATIENT REQUEST
 ; F - FOIA REQUEST
 ; N - NON-REGISTERED PATIENT REQUEST
 ; B - REGISTERED OR NON-REGISTERED PATIENT REQUEST
 ; INPUT PARAMETERS: 
 ; PTR = ZERO NODE FROM 19620 OR THE .01 FIELD (EITHER WILL WORK
 ; REC = IEN
 N TYP,T1
 S T1=$P($P(PTR,","),".",2)
 S TYP=$S(T1=95:"F",T1=96:"N",1:"P")
 I TYP="P" S ^DSIR(19620,"APT","P",REC)="",^DSIR(19620,"APT","B",REC)=""
 I TYP="N" S ^DSIR(19620,"APT","N",REC)="",^DSIR(19620,"APT","B",REC)=""
 I TYP="F" S ^DSIR(19620,"APT","F",REC)=""
 Q
KILLAPT(REC) ; THIS ROUTINE IS USED TO KILL "APT" INDEX ENTRIES ON 19620
 K ^DSIR(19620,"APT","P",REC),^DSIR(19620,"APT","B",REC),^DSIR(19620,"APT","F",REC)
 Q
