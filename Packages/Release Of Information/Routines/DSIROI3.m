DSIROI3 ;dss/blj,AMC/EWL - Release Of Information ;04/14/2011 11:18
 ;;8.2;RELEASE OF INFORMATION - DSSI;;Nov 08, 2011;Build 25
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;2056  $$GET1^DIQ
 ;2053  FILE^DIE
 ;10056 ^DIC(5,- STATE FILE READ ACCESS
 ;10009 FILE^DICN
 ;10103 $$FMADD^XLFD
 ;10103 $$NOW^XLFD
 ;10060 $$GET1^DIQ(200,duz,.01) Q
 ;
CHECKCUR(RETN,DFN,REQ) ; Check for previous requests RPC - DSIR CHECK PREV REQ
 ;
 ; Input: DFN: Patient IFN
 ;        REQ: Requestor IFN
 ;
 ; Output: Array of:
 ;        (1): Number of requests^
 ;        (2-n): Date of request^status^Clerk of record for request.
 ;
 N X,Z,CLRK,CNT,DATE,INC,STAT,CLDT
 S CNT=2,INC=""
 S DATE=$$FMADD^XLFDT($$NOW^XLFDT,-60)
 F  S INC=$O(^DSIR(19620,"B",DFN,INC)) Q:INC=""  D
 .I $P($G(^DSIR(19620,INC,1)),U)'=REQ Q
 .S CLRK=+$P($G(^DSIR(19620,INC,0)),U,3)
 .S OPDT=$P($G(^DSIR(19620,INC,10)),U,6)
 .S CLDT=$P($G(^DSIR(19620,INC,10)),U,7)
 .S STAT=$$CURSTAT^DSIROI6(INC),CLRK=$$GET1^DIQ(200,CLRK,.01)
 .;OLD CODE
 .;I $E(STAT)'="C" S RETN(CNT)=STAT_U_OPDT_U_CLRK_U_INC,CNT=CNT+1
 .;E  I OPDT>DATE S RETN(CNT)=STAT_U_OPDT_U_CLRK_U_INC,CNT=CNT+1
 .;
 .;8.1 CODE ADDED 3/3/11 EWL
 .I "OP"[$E(STAT)!(CLDT>DATE) S RETN(CNT)=STAT_U_OPDT_U_CLRK_U_INC,CNT=CNT+1
 .Q
 S RETN(1)=CNT-2_U
 Q
 ;
GETDATES(RETN,IFN) ; Get from/to dates for given request  RPC - DSIR GET DOC DATES
 ;
 ; Input: IFN of current ROI request
 ;
 ; Output:  Array of:  format FromDate^ToDate
 ;          Line 1: Meds
 ;          Line 2: Vitals
 ;          Line 3: Radiology
 ;          Line 4: Immunizations
 ;          Line 5: Consults
 ;          Line 6: Postings
 ;          Line 7: Discharge Summary
 ;          Line 8: Prog. Note
 ;          Line 9: Labs
 ;         Line 10: Surgery
 ;
 ;  In internal Fileman D/T format.
 S RETN(1)=$G(^DSIR(19620,IFN,"MED"))
 S RETN(2)=$G(^DSIR(19620,IFN,"VITALS"))
 S RETN(3)=$G(^DSIR(19620,IFN,"RADS"))
 S RETN(4)=$G(^DSIR(19620,IFN,"IMM"))
 S RETN(5)=$G(^DSIR(19620,IFN,"CONSLT"))
 S RETN(6)=$G(^DSIR(19620,IFN,"POSTS"))
 S RETN(7)=$G(^DSIR(19620,IFN,"DCGSUM"))
 S RETN(8)=$G(^DSIR(19620,IFN,"PRGNTS"))
 S RETN(9)=$G(^DSIR(19620,IFN,"LABS"))
 S RETN(10)=$G(^DSIR(19620,IFN,"SURG"))
 F X=1:1:10 S:RETN(X)'[U RETN(X)=RETN(X)_U
 Q
 ;
SAVEDATE(RETN,IFN,DATS) ; Saves date ranges for clinical documents for a request.  PRC - DSIR SET DOC DATES
 ;
 ; INPUT: IFN: Internal file number of request
 ;        DATS:  Array of FM Date/Times  format  FROM ^ TO
 ;        1) Medications
 ;        2) Vitals
 ;        3) Radiology
 ;        4) Immunology
 ;        5) Consults
 ;        6) Postings
 ;        7) Discharge Summaries
 ;        8) Progress Notes
 ;        9) Labs
 ;       10) Surgery
 N X,DIERR,ERR,ROOT,TMP S IFN=IFN_","
 F X=1:1:10 S DATS(X)=$TR(DATS(X),"~",U),TMP(X)=($G(DATS(X))!$P($G(DATS(X)),U,2))
 S:$D(DATS(1)) ROOT(19620,IFN,101)=$S(+DATS(1):+DATS(1)\1,1:$P(DATS(1),U)),ROOT(19620,IFN,102)=$S($P(DATS(1),U,2):$P(DATS(1),U,2)\1,1:$P(DATS(1),U,2))
 S:$D(DATS(2)) ROOT(19620,IFN,103)=$S(+DATS(2):+DATS(2)\1,1:$P(DATS(2),U)),ROOT(19620,IFN,104)=$S($P(DATS(2),U,2):$P(DATS(2),U,2)\1,1:$P(DATS(2),U,2))
 S:$D(DATS(3)) ROOT(19620,IFN,105)=$S(+DATS(3):+DATS(3)\1,1:$P(DATS(3),U)),ROOT(19620,IFN,106)=$S($P(DATS(3),U,2):$P(DATS(3),U,2)\1,1:$P(DATS(3),U,2))
 S:$D(DATS(4)) ROOT(19620,IFN,107)=$S(+DATS(4):+DATS(4)\1,1:$P(DATS(4),U)),ROOT(19620,IFN,108)=$S($P(DATS(4),U,2):$P(DATS(4),U,2)\1,1:$P(DATS(4),U,2))
 S:$D(DATS(5)) ROOT(19620,IFN,109)=$S(+DATS(5):+DATS(5)\1,1:$P(DATS(5),U)),ROOT(19620,IFN,110)=$S($P(DATS(5),U,2):$P(DATS(5),U,2)\1,1:$P(DATS(5),U,2))
 S:$D(DATS(6)) ROOT(19620,IFN,111)=$S(+DATS(6):+DATS(6)\1,1:$P(DATS(6),U)),ROOT(19620,IFN,112)=$S($P(DATS(6),U,2):$P(DATS(6),U,2)\1,1:$P(DATS(6),U,2))
 S:$D(DATS(7)) ROOT(19620,IFN,113)=$S(+DATS(7):+DATS(7)\1,1:$P(DATS(7),U)),ROOT(19620,IFN,114)=$S($P(DATS(7),U,2):$P(DATS(7),U,2)\1,1:$P(DATS(7),U,2))
 S:$D(DATS(8)) ROOT(19620,IFN,115)=$S(+DATS(8):+DATS(8)\1,1:$P(DATS(8),U)),ROOT(19620,IFN,116)=$S($P(DATS(8),U,2):$P(DATS(8),U,2)\1,1:$P(DATS(8),U,2))
 S:$D(DATS(9)) ROOT(19620,IFN,117)=$S(+DATS(9):+DATS(9)\1,1:$P(DATS(9),U)),ROOT(19620,IFN,118)=$S($P(DATS(9),U,2):$P(DATS(9),U,2)\1,1:$P(DATS(9),U,2))
 S:$D(DATS(10)) ROOT(19620,IFN,119)=$S(+DATS(10):+DATS(10)\1,1:$P(DATS(10),U)),ROOT(19620,IFN,120)=$S($P(DATS(10),U,2):$P(DATS(10),U,2)\1,1:$P(DATS(10),U,2))
 D:$D(ROOT) FILE^DIE("K","ROOT","ERR")
 I '$D(DIERR) S RETN(1)="1^"
 E  S RETN(1)=$$ERR^DSIROI0
 Q
 ;
ISREQSTR(RET,DFN) ; DSIR IS PATIENT REQUESTOR
 ;TEST FOR AN ENTRY IN THE "DPATIENT" INDEX
 ; INPUT  -  DFN - FULLY QUALIFIED POINTER TO ^DPT OR ^DSIR(19520.96
 ; RETURNS - IEN^LOOKUP NAME (.01 FIELD OF 19620.12)
 ;        OR -1^PATIENT NOT IN REQUESTOR TABLE    
 N IEN,QQ
 I $D(^DSIR(19620.12,"DPATIENT",DFN)) D  Q
 .S IEN=0,QQ=$NA(^DSIR(19620.12,"DPATIENT",DFN)),QQ=$Q(@QQ),IEN=$QS(QQ,4),RET=IEN_U_$P(^DSIR(19620.12,IEN,0),U)
 S RET="-1^PATIENT NOT IN REQUESTOR TABLE"
 Q
CKRQ(RET,DFN) ; DSIROI3 CKRQ CHK PAT REQUESTOR
 ;TEST FOR AN ENTRY IN THE "DPATIENT" INDEX
 ; INPUT  -  DFN - FULLY QUALIFIED POINTER TO ^DPT OR ^DSIR(19520.96
 ; RETURNS - ARRAY: IEN^LOOKUP NAME (.01 FIELD OF 19620.12)
 ;        OR -1^PATIENT NOT IN REQUESTOR TABLE
 N IEN,QQ
 I $D(^DSIR(19620.12,"DPATIENT",DFN)) D  Q
 .S IEN=0,QQ=$NA(^DSIR(19620.12,"DPATIENT",DFN))
 .F  S QQ=$Q(@QQ) Q:$QS(QQ,3)'=DFN  S IEN=$QS(QQ,4),RET(IEN)=IEN_U_$P(^DSIR(19620.12,IEN,0),U)
 S RET(1)="-1^PATIENT NOT IN REQUESTOR TABLE"
 Q
GTREQSTR(RETN,PREF,STYP) ; GET A LIST OF ALL REQUESTORS - RPC - DSIR GET REQUESTORS
 ;INPUT PARAMETERS
 ;  PREF - FIRST PART OF THE REQUESTOR NAME(S) BEING SEARCHED FOR
 ;  STYP - "L" - USE THE "AC" INDEX FOR LAST NAME
 ;         "C" - USE THE "AD" INDEX FOR CORPORATE NAME
 ;RETURNS:
 ; -1^NO DATA FOUND
 ; OR
 ; FULL_NAME^CORPORATE_NAME^CITY^STATE^PHONE^IEN^PATIENT POINTER
 S RETN=$NA(^TMP("DSIRREQUESTORS",$J)) K @RETN,^TMP("DSIRREQUESTORS",$J)
 N CUR,PR,LN,NXT,CT,DBL,INX,TMP,NODE2,NODE5 S CT=0
 S INX="AC" I STYP="C" S INX="AD"
 I $D(PREF) D 
 .S PR=1,NXT=$G(PREF),LN=$L(NXT)
 .S:LN>0 NXT=$E(NXT,1,LN-1)_$CHAR($ASCII($E(NXT,LN,LN))-1)_"~~~~~~"
 I '$D(PREF) S NXT=" "
 I PR D
 .F  S NXT=$O(^DSIR(19620.12,INX,NXT)) S CUR=$E(NXT,1,LN) Q:'(PREF=CUR)  D
 ..S DBL=0 F  S DBL=$O(^DSIR(19620.12,INX,NXT,DBL)) Q:'DBL  D
 ...S CT=CT+1,NODE1=^DSIR(19620.12,DBL,1)
 ...S NODE5=$G(^DSIR(19620.12,DBL,5)) Q:'NODE5 
 ...S ^TMP("DSIRREQUESTORS",$J,CT)=$$NXTRQSTR(NODE1,NODE5)_U_DBL_U_$P(^DSIR(19620.12,DBL,0),U,3)
 ...I STYP="C" S $P(^TMP("DSIRREQUESTORS",$J,CT),U)=""
 I 'PR D
 .F  S NXT=$O(^DSIR(19620.12,INX,NXT)) Q:'$D(NXT)  D
 ..S DBL=0 F  S DBL=$O(^DSIR(19620.12,INX,NXT,DBL)) Q:'DBL  D
 ...S CT=CT+1,NODE1=^DSIR(19620.12,DBL,1)
 ...S NODE5=^DSIR(19620.12,DBL,5)
 ...S ^TMP("DSIRREQUESTORS",$J,CT)=$$NXTRQSTR(NODE1,NODE5)_U_DBL_U_$P(^DSIR(19620.12,DBL,0),U,3)
 ...I STYP="C" S $P(^TMP("DSIRREQUESTORS",$J,CT),U)=""
 S RETN=$NA(^TMP("DSIRREQUESTORS",$J)) I CT=0 S @RETN@(1)="-1^NO DATA FOUND"
 Q
NXTRQSTR(N1,N5) ;
 N FULLNAME,CORPNAME,CITY,STATE,ST,ZIP,PHONE,APTR,TEMP
 S FULLNAME=$P(N1,U,3)_","_$P(N1,U)_" "_$P(N1,U,2)
 S CORPNAME=$P(N1,U,4)
 S APTR=$P(N5,U)
 S PHONE=$P($G(^DSIR(19620.92,APTR,1)),U)
 S CITY=$P($G(^DSIR(19620.92,APTR,0)),U,4)
 S STATE=$P($G(^DSIR(19620.92,APTR,0)),U,5)
 S ZIP=$P($G(^DSIR(19620.92,APTR,0)),U,6)
 I $G(STATE)]"" S TMP=$$GET1^DIQ(5,STATE_",",1)
 S TEMP=FULLNAME_U_CORPNAME_U_$G(CITY)_U_$G(ST)_U_$G(PHONE)
 Q TEMP
UPDRQSTR(AXY,DATA) ;RPC - DSIR ADD/EDIT REQUESTOR
 ; INPUT PARAMETER 
 ; DATA - ARRAY - This will be a multiple value parameter.
 ;  The values in order will be as follows: 
 ;  1. Requestor File Internal Number (Null if new OR Numeric)
 ;  2. Lookup Name - Text
 ;  3. Requestor type - Pointer to DSIR BILLING TYPE file (19620.8)
 ;  4. First Name
 ;  5. Middle Name
 ;  6. Last Name
 ;  7. Corporate Name
 ;  8. Salutation
 ;  9. E-mail
 ; 10. Primary address Pointer (IEN in 19620.92)
 ; 11. PATIENT POINTER (IE. "441;DPT(", "67:DSIR(19620.96,")
 ;
 ; RETURN VALUE
 ; Requestor file Internal Number
 ; OR
 ; -1^ERROR MESSAGE
 N RQSTRDAT,IEN,IENS,NEW,FI
 F CT=1:1:13 S DATA(CT)=$G(DATA(CT))
 S FI=19620.12,NEW=DATA(1)']""
 I DATA(2)']"" S AXY="-1^MISSING LOOK UP NAME, REQUESTOR CREATION ABORTED" Q
 I NEW D  I IEN<0 S AXY="-1^UNABLE TO CREATE NEW REQUESTOR" Q
 .S X=DATA(2),DIC="^DSIR(19620.12,",DIC(0)="L" D FILE^DICN S IEN=+Y
 S IEN=$G(IEN,$G(DATA(1))),IENS=IEN_","
 S:$G(DATA(2))]"" RQSTRDAT(FI,IENS,.01)=DATA(2)
 S:$G(DATA(3))]"" RQSTRDAT(FI,IENS,.02)=DATA(3)
 S:$G(DATA(4))]"" RQSTRDAT(FI,IENS,.11)=DATA(4)
 S:$G(DATA(5))]"" RQSTRDAT(FI,IENS,.12)=DATA(5)
 S:$G(DATA(6))]"" RQSTRDAT(FI,IENS,.13)=DATA(6)
 S:$G(DATA(7))]"" RQSTRDAT(FI,IENS,.14)=DATA(7)
 S:$G(DATA(8))]"" RQSTRDAT(FI,IENS,.15)=DATA(8)
 S:$G(DATA(9))]"" RQSTRDAT(FI,IENS,4.02)=DATA(9)
 S:$G(DATA(10))]"" RQSTRDAT(FI,IENS,5)=DATA(10)
 S:$G(DATA(11))]"" RQSTRDAT(FI,IENS,.03)=DATA(11)
 D FILE^DIE("","RQSTRDAT")
 S AXY=IEN
 Q
