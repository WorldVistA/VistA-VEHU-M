ONCSCHMM ;Hines OIFO/RTK - Miscellaneous schema code ;07/25/19
 ;;2.2;ONCOLOGY;**10,12**;Jul 31, 2013;Build 8
 ;
 Q
 ;
SCRNLV ;Screen for Lymph-Vascular Invasion field
 I $P($G(^ONCO(165.5,D0,0)),"^",16)<3180000,((Y<2)!(Y>7)) Q
 D ^ONCSCHMA I ('$D(ONCSCMA))!(ONCSCMA=0) Q
 I $P($G(^ONCO(165.5,D0,0)),"^",16)>3171231 D SCHMCHK
 I $P($G(^ONCO(165.5,D0,0)),"^",16)>3171231 I ONCNO8S=1,(Y'=8) Q
 I $P($G(^ONCO(165.5,D0,0)),"^",16)>3171231 I ONCONL8=1,(Y=8) Q
 I $P($G(^ONCO(165.5,D0,0)),"^",16)>3171231 I ONCNO8S=0,ONCONL8=0,Y<100 Q
 Q
SCHMCHK ;
 S ONCNO8S=0,ONCONL8=0
 I "00210^00220^00241^00242^00381^00382^00383^00400^00410^00421^00422^00440^00450^00480^00580^00600^00610^00631^00633^00640^00660^00671^00672^00700^00750^00090^00310^00360^00430^0073^00740^00760"[ONCSCMA Q  ;all codes apply
 I "00071^00072^00073^00074^00075^00076^00077^00080^00100^00111^00112^00121^00122^00130^00131^00132^00133^00161^00169^00170^00180^00190^00200^00230^00250"[ONCSCMA S ONCNO8S=1 Q  ;code 8 does not apply
 I "00260^00270^00280^00290^00301^00302^00320^00330^00340^00350^00460^00470^00500^00510^00520^00530^00541^00542^00560^00570^00590^00620"[ONCSCMA S ONCNO8S=1 Q  ;code 8 does not apply
 I "00060^00118^00119^00128^00140^00150^00278^00288^00358^00370^00378^00458^00478^00551^00552^00553^00558^00559^00598^00638"[ONCSCMA S ONCONL8=1 Q
 I "00650^00680^00690^00698^00710^00718^00721^00722^00723^00770^00778^00790^00795^00811^00812^00821^00822^00830^99999"[ONCSCMA S ONCONL8=1 Q  ;only 8s
 Q
SCRNSS ;Screen for SUMMARY STAGE 2018 (#1764) field
 N Z S Z=0 N ONCHIT S ONCHIT=0 D ^ONCSCHMA I ('$D(ONCSCMA))!(ONCSCMA=0) Q
 I "00370^00381^00382^00383^00400^00410^00421^00422^00440^00450^00458^00541^00680^00811"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=8):1,1:0) I Z Q
 I "00071^00072^00073^00074^00075^00076^00077^00360"[ONCSCMA S ONCHIT=1,Z=$S((Y'=8):1,1:0) I Z Q
 I "00060^"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=1)&(Y'=2)&(Y'=8):1,1:0) I Z Q
 I "00721^00722"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=3)&(Y'=4):1,1:0) I Z Q
 I "00723^"[ONCSCMA S ONCHIT=1,Z=$S((Y'=3)&(Y'=4):1,1:0) I Z Q
 I "00710^00790^00795^00812"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=3)&(Y'=4)&(Y'=8):1,1:0) I Z Q
 I "00821^00822"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=2)&(Y'=4)&(Y'=8):1,1:0) I Z Q
 I "00830^"[ONCSCMA S ONCHIT=1,Z=$S((Y'=0)&(Y'=2)&(Y'=3)&(Y'=4)&(Y'=8):1,1:0) I Z Q
 I "99999^"[ONCSCMA S ONCHIT=1,Z=$S((Y=9):1,1:0) I Z Q
 I ONCHIT=0 I ONCSCMA<999999 S Z=1 I Z Q
 Q
RADPH3 ;
 I $L(X)=3 Q
 S X=$E("000000",1,(3-$L(X)))_X
 Q
RADPH5 ;
 I $L(X)=5 Q
 S X=$E("000000",1,(5-$L(X)))_X
 Q
RADPH6 ;
 I $L(X)=6 Q
 S X=$E("000000",1,(6-$L(X)))_X
 Q
DEC1 ;Input transforms & decimals for SSDi's to accept XX & decimals
 S X=$TR(X,"ax","AX")
 I X?1N S X=X_".0" Q
 I X?1"."1N S X="0"_X Q
 D XX1 Q
XX1 ;
 I X?1"X"1N S X="X."_$E(X,$L(X)) Q
 Q
DEC2 ;
 S X=$TR(X,"ax","AX")
 I X?1.2N S X=X_".0" Q
 I X?1"."1N S X="0"_X Q
 D XX2 Q
XX2 ;
 I X?1.2"X"1N S X="XX."_$E(X,$L(X)) Q
 Q
DEC3 ;
 S X=$TR(X,"ax","AX")
 I X?1.3N S X=X_".0" Q
 I X?1"."1N S X="0"_X Q
 D XX3 Q
XX3 ;
 I X?1.3"X"1N S X="XXX."_$E(X,$L(X)) Q
 Q
DEC4 ;
 S X=$TR(X,"ax","AX")
 I X?1.4N S X=X_".0" Q
 I X?1"."1N S X="0"_X Q
 D XX4 Q
XX4 ;
 I X?1.4"X"1N S X="XXXX."_$E(X,$L(X)) Q
 Q
DEC5 ;
 S X=$TR(X,"ax","AX")
 I X?1.5N S X=X_".0" Q
 I X?1"."1N S X="0"_X Q
 I X?6N K X Q
 D XX5 Q
XX5 ;
 I X?1.5"X"1N S X="XXXXX."_$E(X,$L(X)) Q
 Q
GEN2 ;
 S X=$TR(X,"x","X")
 I "ABCDEFGHIJKLMNOPQRSTUVWYZabcdefghijklmnopqrstuvwyz"[$E(X,1) K X Q
 I X?1N S X="0"_X
 Q
GEN3 ;
 S X=$TR(X,"x","X")
 I X?1N S X="00"_X
 I X?2N S X="0"_X
 Q
BTTIT ;Breslow Tumor Thickness Input Transform #3817
 D DEC2
 I '(X?0.2AN0.1"."1N) K X Q
 I "0123456789.AX"'[$E(X,1) K X Q
 I $E(X,1,2)="AX" S X="AX.0" Q
 Q
ERR ;ER & PR Percent Positive or Range #3826 & #3914
 S X=$TR(X,"rx","RX")
 I "0123456789RX"'[$E(X,1) K X Q
 I X?1N S X="00"_X W "  ...",X
 I X?2N S X="0"_X W "  ...",X
 I X?1"X"1N S X="X"_X W "  ...",X
 I $L(X)<3 K X Q
 I $E(X,1)="R","R10^R20^R30^R40^R50^R60^R70^R80^R90^R99"'[X K X Q
 I $E(X,1)="X","XX8^XX9"'[X K X Q
 Q
ERTA ;ER & PR Allred Score #3828 & #3916
 S X=$TR(X,"x","X")
 I X?1N S X="0"_X W "  ...",X
 I "00^01^02^03^04^05^06^07^08^X8^X9"'[X K X Q
 Q
SAR ;Sarcomtoid Features #3925
 S X=$TR(X,"rx","RX")
 I "0123456789RX"'[$E(X,1) K X Q
 I X?1N S X="00"_X W "  ...",X
 I X?2N S X="0"_X W "  ...",X
 I X?1"X"1N S X="X"_X W "  ...",X
 I X?1"R"1N S X=$E(X,1)_"0"_$E(X,2) W "  ...",X
 I $L(X)<3 K X Q
 I $E(X,1)="R","R01^R02^R03^R40^R05"'[X K X Q
 I $E(X,1)="X","XX6^XX7^XX8^XX9"'[X K X Q
 Q
FIGO ;FIGO #3836
 I ((X>42)&(X<97)) K X Q
 I ((X>11)&(X<20)) K X Q
 I ((X>24)&(X<30)) K X Q
 I X?1N S X="0"_X
 Q
FIGOOT ;
 I Y="01" S Y="01   FIGO Stage I"
 I Y="02" S Y="02   FIGO Stage IA"
 I Y="03" S Y="03   FIGO Stage IA1"
 I Y="04" S Y="04   FIGO Stage IA2"
 I Y="05" S Y="05   FIGO Stage IB"
 I Y="06" S Y="06   FIGO Stage IB1"
 I Y="07" S Y="07   FIGO Stage IB2"
 I Y="08" S Y="08   FIGO Stage IC"
 I Y="09" S Y="09   FIGO Stage IC1"
 I Y=10 S Y="10   FIGO Stage IC2"
 I Y=11 S Y="11   FIGO Stage IC3"
 I Y=20 S Y="20   FIGO Stage II"
 I Y=21 S Y="21   FIGO Stage IIA"
 I Y=22 W !,"22   FIGO Stage IIA1"
 I Y=23 S Y="23   FIGO Stage IIA2"
 I Y=24 S Y="24   FIGO Stage IIB"
 I Y=30 S Y="30   FIGO Stage III"
 I Y=31 S Y="31   FIGO Stage IIIA"
 I Y=32 S Y="32   FIGO Stage IIIA1"
 I Y=33 W !,"33   FIGO Stage IIIAi"
 I Y=34 S Y="34   FIGO Stage IIIAii"
 I Y=35 S Y="35   FIGO Stage IIIA2"
 I Y=36 S Y="36   FIGO Stage IIIB"
 I Y=37 S Y="37   FIGO Stage IIIC"
 I Y=38 S Y="38   FIGO Stage IIIC1"
 I Y=39 S Y="39   FIGO Stage III2"
 I Y=40 S Y="40   FIGO Stage IV"
 I Y=41 S Y="41   FIGO Stage IVA"
 I Y=42 S Y="42   FIGO Stage IVB"
 I Y=97 S Y="97   Not applicable: Carcinoma in situ"
 I Y=98 S Y="98   Not applicable: Information not collected for this case"
 I Y=99 S Y="99   Not documented in medical record"
 Q
PROPE ;
 I X'?3N K X Q
 I "000^300^350^400^500^600^700^800^900^950^999"'[X K X Q
 Q
QTEST ;
 F GIEN=0:0 S GIEN=$O(^ONCO(164.44,GIEN)) Q:GIEN'>0  D
 .F JIEN=0:0 S JIEN=$O(^ONCO(164.44,GIEN,1,JIEN)) Q:JIEN'>0  D
 ..W !,GIEN,?3,JIEN
 ..S ONCRTKID=$P($G(^ONCO(164.44,GIEN,1,JIEN,0)),"^",3)
 ..S ONCRTKCH=$P($G(^ONCO(164.44,GIEN,1,JIEN,0)),"^",5)
 ..S ONCRTKNM=$P($G(^ONCO(164.44,GIEN,1,JIEN,0)),"^",4)
 ..W ?6,ONCRTKID,?11,ONCRTKCH,?16,ONCRTKNM
 ..I ONCRTKID'="" S ONCRTK("AJCCID",ONCRTKID)=""
 ..I ONCRTKCH'="" S ONCRTK("AJCCCH",ONCRTKCH)=""
 Q
