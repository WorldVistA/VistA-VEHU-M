DVBSECID ;NTX/JCS - CAPRI SECID RPC; July 2nd, 2024@16:40 ; 7/2/24 4:40pm
 ;;2.7;AMIE;**252**;July 2nd 2024;Build 92
 Q
 ;
GETSECID(DVBRTN,DVBDUZ) ;RPC: DVBA CAPRI GET SECID;CAPRI-11679;JCS
 I $G(DVBDUZ)="" D  Q
 . S DVBRTN="-1"_U_"User's DUZ is required."
 I '$D(^VA(200,DVBDUZ)) D  Q
 . S DVBRTN="-1"_U_"User's DUZ does not exist."
 N DVBDATA,DVBTDUZ,DVBI
 D GETS^DIQ(200,DVBDUZ,"205.1;205.2;205.3;205.4;205.5","I","DVBDATA")
 S DVBTDUZ=DVBDUZ_","
 I $G(DVBDATA(200,DVBTDUZ,205.1,"I"))="" D  Q
 . S DVBRTN="-1"_U_"User's SecID does not exist."
 S DVBRTN="1"
 F DVBI=205.1:.1:205.5 S DVBRTN=DVBRTN_U_$G(DVBDATA(200,DVBTDUZ,DVBI,"I"))
 Q
