DG53932P ;ISP/RFR - PATCH 932 POST-INIT;May 21, 2018@16:23
 ;;5.3;Registration;**932**;Aug 13, 1993;Build 210
 ;RECOMPILE TEMPLATES WITH NEW INDEXES
 D BMES^XPDUTL("  Recompiling input templates...")
 N LINENUM,LINE,FILE,FIELD,FIELDS,TEMPLATE,TEMPLTS,X,Y,DMAX,MESSAGE
 F LINENUM=1:1 S LINE=$T(DATA+LINENUM) Q:LINE'[";;"  D
 .I LINE["FILE" S FILE=$P(LINE,U,2) Q
 .Q:$G(FILE)=""
 .S FIELD=$P(LINE,";;",2) Q:FIELD=""
 .S FIELDS(FILE,FIELD)=""
 S FILE=0 F  S FILE=$O(FIELDS(FILE)) Q:'+FILE  S FIELD=0 F  S FIELD=$O(FIELDS(FILE,FIELD)) Q:'+FIELD  D
 .S TEMPLATE=0 F  S TEMPLATE=$O(^DIE("AF",FILE,FIELD,TEMPLATE)) Q:'+TEMPLATE  S TEMPLTS(TEMPLATE)=""
 S TEMPLATE=0 F  S TEMPLATE=$O(TEMPLTS(TEMPLATE)) Q:'+TEMPLATE  D
 .S Y=TEMPLATE,DMAX=$$ROUSIZE^DILF
 .S X=$P($G(^DIE(Y,"ROU")),U,2) Q:X=""
 .D EN^DIEZ
 D MES^XPDUTL("    DONE")
 D BMES^XPDUTL("  Scheduling option DG PTF ICD CODE NOTIFIER...")
 N ERROR,RESCHED,WHEN
 S RESCHED=15 ;TASK SHOULD RUN EVERY RESCHED MINUTES
 S WHEN=$P($$FMTE^XLFDT($$FMADD^XLFDT($$NOW^XLFDT,0,0,RESCHED)),":",1,2)
 D RESCH^XUTMOPT("DG PTF ICD CODE NOTIFIER",WHEN,,(60*RESCHED)_"S","L",.ERROR)
 I $G(ERROR)=-1 D MES^XPDUTL("    FAILED; manually schedule the option to run every "_RESCHED_" minutes")
 I '$D(ERROR) D MES^XPDUTL("    DONE")
 Q
DATA ;LIST OF FILES AND FIELDS MODIFIED BY THIS PATCH
 ;;FILE^45
 ;;79
 ;;79.16
 ;;79.17
 ;;79.18
 ;;79.19
 ;;79.201
 ;;79.21
 ;;79.22
 ;;79.23
 ;;79.24
 ;;79.241
 ;;79.242
 ;;79.243
 ;;79.244
 ;;79.245
 ;;79.246
 ;;79.247
 ;;79.248
 ;;79.249
 ;;79.2491
 ;;79.24911
 ;;79.24912
 ;;79.24913
 ;;79.24914
 ;;79.24915
 ;;80
 ;;FILE^45.02
 ;;5
 ;;6
 ;;7
 ;;8
 ;;9
 ;;11
 ;;12
 ;;13
 ;;14
 ;;15
 ;;81.01
 ;;81.02
 ;;81.03
 ;;81.04
 ;;81.05
 ;;81.06
 ;;81.07
 ;;81.08
 ;;81.09
 ;;81.1
 ;;81.11
 ;;81.12
 ;;81.13
 ;;81.14
 ;;81.15
 ;;FILE^45.06
 ;;.04
 ;;FILE^46
 ;;.04
 ;;.05
 ;;.06
 ;;.07
 ;;.21
 ;;.22
 ;;.23
 ;;.24
 Q
