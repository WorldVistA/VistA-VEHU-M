DGAUDIT2 ;PER/LAB,ISL/DKA - Patient Lookup Audit Report for VAS ;May 17, 2021@12:09
 ;;5.3;Registration;**964**;Aug 13, 1993;Build 323
 ;;Per VA Directive 6402, this routine should not be modified.
 ;
 ; Reference to FILE^DICN in ICR #10009
 ;
 Q  ; No entry from top
 ;
SELAUD(DGVFILNO,DGVDFN,REQTYP,DGOPT) ;Audit Patient records at selection
 ; VistA Security Remediation
 ; When a patient is manually selected to be viewed or edited, an
 ; audit record will be created. 
 Q:$G(DGVDFN)=""   ; if new patient, cannot display information
 Q:$$ANON^DGAUDIT1(DUZ)  ; anonymous/proxy user don't continue
 S DGVDFN=+DGVDFN
 Q:'$$FIND1^DIC(2,"","A","`"_DGVDFN)
 ;
 ; D CHKSIZE^DGAUDIT,SNDMSG^DGAUDIT(.DGAUDERR) 2/23 took out cannot add here as this will be checked at install and not let the full dia global be populated
 N DA,DIC,NINUM,DO,DTOUT,DUOUT,INDX,LFLAG,MENUOPT,X,DGVECNT,Y,DIY,DIX
 S MENUOPT=$S($L($P($G(DGOPT),"^")):$P($G(DGOPT),"^"),$L($P($G(DGOPT),"^",2)):$P($G(DGOPT),"^",2),$L($P($G(XQY0),"^")):$P($G(XQY0),"^"),1:"UNKNOWN/NON-STANDARD")
 ;
 D PROCJSON(REQTYP,DGVDFN,$G(DUZ),$G(MENUOPT),$G(DGVFILNO),$G(DUZ(2)))
 Q
 ;
 ;PROCJSON(DGAUDECNT) ; Create the JSON from the individual fields in the DGAUD AUDIT QUEUE record
PROCJSON(REQTYP,DGVDFN,DUZ,MENUOPT,DGVFILNO,DUZ2) ; Create the JSON from the individual fields in the DGAUD AUDIT QUEUE record
 ; ISL/DKA This copy is for playing around with using only FileMan calls to get the data
 N D,D0,DA,DI,DIC,DIE,DIERR,DO,DQ,DR,X,DGARR,DGAUDARR,DGAUDDATA,DGAUDDFN,DGAUDDT,DGAUDDUZ,DGAUDERR
 N DGAUDFILE,DGAUDFILNO,DGAUDJSON,DGAUDOFFN,DGAUDREF,DGAUDSITEN,DGAUDSTANUM,Y,DGFDA,DGMVI,DGERR
 N DGFDA,DGNOWDTM,DGAUDERR,DGAUDECNT,DGFILNAME
 ;
 S DGAUDECNT="+1"
 S DGNOWDTM=$$NOW^XLFDT
 D FILE^DID(DGVFILNO,,"NAME","DGFILNAME","DGERR")
 ;
 S DGFDA(46.3,DGAUDECNT_",",.01)=DGNOWDTM
 ;
 S DGAUDDATA(46.3,DGAUDECNT_",",.02,"E")=$G(REQTYP)
 S DGAUDDATA(46.3,DGAUDECNT_",",.03,"E")=$$FMTE^XLFDT(DGNOWDTM,2)
 S DGAUDDATA(46.3,DGAUDECNT_",",.04,"E")=$$GET1^DIQ(2,+$G(DGVDFN),.01)
 S DGAUDDATA(46.3,DGAUDECNT_",",.05,"E")=$$GET1^DIQ(200,$G(DUZ),.01)
 S DGAUDDATA(46.3,DGAUDECNT_",",.06,"E")=$G(MENUOPT)
 S DGAUDDATA(46.3,DGAUDECNT_",",.07,"E")=$G(DGFILNAME("NAME"),"UNKNOWN")
 S DGAUDDATA(46.3,DGAUDECNT_",",.08,"E")=$$GET1^DIQ(4,+$G(DUZ2),.01)
 ;
 S DGAUDDATA(46.3,DGAUDECNT_",",.02,"I")=$E($G(REQTYP))
 S DGAUDDATA(46.3,DGAUDECNT_",",.03,"I")=DGNOWDTM
 S DGAUDDATA(46.3,DGAUDECNT_",",.04,"I")=DGVDFN
 S DGAUDDATA(46.3,DGAUDECNT_",",.05,"I")=$G(DUZ)
 S DGAUDDATA(46.3,DGAUDECNT_",",.06,"I")=$$FIND1^DIC(19,,,$G(MENUOPT))
 S DGAUDDATA(46.3,DGAUDECNT_",",.07,"I")=$G(DGVFILNO)
 S DGAUDDATA(46.3,DGAUDECNT_",",.08,"I")=DUZ2
 ;
 S DGFDA(46.3,DGAUDECNT_",",.02)=$E($G(REQTYP))
 S DGFDA(46.3,DGAUDECNT_",",.03)=DGNOWDTM
 S DGFDA(46.3,DGAUDECNT_",",.04)=$G(DGVDFN)
 S DGFDA(46.3,DGAUDECNT_",",.05)=$G(DUZ)
 S DGFDA(46.3,DGAUDECNT_",",.06)=DGAUDDATA(46.3,DGAUDECNT_",",.06,"I")
 S DGFDA(46.3,DGAUDECNT_",",.07)=$G(DGVFILNO)
 S DGFDA(46.3,DGAUDECNT_",",.08)=DUZ2
 ;
 ;
 S DGARR=$NA(DGAUDARR("data","HEADER"))
 D GETS^DIQ(46.3,DGAUDECNT,"*","IE","DGAUDDATA")
 ; Set DGAUDREF for use with indirection below to get each individual field from the returned array.
 S DGAUDREF=$NA(DGAUDDATA(46.3,DGAUDECNT_","))
 ; 
 S @DGARR@("SchemaType")="FMAUDIT"
 S @DGARR@("RequestType")=@DGAUDREF@(.02,"E")
 S @DGARR@("DateTime")=$$FMTHL7^XLFDT(@DGAUDREF@(.03,"I"))
 S DGAUDDT=@DGAUDREF@(.03,"I")
 S @DGARR@("Week")=$$WEEK^DGAUDIT1(DGAUDDT)
 S @DGARR@("Year")=+$$FMTE^XLFDT(DGAUDDT,7)
 ;
 S DGARR=$NA(DGAUDARR("data","HEADER","Patient"))
 S (DGAUDDFN,@DGARR@("DFN"))=@DGAUDREF@(.04,"I")
 S DGMVI=$$GETICN^MPIF001(DGAUDDFN),DGMVI=$S(DGMVI>0:DGMVI,1:"") ;$$GET1^DIQ(2,DGAUDDFN,991.1)
 S @DGARR@("MVI")=$S($L(DGMVI):DGMVI,1:"null")
 S @DGARR@("PatientName")=@DGAUDREF@(.04,"E")
 S @DGARR@("SSN")=$$GET1^DIQ(2,DGAUDDFN,.09)
 S @DGARR@("INITPLUS4")=$$GET1^DIQ(2,DGAUDDFN,.0905)
 S @DGARR@("DOB")=$$FMTHL7^XLFDT($$GET1^DIQ(2,DGAUDDFN,.03,"I"))
 ;
 S DGARR=$NA(DGAUDARR("data","HEADER","User"))
 S (DGAUDDUZ,@DGARR@("DUZ"))=@DGAUDREF@(.05,"I")
 S @DGARR@("UID")=$$GET1^DIQ(200,DGAUDDUZ,205.4)
 S @DGARR@("UserName")=$$GET1^DIQ(200,DGAUDDUZ,.01)
 S @DGARR@("Title")=$$GET1^DIQ(200,DGAUDDUZ,8)
 ;
 S DGARR=$NA(DGAUDARR("data","HEADER","Location"))
 S DGAUDSITEN=@DGAUDREF@(.08,"I")
 I DGAUDSITEN="",DGAUDDUZ'="" S DGAUDSITEN=$O(^VA(200,DGAUDDUZ,2,"AX1",1,"")) ; Checking to make sure that DGAUDDUZ is not null to avoid a subscript FLS
 S:DGAUDSITEN="" DGAUDSITEN=$$GET1^DIQ(8989.3,1,217,"I")
 S DGAUDOFFN=$$GET1^DIQ(4,DGAUDSITEN,100)
 S @DGARR@("Site")=$S(DGAUDOFFN'="":DGAUDOFFN,1:@DGAUDREF@(.08,"E"))
 S DGAUDSTANUM=$$GET1^DIQ(4,DGAUDSITEN,99)
 I DGAUDSTANUM="" S DGAUDSTANUM=$$GET1^DIQ(4,$$GET1^DIQ(8989.3,1,217,"I"),99)
 S @DGARR@("StationNumber")=DGAUDSTANUM
 ;
 S DGARR=$NA(DGAUDARR("data","SCHEMA"))
 S (DGAUDFILNO,@DGARR@("FILE NUMBER"))=@DGAUDREF@(.07,"I")
 S @DGARR@("FILE NAME")=@DGAUDREF@(.07,"E")
 S @DGARR@("ACCESSED")="INQUIRED TO ENTRY"
 S @DGARR@("MENU OPTION USED")=@DGAUDREF@(.06,"E")
 ;
 S DGARR=$NA(DGAUDARR("id"))
 S @DGARR=+$G(DUZ)_"."_+$G(DGVDFN)_"."_DGNOWDTM
 ;
 D ENCODE^XLFJSON("DGAUDARR","DGAUDJSON","DGAUDERR")
 S DGFDA(46.3,DGAUDECNT_",",1)=DGAUDJSON(1)
 D UPDATE^DIE("","DGFDA","","RESULT")
 Q
