DGPTDD ;ALB/LD - DD CALLS FOR PTF (#45) FILE;Dec 19, 2019@16:19
 ;;5.3;Registration;**58,932**;Aug 13, 1993;Build 210
 ;
 ; DD calls for the Suffix and Transferring Suffix fields of PTF
 ; file (#45).
 ;
ACTIVE(X,Y,DGADM) ; Suffix active during patient's admission date?
 ;
 ;         DGEFDT  -- Suffix Effective Date
 ;         DGEFIEN -- Suffix Effective Date IEN
 ;        DGSUFPTR -- Suffix pointer from Station Type file
 ;
 ;  INPUT:     X   --  Suffix
 ;             Y   --  Station Type Number
 ;         DGADM   --  PTF IEN (use to get 2nd piece which is
 ;                              admission date or use DT if null)
 ; OUTPUT: DGACT   --  Active during admission date? (1=YES,0=NO)
 ;
 N DGACT,DGEFDT,DGEFIEN,DGFL,DGSUFPTR,DGI
 S (DGACT,DGEFIEN,DGEFDT,DGFL,DGSUFPTR)=0
 F DGI=0:0 S DGI=$O(^DIC(45.81,+$G(Y),"S","B",DGI)) Q:'DGI!$G(DGFL)  D
 .I $P($G(^DIC(45.68,DGI,0)),U)=$G(X) S DGSUFPTR=DGI,DGFL=1
 I $D(^DGPT(+$G(DGADM),0)) S DGADM=+$P(^(0),U,2)
 S DGADM=$S(+$G(DGADM)>0:-DGADM,1:-DT) S:$P(DGADM,".",2) DGADM=$P(DGADM,".") S DGADM=DGADM_.2359
 S DGEFDT=+$O(^DIC(45.68,DGSUFPTR,"E","AEFF",DGADM))
 I -(DGEFDT)'>0 S DGEFDT=+$O(^DIC(45.68,DGSUFPTR,"E","B",DGEFDT)),DGEFDT=-DGEFDT
 S DGEFIEN=$O(^DIC(45.68,DGSUFPTR,"E","AEFF",DGEFDT,DGEFIEN))
 S DGACT=$P($G(^DIC(45.68,+DGSUFPTR,"E",+DGEFIEN,0)),U,2)
 Q +$G(DGACT)
 ;
ACTLST(DGADM) ;    List of active suffixes
 ;
 ;          DGEFFDT -- Suffix Effective Date
 ;         DGEFFIEN -- Suffix Effective Date IEN
 ;
 ;  INPUT:     DGADM  --  PTF IEN (use to get 2nd piece which is
 ;                                 admission date or use DT if null)
 ; OUTPUT:     List of active suffixes during admission date
 ;
 N DGCTR,DGEFFDT,DGEFFIEN,DGI,DGOUT,DGST,DGX,DGY
 S (DGEFFDT,DGOUT)=0,DGCTR=1
 I $D(^DGPT(+$G(DGADM),0)) S DGADM=+$P(^(0),U,2)
 S DGADM=$S(+$G(DGADM)>0:-DGADM,1:-DT) S:$P(DGADM,".",2) DGADM=$P(DGADM,".")
 F DGST=0:0 S DGST=$O(^DIC(45.81,"B",DGST)) Q:'DGST  D
 .F DGI=0:0 S DGI=$O(^DIC(45.81,DGST,"S","B",DGI)) Q:'DGI  D
 ..S DGEFFDT=+$O(^DIC(45.68,DGI,"E","AEFF",DGADM))
 ..I -(DGEFFDT)'>0 S DGEFFDT=$O(^DIC(45.68,DGI,"E","B",DGEFFDT)),DGEFFDT=-DGEFFDT
 ..S DGEFFIEN=0,DGEFFIEN=$O(^DIC(45.68,DGI,"E","AEFF",DGEFFDT,DGEFFIEN))
 ..S:$P($G(^DIC(45.68,DGI,"E",+DGEFFIEN,0)),U,2)=1 ^TMP("ACTSUFF",$J,DGCTR)=$P($G(^DIC(45.68,DGI,0)),U)_U_$P($G(^DIC(45.81,DGST,0)),U,2),DGCTR=DGCTR+1
 W @IOF,"Choose From:",!
 F DGX=0:0 S DGX=$O(^TMP("ACTSUFF",$J,DGX)) Q:'DGX!($G(DGOUT))  D
 .I $Y>(IOSL-5) D NEXTSCR
 .W:'$G(DGOUT) !,$P($G(^TMP("ACTSUFF",$J,DGX)),U),?15,$P($G(^TMP("ACTSUFF",$J,DGX)),U,2)
 K ^TMP("ACTSUFF")
 Q
NEXTSCR ;
 F DGY=$Y:1:(IOSL-4) W !
 S DIR(0)="E" D ^DIR K DIR I $D(DIRUT)!($D(DUOUT)) S DGOUT=1 K DIRUT,DTOUT,DUOUT G NEXTSCRQ
 W @IOF,"Choose From:",!
NEXTSCRQ ;
 Q
 ;=============================================
 ;QUEUE DATA CHANGE MESSAGES WHEN AN ICD
 ;DIAGNOSIS CODE IS RECORDED, EDITED OR DELETED
NOTIFY(OLDVAL,NEWVAL,DA,TYPE,FIELD) ;
 N IEN,RECTYPE,DFN,SUB,EXIT,SAVE,NODE,NAME,CACHE
 S IEN=$$IENS^DILF(.DA),IEN("TOP")=$P(IEN,",",$L(IEN,",")-1)
 Q:+IEN<1
 S TYPE=$G(TYPE),FIELD=$G(FIELD)
 I TYPE="DISCHARGE" S DFN=$G(NEWVAL(1)),RECTYPE=$G(NEWVAL(2)),SUB=3
 I (TYPE="MOVEMENT")!(TYPE="SERVICE") D
 .S DFN=$P($G(^DGPT(IEN("TOP"),0)),U)
 .S RECTYPE=$P($G(^DGPT(IEN("TOP"),0)),U,11),SUB=1
 I TYPE="SERVICE46",$G(NEWVAL(1))>0 D
 .S DFN=$P($G(^DGPT(NEWVAL(1),0)),U)
 .S RECTYPE=$P($G(^DGPT(NEWVAL(1),0)),U,11),SUB=2,IEN("TOP")=NEWVAL(1)
 S CACHE="DG PTF ICD NOTIFIER CACHE"
 I $D(OLDVAL)>9,$D(NEWVAL)>9 D  Q:$G(EXIT)
 .;DO NOT SEND NOTIFICATION FOR CENSUS RECORD (RECTYPE=2)
 .I (+$G(DFN)<1)!($G(RECTYPE)="")!($G(RECTYPE)=2) S EXIT=1 Q
 .I (FIELD="")!('+$G(SUB)) S EXIT=1 Q
 .I OLDVAL(SUB)=NEWVAL(SUB) S EXIT=1 Q
 .I $D(^TMP("DG PTF DRIVER",$J)) D
 ..S SAVE=$NA(^XTMP(CACHE,IEN("TOP"))),EXIT=1
 ..S ^XTMP(CACHE,0)=$$FMADD^XLFDT(DT,5)_U_DT_U_"PTF ICD DIAGNOSIS CODE CACHE"
 .I '$D(^TMP("DG PTF DRIVER",$J)) D
 ..S SAVE=$NA(^TMP("DG PTF ICD NOTIFIER",$J))
 ..K @SAVE
 .S @SAVE@(TYPE,FIELD,"OLD")=$G(OLDVAL(SUB))
 .S @SAVE@(TYPE,FIELD,"NEW")=$G(NEWVAL(SUB))
 .S @SAVE@(TYPE,"IENS")=IEN,@SAVE@("DFN")=DFN
 .S @SAVE@("INTEREST DATE")=$P($G(^DGPT(IEN("TOP"),0)),U,2)
 .S @SAVE@("OCCURRED DATE")=$$NOW^XLFDT
 .N VAIN,VAERR,INST
 .D INP^VADPT
 .Q:+$G(VAERR)
 .I +$G(VAIN(2))>0 S @SAVE@("PRIMARY PROVIDER")=+VAIN(2)
 .I +$G(VAIN(11))>0 S @SAVE@("ATTENDING PHYSICIAN")=+VAIN(11)
 .S INST=+VAIN(4) Q:INST=0
 .S INST=+$P($G(^DIC(42,INST,0)),U,11) Q:INST=0
 .S INST=+$P($G(^DG(40.8,INST,0)),U,7) Q:INST=0
 .S @SAVE@("INSTITUTION")=INST
 S NAME="DG PTF ICD NOTIFIER",NODE=$J_" "_$$NOW^XLFDT
 F  Q:'$D(^XTMP(NAME,NODE))  I $D(^XTMP(NAME,NODE)) H 1 S NODE=$J_" "_$$NOW^XLFDT
 K ^XTMP(NAME,NODE)
 S ^XTMP(NAME,0)=$$FMADD^XLFDT(DT,3)_U_DT_U_"PTF ICD NOTIFIER DATA"
 I $D(^XTMP(CACHE,IEN("TOP"))) D
 .M ^XTMP(NAME,NODE)=^XTMP(CACHE,IEN("TOP"))
 .K ^XTMP(CACHE,IEN("TOP"))
 .I $O(^XTMP(CACHE,0))="" K ^XTMP(CACHE)
 I $D(^TMP("DG PTF ICD NOTIFIER",$J)) D
 .M ^XTMP(NAME,NODE)=^TMP("DG PTF ICD NOTIFIER",$J)
 .K ^TMP("DG PTF ICD NOTIFIER",$J)
 Q
 ;
CPTCNTRL(OLDVAL,NEWVAL,DA,TYPE) ;MONITOR DELETIONS OF CPT RECORDS
 N PIECE,NODE0,NODE1,X1,X2,FIELD,OLD,NEW
 S TYPE=$G(TYPE)
 Q:$G(OLDVAL(1))=$G(NEWVAL(1))
 I TYPE="SERVICE" D
 .;DELETED
 .I $G(NEWVAL(1))=1 S OLD="X1",NEW="X2"
 .;UNDELETED
 .I +$G(NEWVAL(1))=0 S OLD="X2",NEW="X1"
 .Q:$G(OLD)=""
 .I $G(DA(1))>0 S @OLD@(1)=$P($G(^DGPT(DA(1),"C",DA,0)),U,4)
 .Q:@OLD@(1)=""
 .S @NEW@(1)="",FIELD="PDX"
 .D NOTIFY(.X1,.X2,.DA,TYPE,FIELD)
 I TYPE="SERVICE46" D
 .;DELETED
 .I +$G(NEWVAL(1))>0 S OLD="X1",NEW="X2"
 .;UNDELETED
 .I $G(NEWVAL(1))="" S OLD="X2",NEW="X1"
 .Q:$G(OLD)=""
 .S NODE0=$G(^DGCPT(46,DA,0)),NODE1=$G(^(1))
 .F PIECE=4:1:7,15:1:18 I $P(NODE0,U,PIECE)'="" D
 ..S @OLD@(1)=$P(NODE1,U,3),@NEW@(1)=@OLD@(1)
 ..S @OLD@(2)=$P(NODE0,U,PIECE),@NEW@(2)=""
 ..S FIELD=$S(PIECE=4:"PDX",1:"SDX0")
 ..S FIELD=FIELD_(PIECE-$S((PIECE>4)&(PIECE<8):4,(PIECE>14)&(PIECE<19):11,1:0))
 ..D NOTIFY(.X1,.X2,.DA,TYPE,FIELD)
 Q
