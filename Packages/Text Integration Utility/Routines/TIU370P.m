TIU370P ; NA/AJB - PERSIAN GULF UPDATE;Nov 06, 2024@12:59:03
 ;;1.0;TEXT INTEGRATION UTILITIES;**370**;Jun 20, 1997;Build 14
 ;
 ; Reference to FIND1^DIC in ICR #2051
 ; Reference to BMES^XPDUTL in ICR #10141
 ; Reference to MES^XPDUTL in ICR #10141
 ;
 Q
DEL ; testing use only
 N DETAILS,ENTRY
 F ENTRY=1:1 S DETAILS=$P($T(ENTRIES+ENTRY),";",2,4) Q:DETAILS=""  D
 . N DA,DIK,NAME,PARENT,TYPE,X,Y
 . S NAME=$P(DETAILS,";"),DA=$$LU(8925.1,NAME,"X"),DIK="^TIU(8925.1,",PARENT=$$LU(8925.1,"PROGRESS NOTES","X"),TYPE=$P(DETAILS,";",3) Q:'DA
 . D ^DIK Q:TYPE'="DC"
 . K ^TIU(8925.1,PARENT,10,"C",NAME)
 . K ^TIU(8925.1,PARENT,10,$O(^TIU(8925.1,PARENT,10,"B",DA,0)))
 . K ^TIU(8925.1,PARENT,10,"B",DA)
 Q
ENTRIES ;name;print name;type;m-code
 ;VA-PERSIAN GULF STATUS;VA-Persian Gulf Status;OBJ
 ;VA-GULFVET QUESTIONNAIRE;;DC
 ;GULFVET QUESTIONNAIRE;GulfVET Questionnaire;DOC
 ;
LU(FILE,NAME,FLAGS,SCREEN,INDEXES) ;
 Q $$FIND1^DIC(FILE,"",$G(FLAGS),NAME,$G(INDEXES),$G(SCREEN))
POST ; post-install
 N DETAILS,ENTRY
 F ENTRY=1:1 S DETAILS=$P($T(ENTRIES+ENTRY),";",2,4) Q:DETAILS=""  D
 . N DA,NAME,PNAME,TYPE,X,Y
 . S NAME=$P(DETAILS,";"),PNAME=$P(DETAILS,";",2),TYPE=$P(DETAILS,";",3)
 . I TYPE="OBJ" S DA=$$CROBJ^TIUCROBJ(NAME,"",PNAME,"S X=$$PGULF^TIUMSER(DFN)")
 . I TYPE="DC" S DA=$$CRDD^TIUCRDD(NAME,TYPE,11,"PROGRESS NOTES")
 . I TYPE="DOC" D  S DA=$$CRDD^TIUCRDD(NAME,TYPE,11,"VA-GULFVET QUESTIONNAIRE","PRIMARY CARE NOTE")
 . . ; remove previously linked entries
 . . D UNLINK
 . Q:PNAME=""
 . N TIU,ERROR
 . S TIU(8925.1,$$LU(8925.1,NAME,"X")_",",.03)=PNAME
 . D UPDATE^DIE("","TIU","","ERROR")
 Q
UNLINK ;
 N NAME S NAME="GULFVET QUESTIONNAIRE"
 N DIALOG S DIALOG=$$LU(801.41,"VA-GULFVET QUESTIONNAIRE","X","I $P(^(0),U,4)=""R""") Q:'DIALOG
 N OUT D FIND^DIC(8927,"","@;.01I","P",NAME,"","","","","OUT")
 N PARENT S PARENT=$$LU(8927,"Document Titles","X") Q:'PARENT
 N NUM S NUM=0 F  S NUM=$O(OUT("DILIST",NUM)) Q:'NUM  D
 . N IEN S IEN=+OUT("DILIST",NUM,0)
 . N NODE S NODE=$G(^TIU(8927,IEN,0))
 . Q:$P(NODE,U,6)>0  ; personal owner
 . Q:$P(NODE,U,15)'=DIALOG  ; reminder dialog
 . N NUM S NUM=0 F  S NUM=$O(^TIU(8927,PARENT,10,NUM)) Q:'NUM  D
 . . Q:$P($G(^TIU(8927,PARENT,10,NUM,0)),U,2)'=IEN
 . . N DA,DIK S DA(1)=PARENT,DA=NUM,DIK="^TIU(8927,"_DA(1)_",10," D ^DIK ; remove entries from parent first
 . N DA,DIK S DA=IEN,DIK="^TIU(8927," D ^DIK ; remove entry
 Q
