TIUCOPRU ;SLC/TDP - Copy/Paste Report Utility ;11/14/17  00:19
 ;;1.0;TEXT INTEGRATION UTILITIES;**290**;Jun 20, 1997;Build 548
 ;
 Q
QUE ; Queue output
 ;ION is a global variable
 ;RPT,ZTDESC,ZTRTN may be passed in (expected to exist)
 N %,IOP,ZTDTH,ZTIO,ZTSAVE,ZTSK
 Q:'$D(ZTRTN)  K IO("Q"),ZTSAVE
 I RPT="S" F %="RPT","SDT","EDT","SRT","PAT","PTNAME","PRV","PROV","CLN","CLIN","DIV","CTHRESH" S ZTSAVE(%)=""
 S:'$D(ZTDESC) ZTDESC=$S(RPT="S":"COPY/PASTE SUMMARY REPORT",1:"COPY/PASTE DETAIL REPORT") S ZTIO=ION
 D ^%ZTLOAD W !,$S($D(ZTSK):"Request Queued!",1:"Request Cancelled!")
 K ZTSK,ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE D ^%ZISC
 S IOP="HOME" D ^%ZIS
 G EN^TIUCOPR
 Q
HDR ;Write Header
 S PG=$S(RPT="S":4,1:7)
 W !,?(RM-$L(PGTTL)/2),PGTTL
 W !,?(RM-$L(PGHDR)/2),PGHDR
 W !,?(RM-$L(DTRNG)/2),DTRNG
 I (SRT'="D")&(SRT'="M") S PG=PG+1 W !,?(RM-$L(PGSRT)/2),PGSRT
 I $G(THRESH)'="" S PG=PG+1 W !,?(RM-$L(THRESH)/2),THRESH
 I RPT="D" D
 . W !,PGCLMN1
 . W !,PGCLMN2
 . W !,PGCLMN3
 . I $G(PGCLMN4)'="" S PG=PG+1 W !,PGCLMN4
 . I $G(PGCLMN5)'="" S PG=PG+1 W !,PGCLMN5
 ;I RPT="S",$G(THRESH)'="" W !,?(RM-$L(THRESH)/2),THRESH
 W !,$E(LINE,1,RM)
 S PG1LN=PG
 Q
EOP ;Stops terminal screen from scrolling out of view.
 N X,LNCNT,LP
 S LNCNT=(PGLNG+1)-PG
 F LP=1:1:LNCNT D
 . W !," "
 R "** Press <ENTER> To Continue, ""^"" To Quit ** ",X:60
 I X="^" S QUIT=1
 Q
SCRNHDR ;Report screen header
 N SCRTTL
 I RPT="D" S SCRTTL="COPY/PASTE DETAIL REPORT"_$S(+DIV>0:" FOR "_$P(DIV,U,2),1:"")
 I RPT="S" S SCRTTL="COPY/PASTE SUMMARY REPORT"_$S(+DIV>0:" FOR "_$P(DIV,U,2),1:"")
 W @IOF
 W ?(IOM-$L(SCRTTL)/2),SCRTTL,!!!
 Q
 ;
HDRSTUP ;Setup the header display
 N SORT,Y
 S PGTTL="DETAIL COPY/PASTE REPORT"_$S(+DIV>0:" FOR "_$P(DIV,U,2),1:"")
 S PGHDR="FOR "_$S(PAT="A":"ALL",1:"SELECTED")_" PATIENT"_$S(PTNAME=1:"",1:"S")
 S Y=SDT
 D DD^%DT
 S STDT=Y
 S Y=EDT
 D DD^%DT
 S ENDT=Y
 S DTRNG="FROM "_STDT_" TO "_ENDT
 I (SRT'="D")&(SRT'="M") D
 . S SORT=$S(SRT="C":"LOCATION",1:"PASTING PROVIDER")
 . S PGSRT="FOR "_$S(((CLN="A")!(PRV="A")):"ALL",1:"SELECTED")_" "_$S(SRT="P":$P(SORT," ",2),1:SORT)_$S(((CLIN=1)!(PROV=1)):"",1:"S")
 . S PGCLMN1=SORT
 . S PGCLMN2="     PASTED DATE/TIME         COPY PATIENT                    PASTE PATIENT"
 . S PGCLMN3="          COPY DATE/TIME           COPY AUTHOR                     COPY DOCUMENT TITLE"
 . S PGCLMN4="          PASTE DATE/TIME          PASTE PROVIDER                  PASTE DOCUMENT TITLE"
 . S PGCLMN5="               COPY MATCH PCT "
 I SRT="D" D
 . S PGCLMN1="PASTED DATE/TIME         COPY PATIENT                    PASTE PATIENT"
 . S PGCLMN2="     COPY DATE/TIME           COPY AUTHOR                     COPY DOCUMENT TITLE"
 . S PGCLMN3="     PASTE DATE/TIME          PASTE PROVIDER                  PASTE DOCUMENT TITLE"
 . S PGCLMN5="          COPY MATCH PCT "_$S(SPRSTXT=0:"     ORIGINAL PASTED TEXT",1:"")
 I SRT="M" D
 . S PGCLMN1="COPY MATCH PCT           COPY PATIENT                    PASTE PATIENT"
 . S PGCLMN2="     COPY DATE/TIME           COPY AUTHOR                     COPY DOCUMENT TITLE"
 . S PGCLMN3="     PASTE DATE/TIME          PASTE PROVIDER                  PASTE DOCUMENT TITLE"
 . S PGCLMN5="     PASTED DATE/TIME         "_$S(SPRSTXT=0:"ORIGINAL PASTED TEXT",1:"")
 Q
 ;
COMMON ;Writes common lines for the detailed report
 ;DATA variable must exist
 N TABL1P1,TABL1P2,TABL1P3,TABL2P1,TABL2P2,TABL2P3,Y
 I ($S($D(TEXT(1)):PG+4,1:PG+3))'<PGLNG D  Q:QUIT
 . I TRM D EOP^TIUCOPRU
 . W @IOF
 . Q:QUIT
 . D HDR^TIUCOPRU
 S PG=PG+3
 S TABL1P1=$S((SRT="D")!(SRT="M"):0,1:5)
 S TABL1P2=$S((SRT="D")!(SRT="M"):25,1:30)
 S TABL1P3=$S((SRT="D")!(SRT="M"):57,1:62)
 S TABL2P1=$S((SRT="D")!(SRT="M"):5,1:10)
 S TABL2P2=$S((SRT="D")!(SRT="M"):30,1:35)
 S TABL2P3=$S((SRT="D")!(SRT="M"):62,1:67)
 S CPYPTNAME=$P(DATA,U,1)
 S PSTPTNAME=$P(DATA,U,2)
 S CPYDT=$P(DATA,U,3)
 S CPYUSER=$P(DATA,U,4)
 S CPYNAME=$P(DATA,U,5)
 I CPYNAME["Percent Match fell" S CPYNAME=$E(CPYNAME,1,57)
 S PSTDT=$P(DATA,U,6)
 S PSTUSER=$P(DATA,U,7)
 S PSTNAME=$P(DATA,U,8)
 I SRT'="M" D
 . S Y=DTPST
 . D DD^%DT
 I SRT="M" S Y=$J((100-APCT)_"%",4)
 W !,?TABL1P1,Y,?TABL1P2,$E(CPYPTNAME,1,30),?TABL1P3,$E(PSTPTNAME,1,30)
 S Y=CPYDT
 D DD^%DT
 W !,?TABL2P1,Y,?TABL2P2,$E(CPYUSER,1,30),?TABL2P3,$E(CPYNAME,1,60)
 S Y=PSTDT
 D DD^%DT
 W !,?TABL2P1,Y,?TABL2P2,$E(PSTUSER,1,30),?TABL2P3,$E(PSTNAME,1,60)
 Q
 ;
TEXT(TEXT) ;Write the originally pasted text
 N TABL3P1,TABL3P2,TABL3P3,TXTLN,APCT,CNT
 S TABL3P1=$S(SRT="D":11,SRT="M":5,1:16)
 S TABL3P2=$S((SRT="D")!(SRT="M"):20,1:25)
 S TABL3P3=$S((SRT="D")!(SRT="M"):30,1:35)
 I SRT'="M" S APCT=$J($P(DATA,U,9)_"%",4)
 S CNT=0
 S TXTLN=0
 F  S TXTLN=$O(TEXT(TXTLN)) Q:TXTLN=""  D  Q:QUIT  Q:SPRSTXT=1
 . I PG+1'<PGLNG D  Q:QUIT
 .. I TRM D EOP^TIUCOPRU
 .. W @IOF
 .. Q:QUIT
 .. D HDR^TIUCOPRU
 . W !
 . I CNT=0 D
 .. I SRT'="M" W ?TABL3P1,APCT
 .. I SRT="M" W ?TABL3P1,DTPST
 .. I SPRSTXT=1 S PG=PG+1
 . S CNT=1
 . I SPRSTXT=1 Q
 . S PG=PG+1
 . W ?TABL3P3,$G(TEXT(TXTLN))
 Q
 ;
TTL ;Print the Detail report overall totals
 I ((((SRT="D")!(SRT="M"))&((PG+6)'<PGLNG))!(((SRT'="D")&(SRT'="M"))&((PG+7)'<PGLNG))) D  Q:QUIT
 . I TRM D EOP^TIUCOPRU
 . W @IOF
 . Q:QUIT
 . D HDR^TIUCOPRU
 S PG=PG+9
 S FNL="TOTALS: "_$S(((CLN="A")!(CLIN>0)):"RESULTED LOCATION(S)",((PRV="A")!(PROV>0)):"RESULTED PROVIDER(S)",1:"")
 W !!!,FNL
 W !,"     PASTES: "_$S(TTLCLIN>0:TTLCLIN,TTLPROV>0:TTLPROV,1:^TMP("TIUCOPR1",$J,"TTLCNT"))
 W !,"     NOTES W/PASTES > THRESHOLD: "_TTLTHRSH
 S TTLPNCNT=+$G(^TMP("TIUCOPR1",$J,"TTLNOTES"))
 W !,"     NOTES W/PASTES: "_TTLPNCNT
 S TLPST=$S(TTLPNCNT>0:(TTLTHRSH/TTLPNCNT)*100,1:"UNDEFINED")
 W !,"     % OF NOTES W/PASTES > THRESHOLD: "_TTLTHRSH_"/"_TTLPNCNT_" = "
 I TLPST="UNDEFINED" W TLPST
 I TLPST'="UNDEFINED" W $J(TLPST,5,2)_"%"
 S PG=PG+1
 W !,"     NOTES: "_+$G(^TMP("TIUCOPR1",$J,"TTLNSEL"))
 S TTLNT=0
 S LPDT=SDT-.000001
 F  S LPDT=$O(^TIU(8925,"H",LPDT)) Q:((LPDT="")!(LPDT>ENDT))  D
 . S NTIEN=""
 . F  S NTIEN=$O(^TIU(8925,"H",LPDT,NTIEN)) Q:NTIEN=""  S TTLNT=TTLNT+1
 W !!,"TOTAL NOTES FOR DATE RANGE: "_TTLNT
 Q
SORTM ;
 N PRVIEN,TEXT,X,Y
 S APCT=""
 F  S APCT=$O(^TMP("TIUCOPR1",$J,"TTLCNT",APCT)) Q:APCT=""  D  Q:QUIT
 . S PRVIEN=""
 . F  S PRVIEN=$O(^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN)) Q:PRVIEN=""  D  Q:QUIT
 .. S DATA=$G(^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN))
 .. S Y=$P(DATA,U,9)
 .. D DD^%DT
 .. S DTPST=Y
 .. K TEXT,Y
 .. I $D(^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN,1)) D
 ... S X=0
 ... F  S X=$O(^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN,X)) Q:X=""  D
 .... S TEXT(X)=$G(^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN,X))
 .. D COMMON^TIUCOPRU Q:QUIT
 .. I $D(TEXT) D TEXT^TIUCOPRU(.TEXT)
 Q
