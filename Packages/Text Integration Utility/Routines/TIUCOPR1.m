TIUCOPR1 ; SLC/TDP - Copy/Paste Report ;02/13/20  13:59
 ;;1.0;TEXT INTEGRATION UTILITIES;**290**;Jun 20, 1997;Build 548
 ;
 ;   DBIA 10003  DD^%DT
 ;   DBIA 10035  ^DPT(
 ;   DBIA  3162  ^GMR(123
 ;   DBIA 10040  ^SC(
 ;   DBIA 10060  ^VA(200,
 ;
 Q
DETAIL ;Detail Report
 N ABVTHRSH,CLNLOC,CLNNM,CPYDATA0,CPYDFN,CPYDT,CPYDUZ,CPYGBL,CPYIEN
 N CPYNAME,CPYOUT,CPYPKG,CPYPTNAME,CPYPTSRC,CPYUSER,DATA,DTPST
 N DTRNG,ENDT,FNL,IEN,LINE,LPDT,MIACPY,MIAPST,NOGO,NOTPAT,NTIEN,PARNT
 N PCT,PDIV,PG,PGCLMN1,PGCLMN2,PGCLMN3,PGCLMN4,PGCLMN5,PGHDR,PGLNG
 N PGSRT,PGTTL,PRVIEN,PRVNM,PSTDFN,PSTDT,PSTDUZ,PSTIEN,PSTNAME,PSTNT
 N PSTNT0,PSTPRV,PSTPRVID,PSTPTNAME,PSTUSER,QUIT,RM,STDT,STRTDT,TEXT
 N THRESH,THRSH,TRM,TTLCLIN,TTLNCNT,TTLNPRV,TTLNSEL,TTLNT,TTLPNCNT
 N TTLPROV,TTLTHACNT,TTLTHRSH,X,Y
 K ^TMP("TIUCOPR1",$J)
 S QUIT=0
 S PGLNG=+$G(IOSL)-1
 I PGLNG=0 S PGLNG=23
 S $P(LINE,"=",1000)=""
 S RM=+$G(IOM)
 I RM=0 S RM=$S(RPT="S":80,1:132)
 S TRM=($E(IOST,1,1)="C")
 S PG=0
 S (THRSH,THRESH)=""
 S THRESH="(THRESHOLD PCT: "_CTHRESH_")"
 D HDRSTUP^TIUCOPRU
 S (TTLCLIN,^TMP("TIUCOPR1",$J,"TTLCNT"),^TMP("TIUCOPR1",$J,"TTLNSEL"),TTLPROV,TTLTHACNT,TTLTHRSH)=0
 I TRM W @IOF
 D HDR^TIUCOPRU
 S ENDT=EDT+.999999
 S (LPDT,STDT)=SDT-.000001
 S STRTDT=9999999-LPDT
 F  S LPDT=$O(^TIUP(8928,"B",LPDT)) Q:((LPDT="")!(LPDT>ENDT))  D
 . S IEN=""
 . F  S IEN=$O(^TIUP(8928,"B",LPDT,IEN)) Q:IEN=""  D
 .. K TEXT
 .. S (PSTPTNAME,PSTNAME,PARNT)=""
 .. S (MIACPY,MIAPST,NOGO)=0
 .. S PSTNT0=$G(^TIUP(8928,IEN,0))
 .. I PSTNT0="" Q
 .. S PARNT=$P(PSTNT0,U,11)
 .. I PARNT'="",PARNT'=IEN Q
 .. S DTPST=$P(PSTNT0,U,1)
 .. S PRVIEN=$P(PSTNT0,U,2)
 .. S PDIV=+$P(PSTNT0,U,3)
 .. I +DIV>0,PDIV'=+DIV Q
 .. S PRVNM=""
 .. I +PRVIEN>0 S PRVNM=$P($G(^VA(200,PRVIEN,0)),U,1)
 .. I PRVNM="" S PRVNM="UNKNOWN PASTER NAME"
 .. S CPYIEN=$P(PSTNT0,U,6)
 .. S CPYPKG=$P(PSTNT0,U,7)
 .. I CPYIEN>0,CPYPKG="" S CPYPKG=8925
 .. S APCT=$P(PSTNT0,U,8)
 .. I APCT="" S APCT="TBD"
THRSH .. S ABVTHRSH=APCT'<CTHRESH
 .. I APCT'="",SRT="M" S APCT=100-APCT
 .. S CPYPTSRC=""
 .. S CPYNAME=""
 .. S CPYOUT=""
 .. S CPYUSER=""
 .. S CPYPTNAME=""
 .. S CPYDUZ=""
 .. S CPYUSER=""
 .. S CPYDFN=""
 .. S PSTNT=+$P(PSTNT0,U,4)
 .. I '$D(^TIU(8925,PSTNT,0)) Q
 .. S NOTPAT=0
 .. I MIAPST'=1 D  Q:NOTPAT
 ... S PSTIEN=$P($G(^TIU(8925,PSTNT,12)),U,2) ;AUTHOR/DICTATOR
 ... I +PSTIEN=0 S PSTIEN=$P($G(^TIU(8925,PSTNT,13)),U,2) ;ENTERED BY
 ... I +PSTIEN>0 S PSTUSER=$P($G(^VA(200,PSTIEN,0)),U,1)
 ... I PSTUSER="" S PSTUSER="UNKNOWN AUTHOR"
 ... S PSTDFN=$P($G(^TIU(8925,PSTNT,0)),U,2)
 ... I +PSTDFN>0 S PSTDFN=+$G(^AUPNPAT(PSTDFN,0))
 ... I PTNAME>0,'$D(PTNAME(PSTDFN)) S NOTPAT=1 Q
 ... I +PSTDFN>0 S PSTPTNAME=$P($G(^DPT(PSTDFN,0)),U,1)
 ... I PSTPTNAME="" S PSTPTNAME="UNKNOWN PATIENT NAME"
 ... S PSTDT=$P($G(^TIU(8925,PSTNT,13)),U,1)
 ... S PSTNAME=$P($G(^TIU(8925,PSTNT,0)),U,1)
 ... S PSTNAME=$P($G(^TIU(8925.1,PSTNAME,0)),U,1)
 ... I PSTNAME="" S PSTNAME="UNKNOWN TITLE"
 .. I SRT="C" Q:MIAPST  D  Q:NOGO
 ... S CLNLOC=+$P($G(^TIU(8925,PSTNT,12)),U,5)
 ... I CLN="I",'$D(CLIN(CLNLOC)) S NOGO=1 Q
 ... I CLNLOC>0 S CLNNM=$P($G(^SC(CLNLOC,0)),U,1)
 ... I CLNLOC=0 S CLNNM="UNKNOWN LOCATION"
 ... I '$G(^TMP("TIUCOPR1",$J,"CLNNOTES",CLNLOC,PSTNT)) D  ;count notes with pastes
 .... S ^TMP("TIUCOPR1",$J,"CLNNOTES",CLNLOC,PSTNT)=1
 .... S ^TMP("TIUCOPR1",$J,"CLNNOTES",CLNLOC)=$G(^TMP("TIUCOPR1",$J,"CLNNOTES",CLNLOC))+1
 .. I SRT="P" D  Q:NOGO
 ... I PRV="I",'$D(PROV(PRVIEN)) S NOGO=1 Q
 ... S PRVNM=$P($G(^VA(200,PRVIEN,0)),U,1)
 ... I '$G(^TMP("TIUCOPR1",$J,"PRVNOTES",PRVIEN,PSTNT)) D  ;count notes with pastes
 .... S ^TMP("TIUCOPR1",$J,"PRVNOTES",PRVIEN,PSTNT)=1
 .... S ^TMP("TIUCOPR1",$J,"PRVNOTES",PRVIEN)=$G(^TMP("TIUCOPR1",$J,"PRVNOTES",PRVIEN))+1
 .. I '$G(^TMP("TIUCOPR1",$J,"TTLNOTES",PSTNT)) D  ;count total notes with pastes
 ... S ^TMP("TIUCOPR1",$J,"TTLNOTES",PSTNT)=1
 ... S ^TMP("TIUCOPR1",$J,"TTLNOTES")=$G(^TMP("TIUCOPR1",$J,"TTLNOTES"))+1
 .. I 'ABVTHRSH Q
 .. I CPYIEN="",CPYPKG="" D
 ... S CPYOUT=$P(PSTNT0,U,10)
 ... S CPYNAME=$P(CPYOUT,";",2)
 ... S CPYPTNAME=$P(CPYOUT,";",3)
 ... I (CPYNAME["Outside of")!(CPYNAME["Percent Match fell below threshold") D
 .... S CPYDT="UNKNOWN"
 .... S CPYUSER="UNKNOWN"
 ... I $P(CPYNAME," - ",1)="ORDER DETAILS" D
 .... S CPYIEN=$P($P(CPYNAME," - ",2),";",1)
 .... S CPYPKG="100"
 ... I $P(CPYNAME," - ",1)'="ORDER DETAILS" D
 .... I $P(CPYOUT,";",4)'="" S CPYUSER=$P(CPYOUT,";",3)
 ... I CPYUSER="" S CPYUSER="UNKNOWN"
 ... I +$G(CPYDT)=0 S CPYDT="UNKNOWN"
 ... I CPYPTNAME="" S CPYPTNAME="UNKNOWN PATIENT NAME"
 .. I CPYPKG="8925" D  Q:MIACPY=1
 ... I '$D(^TIU(8925,CPYIEN)) S MIACPY=1 Q
 ... S CPYDUZ=$P($G(^TIU(8925,CPYIEN,12)),U,2) ;AUTHOR/DICTATOR
 ... I +CPYDUZ=0 S CPYDUZ=$P($G(^TIU(8925,CPYIEN,13)),U,2) ;ENTERED BY
 ... I +CPYDUZ>0 S CPYUSER=$P($G(^VA(200,CPYDUZ,0)),U,1)
 ... I CPYUSER="" S CPYUSER="UNKNOWN AUTHOR"
 ... S CPYDATA0=$G(^TIU(8925,CPYIEN,0))
 ... S CPYDT=$P($G(^TIU(8925,CPYIEN,13)),U,1)
 ... S CPYNAME=$P(CPYDATA0,U,1)
 ... S CPYNAME=$P($G(^TIU(8925.1,CPYNAME,0)),U,1)
 ... I CPYNAME="" S CPYNAME="UNKNOWN TITLE"
 ... S CPYDFN=$P(CPYDATA0,U,2)
 ... I +CPYDFN>0 S CPYDFN=+$G(^AUPNPAT(CPYDFN,0))
 ... I +CPYDFN>0 S CPYPTNAME=$P($G(^DPT(CPYDFN,0)),U,1)
 ... I CPYPTNAME="" S CPYPTNAME="UNKNOWN PATIENT NAME"
 .. I CPYPKG="100" D
 ... S CPYDATA0=$G(^OR(100,CPYIEN,0))
 ... S CPYDT=$P(CPYDATA0,U,7)
 ... S CPYDUZ=$P(CPYDATA0,U,6) ;WHO ENTERED
 ... I +CPYDUZ>0 S CPYUSER=$P($G(^VA(200,CPYDUZ,0)),U,1)
 ... I CPYUSER="" S CPYUSER="UNKNOWN AUTHOR"
 ... S CPYNAME="ORDER #"_$P(CPYDATA0,U,1)
 ... I +$P(CPYNAME,"#",2)=0 S CPYNAME="UNKNOWN ORDER NUMBER"
 ... S CPYDFN=$P(CPYDATA0,U,2) ;ORDERABLE ITEMS (PATIENT/REFERRAL)
 ... I +CPYDFN>0 D
 .... S CPYGBL=$P(CPYDFN,";",2)
 .... S CPYDFN=+CPYDFN
 ... I CPYGBL="DPT(" S CPYPTNAME=$P($G(^DPT(CPYDFN,0)),U,1)
 ... I CPYGBL="LRT(67," S CPYPTNAME=$P($G(^LRT(67,CPYDFN,0)),U,1),CPYPTSRC="R"
 ... I CPYPTNAME="" S CPYPTNAME="UNKNOWN PATIENT NAME"
 .. I CPYPKG="123" D
 ... S CPYDATA0=$G(^GMR(123,CPYIEN,0))
 ... S CPYDT=$P(CPYDATA0,U,1)
 ... S CPYDUZ=$P(CPYDATA0,U,14) ;SENDING PROVIDER
 ... I +CPYDUZ>0 S CPYUSER=$P($G(^VA(200,CPYDUZ,0)),U,1)
 ... I +CPYDUZ<1,$P($G(^GMR(123,CPYIEN,12)),U,6)'="" S CPYUSER=$P($G(^GMR(123,CPYIEN,12)),U,6),CPYDUZ="IFC"
 ... I CPYUSER="" S CPYUSER="UNKNOWN AUTHOR"
 ... S CPYNAME="CONSULT #"_CPYIEN
 ... I +$P(CPYNAME,"#",2)=0 S CPYNAME="UNKNOWN CONSULT NUMBER"
 ... S CPYDFN=$P(CPYDATA0,U,2) ;PATIENT NAME (IEN)
 ... I +CPYDFN>0 S CPYPTNAME=$P($G(^DPT(CPYDFN,0)),U,1)
 ... I CPYPTNAME="" S CPYPTNAME="UNKNOWN PATIENT NAME"
 .. I ((IEN=PARNT)!(PARNT="")) D
 ... S X=0
 ... F  S X=$O(^TIUP(8928,IEN,1,X)) Q:X=""  D
 .... S TEXT(X)=$G(^TIUP(8928,IEN,1,X,0))
CCNT .. I SRT="C" D
 ... S ^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN)=$G(CPYPTNAME)_U_$G(PSTPTNAME)_U_$G(CPYDT)_U_$G(CPYUSER)_U_$G(CPYNAME)_U_$G(PSTDT)_U_$G(PSTUSER)_U_$G(PSTNAME)_U_$G(APCT)
 ... S X=0 F  S X=$O(TEXT(X)) Q:X=""  D
 .... S ^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN,X)=$G(TEXT(X))
 ... S ^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC)=+$G(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC))+1
 ... I '$G(^TMP("TIUCOPR1",$J,"TTLNCLN",CLNLOC)) D
 .... N NTIEN,RDT,STATUS,TYPE
 .... S TYPE=""
 .... F  S TYPE=$O(^TIU(8925,"ALOC",CLNLOC,TYPE)) Q:TYPE=""  D
 ..... S STATUS=""
 ..... F  S STATUS=$O(^TIU(8925,"ALOC",CLNLOC,TYPE,STATUS)) Q:STATUS=""  D
 ...... S RDT=(9999999-ENDT)
 ...... F  S RDT=$O(^TIU(8925,"ALOC",CLNLOC,TYPE,STATUS,RDT)) Q:((RDT="")!(RDT>STRTDT))  D
 ....... S NTIEN=""
 ....... F  S NTIEN=$O(^TIU(8925,"ALOC",CLNLOC,TYPE,STATUS,RDT,NTIEN)) Q:NTIEN=""  D
 ........ S ^TMP("TIUCOPR1",$J,"TTLNCLN",CLNLOC)=+$G(^TMP("TIUCOPR1",$J,"TTLNCLN",CLNLOC))+1
 ........ I '$G(^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)) D
 ......... S ^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)=1
 ......... S ^TMP("TIUCOPR1",$J,"TTLNSEL")=+$G(^TMP("TIUCOPR1",$J,"TTLNSEL"))+1
 ... I ABVTHRSH,'$G(TTLTHACNT(CLNLOC,PSTNT)) D
 .... S TTLTHACNT(CLNLOC,PSTNT)=1
 .... S TTLTHACNT(CLNLOC)=$G(TTLTHACNT(CLNLOC))+1
 ... I '$G(TTLTHRSH(PSTNT)),ABVTHRSH D
 .... S TTLTHRSH(PSTNT)=1
 .... S TTLTHRSH=TTLTHRSH+1
 ... S TTLCLIN=TTLCLIN+1
PCNT .. I SRT="P" D
 ... S ^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST)=$G(CPYPTNAME)_U_$G(PSTPTNAME)_U_$G(CPYDT)_U_$G(CPYUSER)_U_$G(CPYNAME)_U_$G(PSTDT)_U_$G(PSTUSER)_U_$G(PSTNAME)_U_$G(APCT)
 ... S X=0 F  S X=$O(TEXT(X)) Q:X=""  D
 .... S ^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST,X)=$G(TEXT(X))
 ... S ^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN)=+$G(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN))+1
 ... I ABVTHRSH,'$G(TTLTHACNT(PRVIEN,PSTNT)) D
 .... S TTLTHACNT(PRVIEN,PSTNT)=1
 .... S TTLTHACNT(PRVIEN)=$G(TTLTHACNT(PRVIEN))+1
 ... I '$G(^TMP("TIUCOPR1",$J,"TTLNPRV",PRVIEN)) D
 .... N NTIEN,RDT
 .... S RDT=STDT
 .... F  S RDT=$O(^TIU(8925,"AADT",PRVIEN,RDT)) Q:RDT=""  Q:RDT>ENDT  D
 ..... S NTIEN=""
 ..... F  S NTIEN=$O(^TIU(8925,"AADT",PRVIEN,RDT,NTIEN)) Q:NTIEN=""  D
 ...... S ^TMP("TIUCOPR1",$J,"TTLNPRV",PRVIEN)=+$G(^TMP("TIUCOPR1",$J,"TTLNPRV",PRVIEN))+1
 ...... I '$G(^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)) D
 ....... S ^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)=1
 ....... S ^TMP("TIUCOPR1",$J,"TTLNSEL")=+$G(^TMP("TIUCOPR1",$J,"TTLNSEL"))+1
 ... I '$G(TTLTHRSH(PSTNT)),ABVTHRSH D
 .... S TTLTHRSH(PSTNT)=1
 .... S TTLTHRSH=TTLTHRSH+1
 ... S TTLPROV=TTLPROV+1
DCNT .. I (SRT="D")!(SRT="M") D
 ... I SRT="D" D
 .... S ^TMP("TIUCOPR1",$J,"TTLCNT",LPDT,PRVIEN)=$G(CPYPTNAME)_U_$G(PSTPTNAME)_U_$G(CPYDT)_U_$G(CPYUSER)_U_$G(CPYNAME)_U_$G(PSTDT)_U_$G(PSTUSER)_U_$G(PSTNAME)_U_$G(APCT)
 .... S X=0 F  S X=$O(TEXT(X)) Q:X=""  D
 ..... S ^TMP("TIUCOPR1",$J,"TTLCNT",LPDT,PRVIEN,X)=$G(TEXT(X))
 ... I SRT="M" D
 .... S ^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN)=$G(CPYPTNAME)_U_$G(PSTPTNAME)_U_$G(CPYDT)_U_$G(CPYUSER)_U_$G(CPYNAME)_U_$G(PSTDT)_U_$G(PSTUSER)_U_$G(PSTNAME)_U_$G(LPDT)
 .... S X=0 F  S X=$O(TEXT(X)) Q:X=""  D
 ..... S ^TMP("TIUCOPR1",$J,"TTLCNT",APCT,PRVIEN,X)=$G(TEXT(X))
 ... S ^TMP("TIUCOPR1",$J,"TTLCNT")=^TMP("TIUCOPR1",$J,"TTLCNT")+1
 ... I '$G(TTLTHRSH(PSTNT)),ABVTHRSH D
 .... S TTLTHRSH(PSTNT)=1
 .... S TTLTHRSH=TTLTHRSH+1
 ... N NTIEN,RDT
 ... S RDT=STDT
 ... F  S RDT=$O(^TIU(8925,"H",RDT)) Q:RDT=""  Q:RDT>ENDT  D
 .... S NTIEN=""
 .... F  S NTIEN=$O(^TIU(8925,"H",RDT,NTIEN)) Q:NTIEN=""  D
 ..... I '$G(^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)) D
 ...... S ^TMP("TIUCOPR1",$J,"TTLNSEL",NTIEN)=1
 ...... S ^TMP("TIUCOPR1",$J,"TTLNSEL")=+$G(^TMP("TIUCOPR1",$J,"TTLNSEL"))+1
 I SRT="C" D  G:QUIT QDETAIL
 . N TLPST,TNPST,TPST
 . S CLNNM=""
 . F  S CLNNM=$O(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM)) Q:CLNNM=""  D  Q:QUIT
 .. I (PG+5)'<PGLNG D  Q:QUIT
 ... I TRM D EOP^TIUCOPRU
 ... W @IOF
 ... Q:QUIT
 ... D HDR^TIUCOPRU
 .. X $S(PG1LN'=PG:"W !!",1:"W !")
 .. S PG=$S(PG1LN'=PG:PG+2,1:PG+1)
 .. W CLNNM
 .. S CLNLOC=""
 .. F  S CLNLOC=$O(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC)) Q:CLNLOC=""  D  Q:QUIT
 ... S DTPST=""
 ... F  S DTPST=$O(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST)) Q:DTPST=""  D  Q:QUIT
 .... S PRVIEN=""
 .... F  S PRVIEN=$O(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN)) Q:PRVIEN=""  D  Q:QUIT
 ..... S DATA=$G(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN))
 ..... K TEXT
 ..... I $D(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN,1)) D
 ...... S X=0
 ...... F  S X=$O(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN,X)) Q:X=""  D
 ....... S TEXT(X)=$G(^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC,DTPST,PRVIEN,X))
 ..... D COMMON^TIUCOPRU Q:QUIT
 ..... D TEXT^TIUCOPRU(.TEXT)
 ... Q:QUIT
 ... I (PG+4)'<PGLNG D  Q:QUIT
 .... I TRM D EOP^TIUCOPRU
 .... W @IOF
 .... Q:QUIT
 .... D HDR^TIUCOPRU
 ... S TPST=+$G(TTLTHACNT(CLNLOC))
 ... S TNPST=+$G(^TMP("TIUCOPR1",$J,"CLNNOTES",CLNLOC))
 ... S TLPST=$S(TNPST>0:(TPST/TNPST)*100,1:"UNDEFINED")
 ... S PG=PG+7
 ... W !!,"     SUB-TOTALS: "_CLNNM
 ... W !,"          PASTES: "_+^TMP("TIUCOPR1",$J,"TTLCLN",CLNNM,CLNLOC)
 ... W !,"          NOTES W/PASTES > THRESHOLD: "_TPST
 ... W !,"          NOTES W/PASTES: "_TNPST
 ... W !,"          % OF NOTES W/PASTES > THRESHOLD: "_TPST_"/"_TNPST_" = "
 ... I TLPST="UNDEFINED" W TLPST
 ... I TLPST'="UNDEFINED" W $J(TLPST,5,2)_"%"
 ... W !,"          NOTES: "_+$G(^TMP("TIUCOPR1",$J,"TTLNCLN",CLNLOC))
 I SRT="P" D  G:QUIT QDETAIL
 . N TLPST,TNPST,TPST
 . S PRVNM=""
 . F  S PRVNM=$O(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM)) Q:PRVNM=""  D  Q:QUIT
 .. S PRVIEN=""
 .. F  S PRVIEN=$O(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN)) Q:PRVIEN=""  D  Q:QUIT
 ... I (PG+5)'<PGLNG D  Q:QUIT
 .... I TRM D EOP^TIUCOPRU
 .... W @IOF
 .... Q:QUIT
 .... D HDR^TIUCOPRU
 ... X $S(PG1LN'=PG:"W !!",1:"W !")
 ... S PG=$S(PG1LN'=PG:PG+2,1:PG+1)
 ... W PRVNM
 ... S DTPST=""
 ... F  S DTPST=$O(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST)) Q:DTPST=""  D  Q:QUIT
 .... S DATA=$G(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST))
 .... K TEXT
 .... I $D(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST,1)) D
 ..... S X=0
 ..... F  S X=$O(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST,X)) Q:X=""  D
 ...... S TEXT(X)=$G(^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN,DTPST,X))
 .... D COMMON^TIUCOPRU Q:QUIT
 .... I $D(TEXT) D TEXT^TIUCOPRU(.TEXT)
 ... Q:QUIT
 ... I (PG+4)'<PGLNG D  Q:QUIT
 .... I TRM D EOP^TIUCOPRU
 .... W @IOF
 .... Q:QUIT
 .... D HDR^TIUCOPRU
 ... S TPST=+$G(TTLTHACNT(PRVIEN))
 ... S TNPST=^TMP("TIUCOPR1",$J,"PRVNOTES",PRVIEN)
 ... S TLPST=$S(TNPST>0:(TPST/TNPST)*100,1:"UNDEFINED")
 ... S PG=PG+7
 ... W !!,"     SUB-TOTALS: "_PRVNM
 ... W !,"          PASTES: "_+^TMP("TIUCOPR1",$J,"TTLPRV",PRVNM,PRVIEN)
 ... W !,"          NOTES W/PASTES > THRESHOLD: "_TPST
 ... W !,"          NOTES W/PASTES: "_TNPST
 ... W !,"          % OF NOTES W/PASTES > THRESHOLD: "_TPST_"/"_TNPST_" = "
 ... I TLPST="UNDEFINED" W TLPST
 ... I TLPST'="UNDEFINED" W $J(TLPST,5,2)_"%"
 ... W !,"          NOTES: "_+$G(^TMP("TIUCOPR1",$J,"TTLNPRV",PRVIEN))
 I SRT="D" D  G:QUIT QDETAIL
 . S DTPST=""
 . F  S DTPST=$O(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST)) Q:DTPST=""  D  Q:QUIT
 .. S PRVIEN=""
 .. F  S PRVIEN=$O(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST,PRVIEN)) Q:PRVIEN=""  D  Q:QUIT
 ... S DATA=$G(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST,PRVIEN))
 ... K TEXT
 ... I $D(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST,PRVIEN,1)) D
 .... S X=0
 .... F  S X=$O(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST,PRVIEN,X)) Q:X=""  D
 ..... S TEXT(X)=$G(^TMP("TIUCOPR1",$J,"TTLCNT",DTPST,PRVIEN,X))
 ... D COMMON^TIUCOPRU Q:QUIT
 ... I $D(TEXT) D TEXT^TIUCOPRU(.TEXT)
 I SRT="M" D  G:QUIT QDETAIL
 . D SORTM^TIUCOPRU
 D TTL^TIUCOPRU
 I TRM,'QUIT D EOP^TIUCOPRU W @IOF
QDETAIL K TTLCLIN,TTLPROV,^TMP("TIUCOPR1",$J)
 Q
