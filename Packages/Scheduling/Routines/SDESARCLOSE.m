SDESARCLOSE ;ALB/BLB - VISTA SCHEDULING RPCS ;June 4, 2021@13:40
 ;;5.3;Scheduling;**794,799**;Aug 13, 1993;Build 7
 ;;Per VHA Directive 6402, this routine should not be modified
 ; Reference to ^DPT(DFN,0) in ICR #10035
 ;
DISPOSITION(RETURN,ARIEN,DISP,DISPBY,DISPDT,EASTRCKNGNMBR) ;Disposition an appt req
 ;Input
 ;     IEN - Request ID - Pointer to SDEC APPT REQUEST file
 ;     DISP - Disposition
 ;     DISPBY - User Id - Pointer to NEW PERSON file
 ;     DISPDT - Date Dispositioned in external form
 ;     EASTRCKNGNMBR - (optional) Enterprise Appointment Scheduling Tracking Number associated to an appointment.
 N SDAPTREQ,POP,ARFDA,ARRET,ARMSG,X,MI,%DT
 I $G(ARIEN)="" S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,3)
 I $G(ARIEN)'="",'$D(^SDEC(409.85,ARIEN)) S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,4)
 I $G(DISP)="" S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,42)
 ; Re-mapped the Dispositions to their corresponding pointer value
 I $G(DISP)'="" D
 .S:DISP="DEATH" DISP=1
 .S:DISP="REMOVED/NON-VA CARE" DISP=2
 .S:DISP="REMOVED/SCHEDULED-ASSIGNED" DISP=3
 .S:DISP="REMOVED/VA CONTRACT CARE" DISP=4
 .S:DISP="REMOVED/NO LONGER NECESSARY" DISP=5
 .S:DISP="ENTERED IN ERROR" DISP=6
 .S:DISP="TRANSFERRED TO EWL" DISP=7
 .S:DISP="CHANGED CLINIC" DISP=8
 .S:DISP="MRTC PARENT CLOSED" DISP=9
 .S:DISP="REMOVED/EXTERNAL APP" DISP=10 ;* 745
 .S:DISP="FAILURE TO RESPOND" DISP=11
 I $G(DISP)'="" D
 .I '+DISP!((DISP<1)!(DISP>11)) D
 ..S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,43)
 ;validate DISPOSITIONED BY
 I $G(DISPBY)="" S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,53)
 I $G(DISPBY)'="",'$D(^VA(200,+DISPBY,0)) S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,54)
 ;validate DATE DISPOSITIONED
 I $G(DISPDT)'="" N Y S %DT="" S X=DISPDT D ^%DT S DISPDT=Y D
 .I Y=-1 S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,55)
 I $G(DISPDT)="" S POP=1 D ERRLOG^SDESJSON(.SDAPTREQ,56)
 ;validate EAS Tracking Number
 S EASTRCKNGNMBR=$TR($E($G(EASTRCKNGNMBR),1,30),"^"," ")
 I '$G(POP) D
 .S ARFDA=$NA(ARFDA(409.85,ARIEN_","))
 .S @ARFDA@(19)=DISPDT
 .S @ARFDA@(20)=DISPBY
 .S @ARFDA@(21)=DISP
 .S @ARFDA@(23)="C"
 .S @ARFDA@(100)=EASTRCKNGNMBR
 .D UPDATE^DIE("","ARFDA","ARRET","ARMSG")
 .I '$D(ARMSG) D
 ..S SDAPTREQ("ApptReqDisposition","FilingSuccess")=1
 .I $D(ARMSG("DIERR")) D
 ..S POP=1
 ..F MI=1:1:$G(ARMSG("DIERR")) D ERRLOG^SDESJSON(.SDAPTREQ,47,$G(ARMSG("DIERR",MI,"TEXT",1)))
 .;SEND HL7 TO CPRS IF RTC REQUEST
 .I '$G(POP),$P(^SDEC(409.85,ARIEN,0),U,5)="RTC" D
 ..I DISP=3!(DISP=9) D ARDISP^SDECHL7(ARIEN,"")
 ..I DISP'=3&(DISP'=9) D ARDISP^SDECHL7(ARIEN,1)
 ..I $D(^TMP($J,"REJECT",ARIEN)) D
 ...S POP=1,X=$G(^TMP(SDHL7IN("ORDER IEN")))
 ...D ERRLOG^SDESJSON(.SDAPTREQ,47,X)
 D BUILDER
 Q
BUILDER ;Convert data to JSON
 N JSONERR
 S JSONERR=""
 D ENCODE^SDESJSON(.SDAPTREQ,.RETURN,.JSONERR)
 Q
