SDES887P ;ALB/MGD,JAS - SD*5.3*887 Post Init Routine ; August 15, 2024
 ;;5.3;SCHEDULING;**887**;AUG 13, 1993;Build 7
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
 ;
EN ;
 D ADDVETSLFCAN
 D CLNSRVFIELD
 Q
 ;
ADDVETSLFCAN  ;
 N CLINIEN,FDA,SRVPROX,VIABPROX,VSELFCHK
 S SRVPROX=$O(^VA(200,"B","SDESOITEAS,SRV",0))
 S VIABPROX=$O(^VA(200,"B","VIABAPPLICATIONPROXY,VIAB",0))
 S:SRVPROX="" SRVPROX=0
 S:VIABPROX="" VIABPROX=0
 ;
 S CLINIEN=0
 F  S CLINIEN=$O(^SC(CLINIEN)) Q:'CLINIEN  D
 . S VSELFCHK=0
 . I SRVPROX,$D(^SC(CLINIEN,"SDPRIV",SRVPROX)) S VSELFCHK=1
 . I VIABPROX,$D(^SC(CLINIEN,"SDPRIV",VIABPROX)) S VSELFCHK=1
 . ;
 . S FDA(44,CLINIEN_",",63)=VSELFCHK
 . D FILE^DIE(,"FDA") K FDA
 Q
 ;
CLNSRVFIELD ; Remove values from field #1.1 of file #409.86
 N CONTIEN,CONTDATA,FDA
 S CONTIEN=0
 F  S CONTIEN=$O(^SDEC(409.86,CONTIEN)) Q:'CONTIEN  D
 . S CONTDATA=$G(^SDEC(409.86,CONTIEN,0))
 . Q:$P(CONTDATA,"^",6)=""
 . S FDA(409.86,CONTIEN_",",1.1)="@"
 . D FILE^DIE(,"FDA") K FDA
 Q
