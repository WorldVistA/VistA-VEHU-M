SDES2APPTCKNSTEP ;ALB/ANU - VISTA SCHEDULING RPCS ;DEC 18, 2023
 ;;5.3;Scheduling;**869**;Aug 13, 1993;Build 13
 ;;Per VHA Directive 6402, this routine should not be modified;
 Q
 ; INPUT
 ;
 ; SDCONTEXT("ACHERON AUDIT ID") = Up to 40 Character unique ID number. Ex: 11d9dcc6-c6a2-4785-8031-8261576fca37
 ; SDCONTEXT("USER DUZ") = The DUZ of the user taking action in the calling application.
 ; SDCONTEXT("USER SECID") = The SECID of the user taking action in the calling application.
 ; SDCONTEXT("PATIENT DFN") = The DFN/IEN of the target patient from the calling application.
 ; SDCONTEXT("PATIENT ICN") = The ICN of the target patient from the calling application.
 ; SDINPUT("SDIEN") = (Required) SDIEN, Appointment IEN
 ; SDINPUT("STATUS") = (Required) STATUS, Status to be set in Check-in Step Status (#409.843) file
 ;
SETAPPTCKNSTEP(JSON,SDCONTEXT,SDINPUT) ;create new status entry in Check-in Step Status (#409.843) file
 N ERRORS,SDRETURN,PARAMETERS,FDA,SDCHECKIN,IEN,ENTRY,SDUSER
 ;
 ; Validate SDCONTEXT
 ;
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("ApptCheckInSteps",1)="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 ; Validate PARAMS
 S PARAMETERS=$$VALPARAMS(.SDINPUT,.ERRORS)
 I $D(ERRORS) S ERRORS("ApptCheckInSteps",1)="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 ;
 S SDUSER=$S(+$G(SDCONTEXT("USER DUZ")):+SDCONTEXT("USER DUZ"),1:$G(DUZ))
 S SDIEN=$P(PARAMETERS,"^"),STATUS=$P(PARAMETERS,"^",2)
 D SETAPPTCKN(.SDRETURN,SDIEN,STATUS,SDUSER,.SDCONTEXT)
 I '$D(SDRETURN) S SDRETURN("ApptCheckInSteps",1)=""
 D BUILDJSON^SDES2JSON(.JSON,.SDRETURN)
 Q
 ;
VALPARAMS(PARAMS,SDERRORS) ; Validate
 N STATUS,SDIEN,SDENTRY,SIEN,STATPOINTER
 ;
 ; Validate Appointment IEN
 S SDIEN=$G(PARAMS("SDIEN"))
 I '$G(SDIEN) D ERRLOG^SDESJSON(.SDERRORS,14) Q 0
 I '$D(^SDEC(409.84,SDIEN,0)) D ERRLOG^SDESJSON(.SDERRORS,15) Q 0 ;invalid appt IEN
 ;
 ; Validate Status
 S STATUS=$G(PARAMS("STATUS"))
 I $G(STATUS)="" D ERRLOG^SDESJSON(.SDERRORS,38) Q 0
 I '$D(^SDEC(409.842,STATUS,0)) D ERRLOG^SDESJSON(.SDERRORS,37) Q 0
 I $$GET1^DIQ(409.84,SDIEN,.17,"E")["CANCEL" D ERRLOG^SDESJSON(.SDERRORS,31) Q 0 ;appt status is "C" (canceled)
 ;
 S SIEN=0,STATPOINTER=0
 F  S SIEN=$O(^SDEC(409.84,SDIEN,3,SIEN)) Q:'SIEN!(STATPOINTER=STATUS)  D
 .S STATPOINTER=$$GET1^DIQ(409.843,SIEN_","_SDIEN_",",.01,"I")
 I STATPOINTER=STATUS D ERRLOG^SDESJSON(.SDERRORS,32) Q 0 ; duplicate status entry, will not file
 ;
 Q:$D(SDERRORS) 0
 Q SDIEN_"^"_STATUS
 ;
SETAPPTCKN(SDRETURN,SDIEN,STATUS,SDUSER,SDCONTEXT) ; File check-in status and date/time in 409.843,.01/1
 N STATPOINTER,SIEN,DATETIME,FDA,SDCHECKIN,SDRETURN1,ERR
 N SDPARAM
 S DATETIME=$$NOW^XLFDT
 S FDA(409.843,"+1,"_SDIEN_",",.01)=STATUS
 S FDA(409.843,"+1,"_SDIEN_",",1)=DATETIME
 D UPDATE^DIE(,"FDA",,"ERR") K FDA
 S FDA(409.84,SDIEN_",",100)=$G(SDCONTEXT("ACHERON AUDIT ID"))
 D FILE^DIE(,"FDA") K FDA
 I '$D(ERR) D  Q
 .S SDRETURN("ApptCheckInSteps","FilingSuccess")=1
 .I STATUS=6 D
 ..S SDPARAM("APPOINTMENT IEN")=SDIEN ; The appointment IEN from the SDES APPOINTMENT (#409.84) file. (required)
 ..;S DATETIME=$$NOW^XLFDT,DATETIME=$$FMTE^XLFDT(DATETIME)
 ..S DATETIME=$$FMTISO^SDAMUTDT($$NOW^XLFDT)
 ..S SDPARAM("CHECKIN DATE TIME")=DATETIME ; ISO DATE and TIME to check-in the patient (required)
 ..D CHECKIN^SDES2CHECKIN(SDRETURN1,.SDCONTEXT,.SDPARAM)
 ..M SDRETURN("ApptCheckInSteps","Checkin")=SDRETURN1("Checkin")
 I $D(ERR) D ERRLOG^SDESJSON(.SDRETURN,36)
 Q
 ;
