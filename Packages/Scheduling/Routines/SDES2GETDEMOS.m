SDES2GETDEMOS ;ALB/BLB - SDES2 GET DEMOGRAPHICS ;APR 22,2024
 ;;5.3;Scheduling;**877**;Aug 13, 1993;Build 14
 ;;Per VHA Directive 6402, this routine should not be modified
 ;---------------------------------------------------------------
 Q
 ;
GETDEMOGRAPHICS(JSON,SDCONTEXT) ;
 N DEMOGRAPHICS,ERRORS
 ;
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("Demographics")="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 ;
 D BUILDRACE(.DEMOGRAPHICS)
 D BUILDRELIGION(.DEMOGRAPHICS)
 D BUILDETHNICITY(.DEMOGRAPHICS)
 D BUILDMARITALSTAT(.DEMOGRAPHICS)
 D BUILDJSON^SDES2JSON(.JSON,.DEMOGRAPHICS)
 Q
 ;
BUILDRACE(DEMOGRAPHICS) ;
 N RACEIEN,RACECOUNT
 ;
 S RACEIEN=0,RACECOUNT=0
 F  S RACEIEN=$O(^DIC(10,RACEIEN)) Q:'RACEIEN  D
 .S RACECOUNT=RACECOUNT+1
 .S DEMOGRAPHICS("Demographics","Race",RACECOUNT)=$$GET1^DIQ(10,RACEIEN,.01,"E")
 Q
 ;
BUILDRELIGION(DEMOGRAPHICS) ;
 N RELIGIONIEN,RELIGIONCOUNT
 ;
 S RELIGIONIEN=0,RELIGIONCOUNT=0
 F  S RELIGIONIEN=$O(^DIC(13,RELIGIONIEN)) Q:'RELIGIONIEN  D
 .S RELIGIONCOUNT=RELIGIONCOUNT+1
 .S DEMOGRAPHICS("Demographics","Religion",RELIGIONCOUNT)=$$GET1^DIQ(13,RELIGIONIEN,.01,"E")
 Q
 ;
BUILDETHNICITY(DEMOGRAPHICS) ;
 N ETHNICITYIEN,ETHNICITYCOUNT
 ;
 S ETHNICITYIEN=0,ETHNICITYCOUNT=0
 F  S ETHNICITYIEN=$O(^DIC(10.2,ETHNICITYIEN)) Q:'ETHNICITYIEN  D
 .S ETHNICITYCOUNT=ETHNICITYCOUNT+1
 .S DEMOGRAPHICS("Demographics","Ethnicity",ETHNICITYCOUNT)=$$GET1^DIQ(10.2,ETHNICITYIEN,.01,"E")
 Q
 ;
BUILDMARITALSTAT(DEMOGRAPHICS) ;
 N MARITALIEN,MARITALCOUNT
 ;
 S MARITALIEN=0,MARITALCOUNT=0
 F  S MARITALIEN=$O(^DIC(11,MARITALIEN)) Q:'MARITALIEN  D
 .S MARITALCOUNT=MARITALCOUNT+1
 .S DEMOGRAPHICS("Demographics","MaritalStatus",MARITALCOUNT)=$$GET1^DIQ(11,MARITALIEN,.01,"E")
 Q
 ;
