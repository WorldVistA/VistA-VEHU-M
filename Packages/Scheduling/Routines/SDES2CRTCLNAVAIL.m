SDES2CRTCLNAVAIL ;ALB/BLB,BWF - SDES2 SET CLINIC AVAILABILITY Aug, 14 2024@12:09
 ;;5.3;Scheduling;**890,897**;Aug 13, 1993;Build 2
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
 ;
CREATE(JSON,SDCONTEXT,AVAILABILITY) ;
 N ERRORS,RETURNAVAIL,CLINICIEN,CLINICSTARTHOUR,NUMBEROFENTRIES,INPUTS,INDEFINITEUNTIL,SDDISPPERHR,SDCLINSTARTHR,SLOTSTOCANCEL,%,%DT
 ;
 M INPUTS=AVAILABILITY
 D VALIDATE(.ERRORS,.AVAILABILITY,.CLINICIEN,.CLINICSTARTHOUR,.NUMBEROFENTRIES,.INPUTS)
 I $D(ERRORS) S ERRORS("ClinicAvailability")="" K COUNT,I D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 ;
 D CREATEAVAIL(.AVAILABILITY,.SDCONTEXT,.SLOTSTOCANCEL,CLINICIEN,CLINICSTARTHOUR,$$GET1^DIQ(44,CLINICIEN,1917,"I"),$$GET1^DIQ(44,CLINICIEN,1914,"I"),NUMBEROFENTRIES,.INPUTS,.INDEFINITEUNTIL,.RETURNAVAIL,.ERRORS)
 ;
 I $L($G(SLOTSTOCANCEL("CancelledSlots",1,"BeginTime"))) D
 .D RECANCELSLOTS(.SLOTSTOCANCEL,CLINICIEN,.SDCONTEXT) K SLOTSTOCANCEL
 ;
 D BUILDJSON^SDES2JSON(.JSON,.RETURNAVAIL)
 K COUNT,I
 Q
 ;
CREATEAVAIL(AVAILABILITY,SDCONTEXT,SLOTSTOCANCEL,CLINICIEN,CLINICSTARTHOUR,SDDISPPERHR,SDCLINSTARTHR,NUMBEROFENTRIES,INPUTS,INDEFINITEUNTIL,RETURNAVAIL,ERRORS) ;
 N COUNT,ENDDATE,DATES,TIMES,SCHEDULEDDAYS,DATENEXTTIMESLOT,CANSLOTSENDDATE,DONE,SDRETURN
 ;
 S COUNT=0,DONE=0
 F  D  Q:COUNT=NUMBEROFENTRIES!(DONE=1)
 .S COUNT=COUNT+1
 .I $G(SCHEDULEDDAYS)[$P($G(INPUTS("START DATE TIME",COUNT)),"T") Q
 .;
 .D CREATEDATESTIMES(.DATES,.TIMES,.INPUTS,$P($G(INPUTS("START DATE TIME",COUNT)),"T"),NUMBEROFENTRIES,.SLOTSTOCANCEL)
 .I $D(SLOTSTOCANCEL("Error")) M RETURNAVAIL=SLOTSTOCANCEL S DONE=1 Q
 .D CREATE^SDES2UTIL1(CLINICIEN,CLINICSTARTHOUR,$$GET1^DIQ(44,CLINICIEN,1912,"I"),$$DOW^XLFDT($P($G(AVAILABILITY("START DATE TIME",COUNT)),"."),1),.INDEFINITEUNTIL,.DATES,.TIMES,SDDISPPERHR,.SDRETURN,.ERRORS) K DATES,TIMES
 .;
 .S RETURNAVAIL("ClinicAvailability",COUNT,"Pattern")="Pattern Filed"
 .I $G(INDEFINITEUNTIL) D
 ..S RETURNAVAIL("ClinicAvailability",COUNT,"DateIndefiniteScheduleEnds")=INDEFINITEUNTIL K INDEFINITEUNTIL
 .S SCHEDULEDDAYS=$G(SCHEDULEDDAYS)_$P($G(INPUTS("START DATE TIME",COUNT)),"T")_U
 .;
 Q
 ;
CREATEDATESTIMES(DATES,TIMES,INPUTS,AVAILABILITYDATE,NUMBEROFENTRIES,SLOTSTOCANCEL) ;
 N COUNT,SUBSCRIPT,STARTDATE,STARTTIME,ENDTIME,SLOTS,LASTTIMESLOTDATE,DONE,CANSLOTSENDDATE,DATENEXTTIMESLOT
 ;
 S COUNT=0
 F  D  Q:COUNT=NUMBEROFENTRIES
 .S COUNT=COUNT+1
 .I $P($G(INPUTS("START DATE TIME",COUNT)),"T")'=AVAILABILITYDATE Q
 .;
 .I $G(INPUTS("NUMBER OF SLOTS",COUNT))="@" D  Q
 ..S DATES=$G(INPUTS("START DATE TIME",COUNT))="",DATES($$ISOTFM^SDAMUTDT(DATES))="",TIMES=""
 .;
 .S LASTTIMESLOTDATE=0,LASTTIMESLOTDATE=$O(DATES(LASTTIMESLOTDATE))
 .I $G(LASTTIMESLOTDATE)=$$ISOTFM^SDAMUTDT($P($G(INPUTS("START DATE TIME",COUNT)),"T"))!('$G(LASTTIMESLOTDATE)) D
 ..S DATES=$$ISOTFM^SDAMUTDT($P($G(INPUTS("START DATE TIME",COUNT)),"T")),DATES(DATES)=""
 ..S TIMES=$G(TIMES)_$P($G(INPUTS("START DATE TIME",COUNT)),"T",2)_"-"_$P($G(INPUTS("END DATE TIME",COUNT)),"T",2)_";"
 ..S TIMES($P($G(INPUTS("START DATE TIME",COUNT)),"T",2))=$P($G(INPUTS("START DATE TIME",COUNT)),"T",2)_"-"_$P($G(INPUTS("END DATE TIME",COUNT)),"T",2)_U_$G(INPUTS("NUMBER OF SLOTS",COUNT))
 .;
 .I $G(INPUTS("INDEFINITE",COUNT)),'$D(DATES(9999999)) D
 ..S DATES=$G(DATES)_";9999999",DATES(9999999)=""
 .;
 .S DATENEXTTIMESLOT=$$ISOTFM^SDAMUTDT($P($G(INPUTS("START DATE TIME",COUNT)),"T"))
 .S CANSLOTSENDDATE=$S($G(AVAILABILITY("INDEFINITE",COUNT)):$$FMTISO^SDAMUTDT($$GETLASTINDEFDATE(CLINICIEN,DATENEXTTIMESLOT,$$GET1^DIQ(44,CLINICIEN,2002))),1:DATENEXTTIMESLOT)
 .I $G(INPUTS("INDEFINITE",COUNT)) D  Q
 ..S DONE=0
 ..F  D  Q:DATENEXTTIMESLOT=$$ISOTFM^SDAMUTDT(CANSLOTSENDDATE)
 ...D GETCANSLOTS(CLINICIEN,.SLOTSTOCANCEL,$G(AVAILABILITY("INDEFINITE",COUNT)),CANSLOTSENDDATE,COUNT,$$FMTISO^SDAMUTDT(DATENEXTTIMESLOT),.INPUTS,.SDCONTEXT)
 ...S DATENEXTTIMESLOT=$$FMADD^XLFDT(DATENEXTTIMESLOT,7)
 .;
 .D GETCANSLOTS(CLINICIEN,.SLOTSTOCANCEL,$G(AVAILABILITY("INDEFINITE",COUNT)),CANSLOTSENDDATE,COUNT,$$FMTISO^SDAMUTDT(DATENEXTTIMESLOT),.INPUTS,.SDCONTEXT)
 ;
 S TIMES=$E($G(TIMES),1,$L($G(TIMES))-1)
 Q
 ;
GETLASTINDEFDATE(CLINICIEN,DATE,MAXBOOKINGDAYS) ;
 N FOUND,OSTDATE
 ;
 S OSTDATE=DATE,FOUND=0
 F  D  Q:FOUND!(OSTDATE>$$FMADD^XLFDT(DT,MAXBOOKINGDAYS))
 .S OSTDATE=$$FMADD^XLFDT(OSTDATE,7)
 .I $D(^SC(CLINICIEN,"OST",OSTDATE)) S FOUND=1
 Q $G(OSTDATE)
 ;
RECANCELSLOTS(SLOTSTOCANCEL,CLINICIEN,SDCONTEXT) ;
 N COUNT,CANCEL,CANCELRETURN
 ;
 S COUNT=0
 F  S COUNT=$O(SLOTSTOCANCEL("CancelledSlots",COUNT)) Q:'COUNT  D
 .S CANCEL("CLINIC IEN")=CLINICIEN
 .S CANCEL("FULL PARTIAL FLAG")=$S($G(SLOTSTOCANCEL("CancelledSlots",COUNT,"BeginTime"))["T":"P",1:"F")
 .S CANCEL("START DATE TIME")=$G(SLOTSTOCANCEL("CancelledSlots",COUNT,"BeginTime"))
 .S CANCEL("END DATE TIME")=$G(SLOTSTOCANCEL("CancelledSlots",COUNT,"EndTime"))
 .D CANCEL^SDES2CANCLNAVAIL(.CANCELRETURN,.SDCONTEXT,.CANCEL)
 Q
 ;
GETCANSLOTS(CLINICIEN,SLOTSTOCANCEL,INDEFINITE,ENDDATE,COUNT,SCHEDULEDATE,INPUTS,SDCONTEXT) ;
 N CANSLOTS,JSON,SLOTS,NUM,SLOTNUM
 ;
 I $$GET1^DIQ(44.005,$$ISOTFM^SDAMUTDT(SCHEDULEDATE)_","_CLINICIEN_",",1,"I")["CANCELLED" D  Q
 .S SLOTSTOCANCEL("CancelledSlots",1,"BeginTime")=SCHEDULEDATE
 .S SLOTSTOCANCEL("CancelledSlots",1,"EndTime")=SCHEDULEDATE
 ;
 S CANSLOTS("CLINICIEN")=CLINICIEN
 S CANSLOTS("SDESSTART")=SCHEDULEDATE_"T"_"0001"_$$GETTZOFFSET^SDESUTIL($$ISOTFM^SDAMUTDT(SCHEDULEDATE),CLINICIEN)
 S CANSLOTS("SDESENDDATE")=SCHEDULEDATE_"T"_2359_$$GETTZOFFSET^SDESUTIL($$ISOTFM^SDAMUTDT(SCHEDULEDATE),CLINICIEN)
 D GETCANCSLOTS^SDES2GETCANSLOTS(.JSON,.SDCONTEXT,.CANSLOTS) K CANSLOTS
 D DECODE^XLFJSON("JSON","SLOTS")
 ;
 I $D(SLOTSTOCANCEL("CancelledSlots",1,"BeginTime")),$L($G(SLOTS("CancelledSlots",1,"BeginTime"))) D  Q
 .S NUM="",NUM=$O(SLOTSTOCANCEL("CancelledSlots",NUM),-1)
 .S SLOTNUM=0
 .F  S SLOTNUM=$O(SLOTS("CancelledSlots",SLOTNUM)) Q:'SLOTNUM  D
 ..S NUM=NUM+1
 ..S SLOTSTOCANCEL("CancelledSlots",NUM,"BeginTime")=$G(SLOTS("CancelledSlots",SLOTNUM,"BeginTime"))
 ..S SLOTSTOCANCEL("CancelledSlots",NUM,"EndTime")=$G(SLOTS("CancelledSlots",SLOTNUM,"EndTime"))
 ;
 I $D(SLOTS("CancelledSlots",1,"BeginTime")) D
 .M SLOTSTOCANCEL=SLOTS
 ;
 S SCHEDULEDATE=$$FMADD^XLFDT(SCHEDULEDATE,7) K SLOTS
 Q
 ;
VALIDATE(ERRORS,AVAILABILITY,CLINICIEN,CLINICSTARTHOUR,NUMBEROFENTRIES,INPUTS) ;
 N FDATA,VAL
 ;
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 D VALFILEIEN^SDES2VALUTIL(.VAL,.ERRORS,44,$G(AVAILABILITY("CLINIC IEN")),1,,18,19)
 I $D(ERRORS) Q
 ;
 S CLINICIEN=$G(AVAILABILITY("CLINIC IEN"))
 S CLINICSTARTHOUR=$$GET1^DIQ(44,CLINICIEN,1914,"I")
 ;
 S NUMBEROFENTRIES=$O(AVAILABILITY("NUMBER OF SLOTS",""),-1)
 I $O(AVAILABILITY("START DATE TIME",""),-1)'=NUMBEROFENTRIES!($O(AVAILABILITY("END DATE TIME",""),-1)'=NUMBEROFENTRIES) D ERRLOG^SDES2JSON(.ERRORS,580)
 ;
 D VALFILEIEN^SDES2VALUTIL(.VAL,.ERRORS,44,CLINICIEN,1,,18,19)
 D VALAPPTLENGTH(.ERRORS,$$GET1^DIQ(44,CLINICIEN,1912,"I"),CLINICIEN)
 D VALSTARTHOUR^SDES2VAL44(.ERRORS,.CLINICSTARTHOUR)
 D VALIDATESLOTS(.ERRORS,.AVAILABILITY)
 D VALIDATEDATETIME(.ERRORS,CLINICIEN,.AVAILABILITY,NUMBEROFENTRIES)
 D VALIDATEINDEF(.ERRORS,.AVAILABILITY)
 D VALPATTERNS(CLINICIEN,.AVAILABILITY,.INPUTS,CLINICSTARTHOUR,NUMBEROFENTRIES)
 Q
 ;
VALIDATEINDEF(ERRORS,AVAILABILITY) ;
 S COUNT=0
 F  S COUNT=$O(AVAILABILITY("INDEFINITE",COUNT)) Q:'COUNT!($D(ERRORS))  D
 .I $G(AVAILABILITY("INDEFINITE",COUNT))="" D ERRLOG^SDES2JSON(.ERRORS,592) Q
 .I $G(AVAILABILITY("INDEFINITE",COUNT))'=1,$G(AVAILABILITY("INDEFINITE",COUNT))'=0 D ERRLOG^SDES2JSON(.ERRORS,591) Q
 Q
 ;
VALAPPTLENGTH(ERRORS,LENGTH,CLINICIEN) ;
 I LENGTH="" D ERRLOG^SDES2JSON(.ERRORS,115) Q
 I ((LENGTH<10)!(LENGTH>240)) D ERRLOG^SDES2JSON(.ERRORS,116) Q
 I (LENGTH#10'=0),(LENGTH#15'=0) D ERRLOG^SDES2JSON(.ERRORS,116) Q
 Q
 ;
VALIDATESLOTS(ERRORS,AVAILABILITY) ;
 N COUNT
 ;
 S COUNT=0
 F  S COUNT=$O(AVAILABILITY("NUMBER OF SLOTS",COUNT)) Q:'COUNT  D
 .I $G(AVAILABILITY("NUMBER OF SLOTS",COUNT))="@" Q
 .I $G(AVAILABILITY("NUMBER OF SLOTS",COUNT))<1!($G(AVAILABILITY("NUMBER OF SLOTS",COUNT))>26) D ERRLOG^SDES2JSON(.ERRORS,125)
 Q
 ;
VALIDATEDATETIME(ERRORS,CLINICIEN,AVAILABILITY,NUMBEROFENTRIES) ;
 N COUNT,TIMEZONEOFFSET,TIME
 ;
 F COUNT=1:1:NUMBEROFENTRIES  D  Q:$D(ERRORS)
 .I $G(AVAILABILITY("NUMBER OF SLOTS",COUNT))="@" Q
 .;
 .I $L($P($P($G(AVAILABILITY("START DATE TIME",COUNT)),"T",2),"-",2))!($L($P($P($G(AVAILABILITY("END DATE TIME",COUNT)),"T",2),"-",2))) D ERRLOG^SDES2JSON(.ERRORS,590)
 .S TIMEZONEOFFSET=$$GETTZOFFSET^SDESUTIL($$ISOTFM^SDAMUTDT($P($G(AVAILABILITY("START DATE TIME",COUNT)),"T")),CLINICIEN)
 .S AVAILABILITY("START DATE TIME",COUNT)=$G(AVAILABILITY("START DATE TIME",COUNT))_TIMEZONEOFFSET
 .S AVAILABILITY("END DATE TIME",COUNT)=$G(AVAILABILITY("END DATE TIME",COUNT))_TIMEZONEOFFSET
 .;
 .D VALISODATERANGE^SDES2VALISODTTM(.ERRORS,$G(AVAILABILITY("START DATE TIME",COUNT)),$G(AVAILABILITY("END DATE TIME",COUNT)),1,CLINICIEN)
 .S AVAILABILITY("START DATE TIME",COUNT)=$$ISOTFM^SDAMUTDT($G(AVAILABILITY("START DATE TIME",COUNT)),CLINICIEN)
 .S AVAILABILITY("END DATE TIME",COUNT)=$$ISOTFM^SDAMUTDT($G(AVAILABILITY("END DATE TIME",COUNT)),CLINICIEN)
 .I $G(AVAILABILITY("START DATE TIME",COUNT))<DT D ERRLOG^SDES2JSON(.ERRORS,71)
 .;
 .I $$GET1^DIQ(44,CLINICIEN,1914,"I")*100>+$E($P($G(AVAILABILITY("START DATE TIME",COUNT)),".",2)_"0000",1,4) D ERRLOG^SDES2JSON(.ERRORS,581)
 .I '$$CHECKDURATION^SDES2UTIL1($G(AVAILABILITY("START DATE TIME",COUNT)),$G(AVAILABILITY("END DATE TIME",COUNT)),$$GET1^DIQ(44,CLINICIEN,1912,"I")) D ERRLOG^SDES2JSON(.ERRORS,582)
 .I $$INACTIVE^SDESUTIL(CLINICIEN,$P($G(AVAILABILITY("START DATE TIME",COUNT)),".")) D ERRLOG^SDES2JSON(.ERRORS,583)
 .I $$GET1^DIQ(44,CLINICIEN,1918.5,"I")'="Y",$D(^HOLIDAY($P($G(AVAILABILITY("START DATE TIME",COUNT)),"."),0)) D ERRLOG^SDES2JSON(.ERRORS,465)
 Q
VALPATTERNS(CLINIEN,AVAILABILITY,INPUTS,STARTDAY,NUMBEROFENTRIES) ;
 N SLT,DOW,DISPINCPERHR,COUNT,DATES,TIMES,SLOTSTOCANCEL,DONE
 S SLT=$$GET1^DIQ(44,CLINIEN,1912,"I")
 S DISPINCPERHR=$$GET1^DIQ(44,CLINIEN,1917,"I")
 S COUNT=0,DONE=0
 F  D  Q:COUNT=NUMBEROFENTRIES!(DONE=1)
 .S COUNT=COUNT+1
 .;I $G(SCHEDULEDDAYS)[$P($G(AVAILABILITY("START DATE TIME",COUNT)),"T") Q
 .S DOW=$$DOW^XLFDT($P($G(AVAILABILITY("START DATE TIME",COUNT)),"."),1)
 .D CREATEDATESTIMES(.DATES,.TIMES,.INPUTS,$P($G(INPUTS("START DATE TIME",COUNT)),"T"),NUMBEROFENTRIES,.SLOTSTOCANCEL)
 .D CHECKLEN(CLINIEN,STARTDAY,SLT,DOW,,.DATES,.TIMES,DISPINCPERHR,,.ERRORS)
 Q
CHECKLEN(DA,STARTDAY,SLT,DOW,INDEFINITEUNTIL,DATES,TIMES,SDDISPPERHR,SDRETURN,ERRORS) ;
 ;DA = Clinic IEN  (SDCLINIC)
 ;SLT - Appointment length
 N D0,X,CNT,STARTTIME,T1,T2,NSL,CTR,DR,HY,MAX,SC,SD,SDREB,SDSTRTDT,SDZQ,ST,STR,Y1,INDEFINITELY,STIME,PATTERNS
 N POP,LT,H1,H2,M1,M2,SDTOP,SDREACT,X,SI,ZDX,DH,DO,D,Y,SDEL,HSI,SDJJ,HHY,SDIN,SDRE,SDRE1,I,OK,X1,X2,A,SDA1,SDSOH,RETURN
 S POP=0
 S STARTTIME=STARTDAY*100
 S (HSI,SI)=$G(SDDISPPERHR,4)
 S:SI=1 SI=4,HSI=1
 S:SI=2 SI=4,HSI=2
 ;
 S D0=""
 F  S (SD,D0)=$O(DATES(D0)) Q:D0=""  D  Q:POP
 .Q:D0?7"9"
 .S (CNT,INDEFINITELY)=0
 .I $O(DATES(D0))?7"9" S INDEFINITELY=1
 .S STARTTIME=""
 .F  S STARTTIME=$O(TIMES(STARTTIME)) Q:STARTTIME=""  D  Q:POP
 ..S X=TIMES(STARTTIME)
 ..S T2=$P($P(X,"^",1),"-",2)
 ..S NSL=$P(X,"^",2)
 ..S T1=STARTTIME
 ..D G3  ;Set up time slots in the T node
 .;
 .D:'POP G5  ;Set up pattern for the date
 K DATES,TIMES
 Q
 ;
G3 ;
 ;
 S SDTOP=1 ;????
 S SDZQ=1
 ;
 S LT=T2,H1=$E(T1,1,2),H2=$E(T2,1,2),M1=$E(T1,3,4),M2=$E(T2,3,4)
 S M2=M2-SLT
G3A I M2<0 S M2=M2+60,H2=H2-1 G G3A
 S:M2?1N M2="0"_M2 S:H2?1N H2="0"_H2
G4 S CNT=CNT+1,PATTERNS(DA,"T",D0,2,CNT,0)=H1_M1_"^"_NSL ;^SC(DA,"T",D0,2,CNT,0)=H1_M1_"^"_NSL
 S M1=M1+SLT
G4A I M1>59 S M1=M1-60,H1=H1+1 G G4A
 S:M1?1N M1="0"_M1 S:H1?1N H1="0"_H1
 I (H1_M1)>(H2_M2) Q
 G G4
 Q
 ;
G5 ;
 S SDEL=0
 G:'CNT DEL1:'$D(SDREACT),DEL1:'$D(SDTOP)&$D(SDREACT)&'CNT,C^SDB
 S DH=SLT*SI\60
 F X=0:0 S X=$O(PATTERNS(DA,"T",D0,2,X)) Q:X=""  D
 .S Y=PATTERNS(DA,"T",D0,2,X,0)
 .F D=1:1:DH S Y(Y#100*SI\60+(Y\100*SI)-(STARTDAY*SI)+D)=$S($P(Y,U,2):$E("123456789jklmnopqrstuvwxyz",$P(Y,U,2)),1:0)
 S (DH,DO,X)=""
 I $D(Y)=1 S SDEL=1 G D
 I $D(HSI) I HSI=1!(HSI=2) D CKSI1
 F Y=1:1 S DH=$D(Y(Y)),X=X_$S('DH&DO:"]",'DO&DH:"[",Y#SI=1:"|",1:" ")_$S(DH:Y(Y),1:" "),DO=DH I 'DH,$O(Y(Y))="" Q
 ;
 K Y
 ; always kill off the T subscript that was created
 K PATTERNS
 I SI+SI+$L(X)>80 S CNT=0,LT=$G(STIME),SDEL=0 S POP=1 D ERRLOG^SDES2JSON(.ERRORS,52,"Pattern exceeds 80 characters for date: "_$$FMTISO^SDAMUTDT(D0,DA)) Q
 G D
CKSI1 F SDJJ=$O(Y(-1)):$S(HSI=1:4,1:2) Q:SDJJ>41  S:$D(Y(SDJJ)) HY(SDJJ)="" I '$D(Y(SDJJ)) Q:$O(Y(SDJJ))=""  S SDJJ=$O(Y(SDJJ-1))-$S(HSI=1:4,1:2)
 F HHY=0:0 S HHY=$O(Y(HHY)) Q:HHY=""  I '$D(HY(HHY)) K Y(HHY)
 Q
 ;
DEL1 S (DH,DO,X)="",SDEL=1
D I $D(SDIN),SDIN>D0 S SDRE1=$S(SDRE:SDRE,1:9999999)
 S DH=X,OK=0,CTR=0
 S SDSOH=$S('$D(^SC(DA,"SL")):0,$P(^SC(DA,"SL"),"^",8)']"":0,1:1)
 F X=D0:0 S X=+$O(^SC(DA,"T",X)) Q:X'>0  D DOW^SDM0 I Y=DOW S Y=X,DO=Y G R
 I X'>0,$D(SDIN),SDIN>D0 D
 .S SDRE1=$S(SDRE=0:9999999,1:SDRE)
 .S X=SDIN
 .F I=0:1:6 D DOW^SDM0 S:Y=DOW OK=1 Q:OK  S X1=X,X2=1 D C^%DTC Q:X>SDRE1
 I OK S Y=X,DO=D0 G R
 S DO=9999999
R K OK
