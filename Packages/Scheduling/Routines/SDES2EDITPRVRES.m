SDES2EDITPRVRES ;ALB/LAB - VISTA SCHEDULING SDES2 EDIT PROVIDER RESOURCE ;JAN 04,2024
 ;;5.3;Scheduling;**869**;Aug 13, 1993;Build 13
 ;;Per VHA Directive 6402, this routine should not be modified
 Q
 ;
EDITPRVRES(RESULT,SDCONTEXT,SDPARAM) ; create provider resource record 409.831
 N ERRORS,RETURN,RESOURCE,SDDUZ
 ;validate context array for accuracy
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("ResourceIEN",1)="" D BUILDJSON^SDES2JSON(.RESULT,.ERRORS) Q
 D VALIDATE(.ERRORS,.SDPARAM)
 I $D(ERRORS) S ERRORS("ResourceIEN",1)="" D BUILDJSON^SDES2JSON(.RESULT,.ERRORS) Q
 D EDITRESOURCE(.RETURN,.ERRORS,.SDPARAM)
 D BUILDJSON^SDES2JSON(.RESULT,.RETURN)
 Q
 ;
VALIDATE(ERRORS,SDPARAM) ; validate input array variables
 D VALFILEIEN^SDES2VALUTIL(,.ERRORS,409.831,$G(SDPARAM("ResourceIEN")),1,"",550,549)
 Q:$D(ERRORS)
 D:($$GET1^DIQ(409.831,SDPARAM("ResourceIEN"),.012,"E")'="PROVIDER") ERRLOG^SDES2JSON(.ERRORS,52,"Not a Provider Resource.")
 D VALFILEIEN^SDES2VALUTIL(,.ERRORS,44,$G(SDPARAM("ClinicIEN")),"","",,19)
 D:$G(SDPARAM("ClinicIEN"))="" ERRLOG^SDES2JSON(.ERRORS,52,"Nothing updated")
 Q:$D(ERRORS)
 Q
 ;
 ;
EDITRESOURCE(RETURN,ERRORS,SDPARAM) ;Edit the provider resource
 N FILEDATA,NEWIEN
 S FILEDATA(409.831,SDPARAM("ResourceIEN")_",",.04)=SDPARAM("ClinicIEN")
 D FILE^DIE(,"FILEDATA","ERRORS") K FILEDATA
 S RETURN("ResourceIEN")=SDPARAM("ResourceIEN")
 Q
 ;
