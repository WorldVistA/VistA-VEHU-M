SDES2GETCLNSLOT ;ALB/TJB - SDES2 GET CLINIC CANCEL SLOTS for HOSPITAL LOCATION FILE 44 ;April 21, 2025
 ;;5.3;Scheduling;**906**;Aug 13, 1993;Build 5
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 ; Documented API's and Integration Agreements
 ; -------------------------------------------
 ;Reference to $$GETS^DIQ is supported by IA #2056
 ;Reference to $$GETS1^DIQ is supported by IA #2056
 ;
 Q
 ; SDINPUT("CLINIC IEN")=IEN from file 44
 ; SDINPUT("START DATE")=START DATE
 ;
CANAVAIL(JSON,SDCONTEXT,SDINPUT) ;
 N ERRORS,STARTDT,DIERR
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("restoreArray",1)="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 D VALCLINIEN^SDES2VAL44(.ERRORS,$G(SDINPUT("CLINIC IEN")),1)
 S STARTDT=$$VALISODTTM^SDES2VALISODTTM(.ERRORS,$G(SDINPUT("START DATE")),$G(SDINPUT("CLINIC IEN")),1)
 I $D(ERRORS) S ERRORS("restoreArray",1)="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 D BUILDSLOTS(.JSON,SDINPUT("CLINIC IEN"),STARTDT)
 Q
BUILDSLOTS(JSON,CLINIC,STARTDT,ENDDT) ;
 N SDARR
 D RETCANPERIODS(.SDARR,CLINIC,STARTDT)
 I '$D(SDARR) S SDARR("restoreArray",1)=""
 D BUILDJSON^SDES2JSON(.JSON,.SDARR)
 Q
RETCANPERIODS(RETURN,CLINIC,DATETIME) ; if sent in time is not cancelled return time periods that are cancelled
 N NEWDATE,STARTDATE,CNT,ECNT
 S STARTDATE=$P(DATETIME,"."),NEWDATE=STARTDATE
 S CNT=1
 S ECNT=2
 F  S NEWDATE=$O(^SC(CLINIC,"SDCAN",NEWDATE)) Q:$P(NEWDATE,".")'=STARTDATE  D 
 . N STARTTIME,ENDTIME
 . S STARTTIME=$$GET1^DIQ(44.05,NEWDATE_","_CLINIC_",",.01,"I")
 . S ENDTIME=$$GET1^DIQ(44.05,NEWDATE_","_CLINIC_",",1,"I")
 . S ENDTIME=$E(ENDTIME,1,2)_":"_$E(ENDTIME,3,4)
 . S RETURN("restoreArray",CNT,"restorePeriod")=$$FMTISO^SDAMUTDT($$GET1^DIQ(44.05,NEWDATE_","_CLINIC_",",.01,"I"),CLINIC)
 . S RETURN("restoreArray",CNT,"restoreDescription")="restore: "_$$FMTISO^SDAMUTDT(STARTTIME,CLINIC)_" to "_$$FMTISO^SDAMUTDT($P(STARTTIME,".")_"."_$TR(ENDTIME,":"),CLINIC)_")"
 . S CNT=CNT+1
 I $G(^SC(CLINIC,"FULL DAY CANCEL",STARTDATE,0))'="" D
 . S RETURN("restoreArray",CNT,"restorePeriod")=$$FMTISO^SDAMUTDT($$GET1^DIQ(44.1902,STARTDATE_","_CLINIC_",",.01,"I"),CLINIC)
 . S RETURN("restoreArray",CNT,"restoreDescription")="restore: "_$$FMTISO^SDAMUTDT(STARTDATE,CLINIC)_" from Full day cancel"
 Q
 ;
