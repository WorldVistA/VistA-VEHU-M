SCTMAPI1 ;RGI/CBR - TEAM API; 08/31/2012
 ;;5.3;scheduling;**260003**;08/13/93;Build 8
LSTATMS(RETURN,SEARCH,START,NUMBER) ; Get active teams
 N %,LST,TMP,CNT,IND
 K RETURN
 S:$L($G(SEARCH))=0 SEARCH=""
 D LSTTMS^SCTMDAL1(.LST,.SEARCH,.START,$G(NUMBER))
 S %=$$BLDLST^SDMAPI(.TMP,.LST)
 S CNT=0
 F IND=0:0 S IND=$O(TMP(IND)) Q:IND=""  D
 . N HIS
 . D GETTMH^SCTMDAL1(.HIS,TMP(IND,"ID"),1)
 . Q:'$D(HIS)
 . Q:'HIS(.03)
 . Q:HIS(.02)>DT
 . S CNT=CNT+1
 . M RETURN(CNT)=TMP(IND)
 S RETURN=1,RETURN(0)=CNT
 Q 1
 ;
LSTAPRPO(RETURN,SEARCH,START,NUMBER) ; Get active practitioners
 N LST,TMP,CNT,IND,FLDS,%
 K RETURN
 S:$L($G(SEARCH))=0 SEARCH=""
 D LSTPRPO^SCTMDAL1(.LST,.SEARCH,.START,$G(NUMBER))
 S FLDS(.03)="USER"
 S %=$$BLDLST^SDMAPI(.TMP,.LST,.FLDS)
 S CNT=0
 F IND=0:0 S IND=$O(TMP(IND)) Q:IND=""  D
 . N PO,POH
 . D GETTMPO^SCTMDAL1(.PO,$P(TMP(IND,"NAME"),U,2),1)
 . Q:PO(.02)'=SEARCH(0)
 . Q:($D(SEARCH(1)))&(PO(.04)'=$G(SEARCH(1)))
 . D GETPOASH^SCTMDAL1(.POAH,+TMP(IND,"NAME"),1)
 . Q:'$D(POAH)
 . Q:'POAH(.04)
 . Q:POAH(.02)>DT
 . S CNT=CNT+1
 . M RETURN(CNT)=TMP(IND)
 . S RETURN(CNT,"NAME")=$P(RETURN(CNT,"NAME"),U,2)
 S RETURN=1,RETURN(0)=CNT
 Q 1
 ;
LSTAPOS(RETURN,SEARCH,START,NUMBER) ; Get active practitioners
 N LST,TMP,CNT,IND,FLDS,%
 K RETURN
 S:$L($G(SEARCH))=0 SEARCH=""
 D LSTPOS^SCTMDAL1(.LST,.SEARCH,.START,$G(NUMBER))
 S FLDS(.02)="TEAM",FLDS(.09)="CLINIC"
 S %=$$BLDLST^SDMAPI(.TMP,.LST,.FLDS)
 S CNT=0
 F IND=0:0 S IND=$O(TMP(IND)) Q:IND=""  D
 . N PO,POH,POAH
 . D GETTMPO^SCTMDAL1(.PO,TMP(IND,"ID"),1)
 . Q:PO(.02)'=SEARCH(0)
 . Q:($D(SEARCH(1)))&(PO(.04)'=$G(SEARCH(1)))
 . D GETTMPOH^SCTMDAL1(.POH,+TMP(IND,"ID"),1)
 . Q:'$D(POH)
 . Q:'POH(.04)
 . Q:POH(.02)>DT
 . S CNT=CNT+1
 . D GETPOASH^SCTMDAL1(.POAH,TMP(IND,"ID"),,1)
 . I $D(POAH) S RETURN(CNT,"USER")=POAH(.03)
 . M RETURN(CNT)=TMP(IND)
 S RETURN=1,RETURN(0)=CNT
 Q 1
 ;
GETEAM(RETURN,SCTM) ; Get team
 D GETEAM^SCTMDAL1(.RETURN,SCTM,1,1,1)
 Q 1
 ;
GETEAMPO(RETURN,SCTMPO) ; Get team position
 D GETEAMPO^SCTMDAL1(.RETURN,SCTMPO,1,1,1)
 Q 1
 ;
