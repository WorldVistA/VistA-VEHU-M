SDES2GRECAPTYPE ;ALB/BWF - SDES2 get recall appointment types ;APR 30, 2024
 ;;5.3;Scheduling;**877**;Aug 13, 1993;Build 14
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
GETRECREMTYPES(RES,SDCONTEXT,SDINPUT) ;
 N SDECCNT,SDI,RECTYPES,ERRORS
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("RecallReminderType",1)="" D BUILDJSON^SDES2JSON(.RES,.ERRORS) Q
 S SDECCNT=0
 S SDI=0 F  S SDI=$O(^SD(403.51,SDI)) Q:'SDI  D
 .S SDECCNT=SDECCNT+1
 .S RECTYPES("RecallReminderType",SDECCNT,"IEN")=SDI
 .S RECTYPES("RecallReminderType",SDECCNT,"Name")=$$GET1^DIQ(403.51,SDI,.01,"E")
 .S RECTYPES("RecallReminderType",SDECCNT,"Synonym")=$$GET1^DIQ(403.51,SDI,1,"E")
 I '$D(RECTYPES) S RECTYPES("RecallReminderType",1)=""
 D BUILDJSON^SDES2JSON(.RES,.RECTYPES)
 Q
