SDESAPPT ;;ALB/BLB - VISTA SCHEDULING RPCS ;June 7, 2021@13:07
 ;;5.3;Scheduling;**788**;Aug 13, 1993;Build 6
 ;
 ;Reference is made to ICR #10035
 Q
 ;
APPGETJSON(SDECY,DFN,BDATE,EDATE) ; Return a list of appointments and associated data by PATIENT
 ;INPUT - DFN (Date File Number) Pointer to PATIENT (#2) File.
 ;RETURN PARMETERS:
 ;
 N IEN,NUM,ERR,IENS,SDAPPT,POP,APPTDATA
 S POP=0
 S DFN=$G(DFN)
 S ERR=0,IEN=0,NUM=0,BDATE=$G(BDATE),EDATE=$G(EDATE)
 S POP=$$VALIDATEDT(.BDATE,.EDATE)
 I DFN="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,1)
 I DFN'="",'$D(^DPT(DFN,0)) S POP=1 D ERRLOG^SDESJSON(.SDAPPT,2)
 I 'POP D
 .F  S IEN=$O(^SDEC(409.84,"CPAT",DFN,IEN)) Q:'IEN  D
 ..I '$$APPTINDTRANGE(IEN,BDATE,EDATE) Q
 ..S NUM=NUM+1
 ..D SUMMARY^SDESAPPTDATA(.APPTDATA,IEN)
 ..M SDAPPT("Appt",NUM)=APPTDATA
 .I '$D(SDAPPT("Appt")) S SDAPPT("Appt")=""
 D BUILDER
 Q
 ;
APPGETONEJSON(SDECY,IEN) ; Return a sing appointment and associated data by IEN
 ;INPUT - IEN (Internal Entry Number) Pointer to SDEC APPOINTMENT (#409.84) File.
 ;RETURN PARMETERS: SDECY - array with JSON formatted data for the Appointment.
 ;
 N NUM,ERR,IENS,SDAPPT,POP,APPTDATA
 S ERR=0,NUM=0,POP=0,IEN=$G(IEN)
 I IEN="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,14)
 I IEN'="",'$D(^SDEC(409.84,IEN,0)) S POP=1 D ERRLOG^SDESJSON(.SDAPPT,15)
 I 'POP D
 .S NUM=NUM+1
 .D SUMMARY^SDESAPPTDATA(.APPTDATA,IEN)
 .M SDAPPT("Appt",NUM)=APPTDATA
 .I '$D(SDAPPT("Appt")) S SDAPPT("Appt")=""
 D BUILDER
 Q
APPTBYRESOURCE(SDECY,SDRESIEN,SDBEG,SDEND) ;
 N POP,APPTIEN,APPTDT,SDAPPT,APPTDATA,COUNTER,JSONERR,DFN,PATIENT
 S (POP,APPTIEN,COUNTER,JSONERR)=""
 K SDECY
 ;Validate input paramaters
 S SDBEG=$G(SDBEG)
 S SDEND=$G(SDEND)
 S POP=$$VALIDATEDT(.SDBEG,.SDEND)
 I $G(SDRESIEN)="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,18)
 I $G(SDRESIEN)'="",'$D(^SDEC(409.831,SDRESIEN,0)) S POP=1 D ERRLOG^SDESJSON(.SDAPPT,19)
 I 'POP D
 .S APPTDT=SDBEG-.0001
 .S SDEND=SDEND+.0001
 .F  S APPTDT=$O(^SDEC(409.84,"ARSRC",SDRESIEN,APPTDT)) Q:APPTDT=""  D  Q:POP
 ..I SDEND,APPTDT>SDEND S POP=1 Q  ;End loop since past the end date
 ..S APPTIEN=""
 ..F  S APPTIEN=$O(^SDEC(409.84,"ARSRC",SDRESIEN,APPTDT,APPTIEN)) Q:APPTIEN=""  D
 ...D SUMMARY^SDESAPPTDATA(.APPTDATA,APPTIEN)
 ...M SDAPPT("Appt",$I(COUNTER))=APPTDATA
 ...S DFN=$G(SDAPPT("Appt",COUNTER,"DFN"))
 ...S PATIENT=""
 ...D PATIENTIDADDDON^SDESPATIENTDATA(.PATIENT,DFN)
 ...M SDAPPT("Appt",COUNTER,"Patient")=PATIENT
 .I '$D(SDAPPT("Appt")) S SDAPPT("Appt")=""
 D BUILDER
 Q
APPTBYCLINIC(SDECY,SDCLINICIEN,SDBEG,SDEND) ;
 N POP,APPTIEN,APPTDT,SDAPPT,APPTDATA,COUNTER,JSONERR,DFN,PATIENT,RESTYPE,SDRESIEN
 S (POP,APPTIEN,COUNTER,JSONERR)=""
 K SDECY
 ;Validate input paramaters
 S SDBEG=$G(SDBEG)
 S SDEND=$G(SDEND)
 S POP=$$VALIDATEDT(.SDBEG,.SDEND)
 I $G(SDCLINICIEN)="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,18)
 I $G(SDCLINICIEN)'="",'$D(^SC(SDCLINICIEN,0)) S POP=1 D ERRLOG^SDESJSON(.SDAPPT,19)
 I 'POP D
 .S SDRESIEN=""
 .F  S SDRESIEN=$O(^SDEC(409.831,"ALOC",SDCLINICIEN,SDRESIEN)) Q:SDRESIEN=""  D
 ..S RESTYPE=$P($G(^SDEC(409.831,SDRESIEN,0)),"^",11)
 ..I $P(RESTYPE,";",2)'="SC(" Q    ;Must be a Hospital Loc
 ..S APPTDT=SDBEG-.0001
 ..S SDEND=SDEND+.0001
 ..F  S APPTDT=$O(^SDEC(409.84,"ARSRC",SDRESIEN,APPTDT)) Q:APPTDT=""  D  Q:POP
 ...I SDEND,APPTDT>SDEND S POP=1 Q  ;End loop since past the end date
 ...S APPTIEN=""
 ...F  S APPTIEN=$O(^SDEC(409.84,"ARSRC",SDRESIEN,APPTDT,APPTIEN)) Q:APPTIEN=""  D
 ....D SUMMARY^SDESAPPTDATA(.APPTDATA,APPTIEN)
 ....M SDAPPT("Appt",$I(COUNTER))=APPTDATA
 ....S DFN=$G(SDAPPT("Appt",COUNTER,"DFN"))
 ....S PATIENT=""
 ....D PATIENTIDADDDON^SDESPATIENTDATA(.PATIENT,DFN)
 ....M SDAPPT("Appt",COUNTER,"Patient")=PATIENT
 .I '$D(SDAPPT("Appt")) S SDAPPT("Appt")=""
 D BUILDER
 Q
 ;
BUILDER ;Convert data to JSON
 N JSONERR
 S JSONERR=""
 D ENCODE^SDESJSON(.SDAPPT,.SDECY,.JSONERR)
 Q
 ;
VALIDATEDT(FROM,THRU) ;
 N POP
 S POP=0  ;Assume all is good
 I FROM="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,25)
 I THRU="" S POP=1 D ERRLOG^SDESJSON(.SDAPPT,26)
 I FROM'="" D
 .S FROM=$$NETTOFM^SDECDATE(FROM,"Y")
 .I FROM=-1 S POP=1 D ERRLOG^SDESJSON(.SDAPPT,27)
  I THRU'="" D
 .S THRU=$$NETTOFM^SDECDATE(THRU,"Y")
 .I THRU=-1 S POP=1 D ERRLOG^SDESJSON(.SDAPPT,28)
 I 'POP,THRU,FROM D
 .I FROM>THRU S POP=1 D ERRLOG^SDESJSON(.SDAPPT,29)
 Q POP
 ;
APPTINDTRANGE(IEN,BEG,END) ;
 ;Function to check if the Appt Sart Date / Time is within the desired date range
 ;
 ; Input
 ;   IEN - Internal # from Appointment File (409.84)
 ;   BEG - (Optional) Date in FM format
 ;   END - (Optional) Date in FM format
 ; Return
 ;   1 = Appointment Start Time is within BEG and END
 ;   0 = Not within the date range
 N INRANGE,APPTDT,FN,IENS,APPTARY,SDMSG,IENS
 S INRANGE=1  ;Assume success
 S FN=409.84,IENS=IEN_","
 D GETS^DIQ(FN,IEN,".01","I","APPTARY","SDMSG")
 S APPTDT=$G(APPTARY(FN,IENS,.01,"I"))
 I APPTDT=""!($G(SDMSG)) S INRANGE=0
 I $G(BEG),$G(APPTDT) S:APPTDT<BEG INRANGE=0
 I $G(END),$G(APPTDT) S:APPTDT>END INRANGE=0
 Q INRANGE
