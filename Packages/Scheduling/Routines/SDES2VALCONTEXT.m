SDES2VALCONTEXT ;ALB/MGD,LAB,LAB - ACHERON CONTEXT VALIDATION UTILITIES ;DEC 2,2024
 ;;5.3;Scheduling;**853,860,895**;Aug 13, 1993;Build 11
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
 ;
VALCONTEXT(SDERRORS,SDCONTEXT) ; Validate SDCONTEXT Array
 ; Required Input Variables
 D VALACHERONID(.SDERRORS,$G(SDCONTEXT("ACHERON AUDIT ID")))
 ; Optional Input Variables
 I $G(SDCONTEXT("PATIENT DFN"))'="" D VALPATDFN^SDES2VAL2(.SDERRORS,SDCONTEXT("PATIENT DFN"))
 I $G(SDCONTEXT("PATIENT ICN"))'="" D VALPATICN^SDES2VAL2(.SDERRORS,SDCONTEXT("PATIENT ICN"))
 I $G(SDCONTEXT("USER DUZ"))'="" D VALUSERDUZ^SDES2VAL200(.SDERRORS,SDCONTEXT("USER DUZ"))
 I $G(SDCONTEXT("USER SECID"))'="" D VALSECID(.SDERRORS,SDCONTEXT("USER SECID"),$G(SDCONTEXT("USER DUZ")))
 I '$D(SDERRORS) D
 . D DFNICNMATCH(.SDERRORS,$G(SDCONTEXT("PATIENT DFN")),$G(SDCONTEXT("PATIENT ICN")))
 Q
 ;
VALACHERONID(SDERRORS,SDACHERONID) ; Validate Acheron EAS ID
 I SDACHERONID="" D ERRLOG^SDES2JSON(.SDERRORS,474) Q
 I $$VALACHERONID^SDES2UTIL(SDACHERONID)=-1 D ERRLOG^SDES2JSON(.SDERRORS,475)
 Q
 ;
DFNICNMATCH(SDERRORS,PATDFN,PATICN) ;verify that sent in values are same for patient
 I (PATDFN'="")&(PATICN'="") D
 . I $O(^DPT("AICN",PATICN,""))'=PATDFN D ERRLOG^SDES2JSON(.SDERRORS,516)
 Q
 ;
VALSECID(SDERRORS,SDSECID,SDUSERDUZ) ;
 I SDSECID="" Q
 I $L(SDSECID)<3!$L(SDSECID)>40 D ERRLOG^SDES2JSON(.SDERRORS,156) Q
 I $$GET1^DIQ(200,SDUSERDUZ,205.1,"I")'=SDSECID D ERRLOG^SDES2JSON(.SDERRORS,156)
 Q
