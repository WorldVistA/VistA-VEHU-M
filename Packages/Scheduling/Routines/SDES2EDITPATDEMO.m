SDES2EDITPATDEMO ;ALB/BLB - SDES2 EDIT PATIENT DEMOGRAPHICS ;APR 22,2024
 ;;5.3;Scheduling;**877**;Aug 13, 1993;Build 14
 ;;Per VHA Directive 6402, this routine should not be modified
 ;---------------------------------------------------------------
 Q
 ;
EDITDEMOGRAPHICS(JSON,SDCONTEXT,PATIENT) ;
 N ERRORS,RETURN
 ;
 D VALIDATE(.ERRORS,.PATIENT,.SDCONTEXT)
 I $D(ERRORS) S ERRORS("PatientDemographics")="" D BUILDJSON^SDES2JSON(.JSON,.ERRORS) Q
 ;
 D EDITDEMO($G(PATIENT("DFN")))
 S RETURN("EditPatientDemographics")=1
 D BUILDJSON^SDES2JSON(.JSON,.RETURN)
 Q
 ;
VALIDATE(ERRORS,PATIENT,SDCONTEXT) ;
 N RACECOUNT
 ;
 D VALCONTEXT^SDES2VALCONTEXT(.ERRORS,.SDCONTEXT)
 D VALFILEIEN^SDES2VALUTIL(,.ERRORS,2,$G(PATIENT("DFN")),1,,1,2)
 ;
 S RACECOUNT=0
 F  S RACECOUNT=$O(PATIENT("RACE",RACECOUNT)) Q:'RACECOUNT  D
 .D VALDEMO(.ERRORS,$E($G(PATIENT("RACE",RACECOUNT)),1,30),"RACE")
 D VALDEMO(.ERRORS,$G(PATIENT("ETHNICITY")),"ETHNICITY")
 D VALDEMO(.ERRORS,$G(PATIENT("MARITAL STATUS")),"MARITAL STATUS")
 D VALDEMO(.ERRORS,$G(PATIENT("RELIGION")),"RELIGION")
 Q
 ;
EDITDEMO(DFN) ;
 N TOPLEVELFDA,ETHNICITYFDA,RACEFDA,ETHNICITYIEN,RACECOUNT,SUBIEN,IENS,ERROR
 ;
 ; top level patient
 I $L($G(PATIENT("RELIGION"))) D
 .S TOPLEVELFDA(2,DFN_",",.08)=$$GETDEMOIEN($G(PATIENT("RELIGION")),"RELIGION")
 I $L($G(PATIENT("MARITAL STATUS"))) D
 .S TOPLEVELFDA(2,DFN_",",.05)=$$GETDEMOIEN($G(PATIENT("MARITAL STATUS")),"MARITAL STATUS")
 I $D(TOPLEVELFDA) D
 .D FILE^DIE(,"TOPLEVELFDA") K TOPLEVELFDA
 ;
 ; sub level ethnicity
 I $L($G(PATIENT("ETHNICITY"))) D
 .S ETHNICITYIEN=0,ETHNICITYIEN=$O(^DPT(DFN,.06,ETHNICITYIEN))
 .S ETHNICITYFDA(2.06,ETHNICITYIEN_","_DFN_",",.01)=$$GETDEMOIEN($G(PATIENT("ETHNICITY")),"ETHNICITY")
 .S ETHNICITYFDA(2.06,ETHNICITYIEN_","_DFN_",",.02)=1
 .D FILE^DIE(,"ETHNICITYFDA") K ETHNICITYFDA
 ;
 ; sub level race
 I $L($G(PATIENT("RACE",1))) D
 .S SUBIEN=0
 .F  S SUBIEN=$O(^DPT(DFN,.02,SUBIEN)) Q:'SUBIEN  D
 ..S IENS=SUBIEN_","_DFN_","
 ..S RACEFDA(2.02,IENS,.01)="@"
 ..D FILE^DIE(,"RACEFDA") K RACEFDA
 .;
 .S RACECOUNT=0
 .F  S RACECOUNT=$O(PATIENT("RACE",RACECOUNT)) Q:'RACECOUNT  D
 ..S IENS="+1,"_DFN_","
 ..S RACEFDA(2.02,IENS,.01)=$$GETDEMOIEN($E($G(PATIENT("RACE",RACECOUNT)),1,30),"RACE")
 ..S RACEFDA(2.02,IENS,.02)=1
 ..D UPDATE^DIE(,"RACEFDA",,"ERROR") K RACEFDA
 Q
 ;
GETDEMOIEN(DEMO,TYPE) ;
 Q $S(TYPE="RELIGION":$O(^DIC(13,"B",DEMO,"")),TYPE="RACE":$O(^DIC(10,"B",DEMO,"")),TYPE="ETHNICITY":$O(^DIC(10.2,"B",DEMO,"")),TYPE="MARITAL STATUS":$O(^DIC(11,"B",DEMO,"")),1:"")
 ;
VALDEMO(ERRORS,DEMOGRAPHIC,TYPE) ;
 I '$L(DEMOGRAPHIC) Q
 I '$D(^DIC($S(TYPE="RELIGION":13,TYPE="RACE":10,TYPE="ETHNICITY":10.2,TYPE="MARITAL STATUS":11,1:""),"B",DEMOGRAPHIC)) D ERRLOG^SDES2JSON(.ERRORS,565)
 Q
 ;
