EC2P123B ;ALB/DE - EC National Procedure Update ; 10/4/12 11:00am
 ;;2.0;EVENT CAPTURE;**123**;8 May 96;Build 8
 ;
 ;this routine is used as a post-init in a KIDS build
 ;to modify the EC National Procedure file (#725)
 ;
 Q
 ;
ADDPROC ;* add national procedures
 ;
 ;  ECXX is in format:
 ;   NAME^NATIONAL NUMBER^CPT CODE^FIRST NATIONAL NUMBER SEQUENCE
 ;   LAST NATIONAL NUMBER SEQUENCE
 ;
 N ECX,ECXX,ECDINUM,ECNAME,ECCODE,ECCPT,ECCOUNT,X,Y,DIC,DIE,DA,DR,DLAYGO,DINUM
 N ECADD,ECBEG,ECEND,ECCODX,ECNAMX,ECSEQ,ECLIEN,ECSTR,ECCPTN
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Adding new procedures to EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 S ECDINUM=$O(^EC(725,9999),-1),ECCOUNT=$P(^EC(725,0),U,4)
 F ECX=1:1 S ECXX=$P($T(NEW+ECX),";;",2) Q:ECXX="QUIT"  D
 .S ECNAME=$P(ECXX,U,1),ECCODE=$P(ECXX,U,2),ECCPTN=$P(ECXX,U,3),ECCODX=ECCODE
 .S ECCPT=""
 .I ECCPTN'="" S ECCPT=$$FIND1^DIC(81,"","X",ECCPTN) I +ECCPT<1 D  Q
 ..S ECSTR="   CPT code "_ECCPTN_" not a valid code in CPT File."
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("   ["_ECCODE_"] "_ECSTR)
 .S ECBEG=$P(ECXX,U,4),ECEND=$P(ECXX,U,5),ECNAMX=ECNAME
 .I ECBEG="" S X=ECNAME D FILPROC Q
 .F ECSEQ=ECBEG:1:ECEND D
 ..S ECADD="000"_ECSEQ,ECADD=$E(ECADD,$L(ECADD)-2,$L(ECADD))
 ..;S ECNAME=ECNAMX_ECADD,X=ECNAME,ECCODE=ECCODX_ECADD
 ..I $E(ECCODX,1,3)'="RCM" S ECNAME=ECNAMX_ECSEQ,X=ECNAME,ECCODE=ECCODX_ECADD
 ..E  S ECNAME=ECNAMX_$E(ECADD,2,99),X=ECNAME,ECCODE=ECCODX_$E(ECADD,2,99)
 ..D FILPROC
 S $P(^EC(725,0),U,4)=ECCOUNT,X=$O(^EC(725,999999),-1),$P(^EC(725,0),U,3)=X
 Q
 ;
FILPROC ;File national procedures
 I '$D(^EC(725,"D",ECCODE)) D
 .S ECDINUM=ECDINUM+1,DINUM=ECDINUM,DIC(0)="L",DLAYGO=725,DIC="^EC(725,"
 .S DIC("DR")="1////^S X=ECCODE;4///^S X=ECCPT"
 .D FILE^DICN
 .I +Y>0 D
 ..S ECCOUNT=ECCOUNT+1
 ..D MES^XPDUTL(" ")
 ..S ECSTR="   Entry #"_+Y_" for "_$P(Y,U,2)
 ..S ECSTR=ECSTR_$S(ECCPT'="":" [CPT: "_ECCPT_"]",1:"")_" ("_ECCODE_")"
 ..D BMES^XPDUTL(ECSTR_"  ...successfully added.")
 .I Y=-1 D
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("ERROR when attempting to add "_ECNAME_" ("_ECCODE_")")
 I $D(^EC(725,"DL",ECCODE)) D
 .S ECLIEN=$O(^EC(725,"DL",ECCODE,""))
 .D MES^XPDUTL(" ")
 .D BMES^XPDUTL("   Your site has a local procedure (entry #"_ECLIEN_") in File #725")
 .D BMES^XPDUTL("   which uses "_ECCODE_" as its National Number.")
 .D BMES^XPDUTL("   Please inactivate this local procedure.")
 .K Y
 Q
NEW ;national procedures to add;;descript^nation #^CPT code^beg seq^end seq
 ;;PHONE GP 2-5 PT, 1ST 30M^NU207^98968
 ;;PHONE GP 6-10 PT, 1ST 30M^NU208^98968
 ;;PHONE GP 11-20PT, 1ST 30M^NU209^98968
 ;;PHONE GP >20PT, 1ST 30M^NU210^98968
 ;;PHONEGP 2-5PT EA AD'L15M^NU211^
 ;;PHONEGP6-10PT EA AD'L15M^NU212^
 ;;PHONEGP11-20PT EA AD'L15M^NU213^
 ;;PHONEGP>20PT EA AD'L15M^NU214^
 ;;FLUENCY EVALUATION^SP567^92521
 ;;SPEECH SOUND PROD EVAL^SP568^92522
 ;;SPEECH SOUND W/LANG EVAL^SP569^92523
 ;;VOICE/RESONANCE EVAL^SP570^92524
 ;;COUNSULT F2F ONLY, 15M ^SW136^T1016
 ;;PSYCHOSOC ASSESS NONLIP SW^SW137^H0031
 ;;PSYCHOSOC ASSESS LIP SW^SW138^90791
 ;;QUIT
NAMECHG ;* change national procedure names
 ;
 ;  ECXX is in format:
 ;   NATIONAL NUMBER^NEW NAME
 ;
 N ECX,ECXX,ECDA,DA,DR,DIC,DIE,X,Y,ECSTR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Changing names in EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 F ECX=1:1 S ECXX=$P($T(CHNG+ECX),";;",2) Q:ECXX="QUIT"  D
 .I $D(^EC(725,"D",$P(ECXX,U,1))) D
 ..S ECDA=+$O(^EC(725,"D",$P(ECXX,U,1),0))
 ..I $D(^EC(725,ECDA,0)) D
 ...S DA=ECDA,DR=".01////^S X=$P(ECXX,U,2)",DIE="^EC(725," D ^DIE
 ...D MES^XPDUTL(" ")
 ...D MES^XPDUTL("   Entry #"_ECDA_" for "_$P(ECXX,U,1))
 ...D BMES^XPDUTL("      ... field (#.01) updated to  "_$P(ECXX,U,2)_".")
 .I '$D(^EC(725,"D",$P(ECXX,U,1))) D
 ..D MES^XPDUTL(" ")
 ..S ECSTR="Can't find entry for "_$P(ECXX,U,1)
 ..D BMES^XPDUTL(ECSTR_" ...field (#.01) not updated.")
 Q
 ;
CHNG ;name changes -national code #^new procedure name
 ;;NU151^NUTR CNSLG IND EA A'L 25M
 ;;SW010^CLINICAL PHONE CALL 5-10M
 ;;SW020^GROUP COUNSELING
 ;;SW044^CLINICAL PHONE CALL 11-20M
 ;;SW045^CLINICAL PHONE CALL 21-30M
 ;;SW075^HEALTH RISK ASSESSMENT
 ;;SW093^TEAM CONF W/O PT/FAM, 30M
 ;;SW122^ADVANCED DIRECTIVES 15M
 ;;SW127^PT/FAM END OF LIFE COUNSEL
 ;;SW001^CONSULT F2F IN HOME
 ;;SW003^DISCHARGE PLANNING, 15M
 ;;SW004^FAM PSYCHOTHERAPY W/O PT
 ;;SW006^PREVENTATIVE COUNSEL, IND
 ;;SW009^TEAM F2F PT/FAM 30M
 ;;SW019^PREVENTATIVE COUNSEL, GRP
 ;;SW035^FAM PSYCHOTHERAPY W/PT
 ;;SW047^HOME VISIT
 ;;SW048^CRC FOLLOWUP, 15M
 ;;SW061^IND PSYCHOTHERAPY, 16-37M
 ;;SW062^IND PSYCHOTHERAPY, 38-52M
 ;;SW063^IND PSYCHOTHERAPY, 53-89M
 ;;SW077^PSYCHOSOCIAL ASSESS 60M
 ;;SW120^SUICIDE RISK ASSESSMENT
 ;;SW124^DISCHARGE CONF,FAM OR S/O
 ;;SW130^FAMILY COUNSELING W/PT
 ;;SW131^FAMILY COUNSELING W/O PT
 ;;QUIT
