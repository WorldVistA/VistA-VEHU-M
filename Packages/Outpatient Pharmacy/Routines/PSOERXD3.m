PSOERXD3 ;ALB/BWF - eRx Drug edit actions - Cont'd ; 5/26/2017 9:57am
 ;;7.0;OUTPATIENT PHARMACY;**651,700,770**;DEC 1997;Build 145
 ;
FNM S NM=$E(X,2,4),NM=$TR(NM,"qwertyuioplkjhgfdsazxcvbnm","QWERTYUIOPLKJHGFDSAZXCVBNM")
 S FLDNM=$S(NM="DOS":"DOSE^*Dosage",NM="DIS":"DOSE ORDERED^Dispense Units",NM="ROU":"ROUTE^*Route",NM="SCH":"SCHEDULE^*Schedule",NM="DUR"!(NM="LIM"):"DURATION^*Duration",1:"")
 S:FLDNM="" FLDNM=$S(NM="CON":"CONJUNCTION^*Conjunction",NM="NOU":"NOUN^Noun",NM="VER":"VERB^Verb",1:"")
 Q
JFN K FLDNM,AR S ENT=+Y,FLDNM=$S(NM="NOU":"NOU",NM="VER":"VER",NM="DOS":"ASK",NM="DIS":"DUPD",NM="ROU":"RTE",NM="SCH":"SCH",NM="DUR"!(NM="LIM"):"DUR",NM="CON":"CON",1:"")
 Q
CNON ;
 I $G(NOUN)'="" Q
 I '$G(PSORXED("DOSE ORDERED",ENT)) Q
 N PSONLT,PSONLL,PSONLG
 S PSONLL=$P($G(DOSE("DD",+$G(PSODRUG("IEN")))),"^",9) I PSONLL="" Q
 S PSONLG=$L(PSONLL)
 I PSONLG'>3 Q
 S PSONLT=$E(PSONLL,(PSONLG-2),PSONLG)
 I PSONLT'="(S)",PSONLT'="(s)" Q
 ;test noun of (S)
 K NOUN
 I $G(PSORXED("DOSE ORDERED",ENT))>1 S PSORXED("NOUN",ENT)=$E(PSONLL,1,(PSONLG-3))_$E(PSONLT,2) Q
 S PSORXED("NOUN",ENT)=$E(PSONLL,1,(PSONLG-3))
 Q
RTE2 I $G(DRET) S PSORXED("ROUTE",ENT)=""
 N DEFRT I $G(RTE) K RTE
 K DIR,DIRUT
 S DIR(0)="F^2:45",DIR("A")="ROUTE",DIR("?")="^D HLP^PSOORED4"
 I $G(CURTE)="" S DEFRT=$$DEFROUTE^PSOERXUT(+$G(PSODRUG("OI"))) I DEFRT'="" S DIR("B")=DEFRT
 I $G(CURTE)'="" S DIR("B")=CURTE
 D ^DIR I X[U,$L(X)>1 S FIELD="RTE",JUMP=1 K DIRUT,DTOUT Q
 Q:$D(DTOUT)!($D(DUOUT))
 I X="@"!(X="") K RTE,ERTE S DRET=1,PSORXED("ROUTE",ENT)="" Q
 K DRET I X=$P($G(^PS(51.2,+$G(PSORXED("ROUTE",ENT)),0)),"^") S RTE=$P(^PS(51.2,PSORXED("ROUTE",ENT),0),"^"),ERTE=$P(^PS(51.2,PSORXED("ROUTE",ENT),0),"^",2) W X_" "_$G(ERTE) Q
 S DIC=51.2,DIC(0)="QEZM",DIC("S")="I $P(^(0),""^"",4)" D ^DIC Q:X[U  G:Y=-1 RTE2 W "  "_$P(Y(0),"^",2)
 S:X'="" PSORXED("ROUTE",ENT)=+Y,RTE=Y(0,0),ERTE=$P(Y(0),"^",2),ERXRTE(ENT)=$P(Y(0),U,3)
 Q
SETUNEX ; Setting variable UNEXINS (Broken off from PSOERXD3 due to routine size limit)
 N DDONE
 I $G(PSORXED("ENT")) D
 .S DDONE=0
 .F I=1:1:PSORXED("ENT")  D  Q:DDONE
 ..I '$D(PSORXED("DOSE ORDERED",I)) S DDONE=1 Q
 ..I '$L($G(UNEXINS)) D  Q
 ...S UNEXINS=$G(PSORXED("VERB",I))_" "_$G(PSORXED("DOSE ORDERED",I))_" "_$G(PSORXED("NOUN",I))_" "_$G(ERXRTE(I))_" "_$G(PSORXED("SCHEDULE",I))
 ...I $L($G(PSORXED("DURATION",I))) S UNEXINS=UNEXINS_" "_$G(PSORXED("DURATION",I))
 ...I $L($G(PSORXED("CONJUNCTION",I))) S UNEXINS=UNEXINS_" "_$G(PSORXED("CONJUNCTION",I))_" "
 ..S UNEXINS=UNEXINS_$G(PSORXED("VERB",I))_" "_$G(PSORXED("DOSE ORDERED",I))_" "_$G(PSORXED("NOUN",I))_" "_$G(ERXRTE(I))_" "_$G(PSORXED("SCHEDULE",I))
 ..I $L($G(PSORXED("DURATION",I))) S UNEXINS=UNEXINS_" "_$G(PSORXED("DURATION",I))
 ..I $L($G(PSORXED("CONJUNCTION",I))) S UNEXINS=UNEXINS_" "_$G(PSORXED("CONJUNCTION",I))_" "
 Q
 ;
VDRG11(PSOIEN,PSOIENS) ;Indication For Use Edit
 N DA,DR,PSODFN,X,Y,DTOUT,DUOUT,DIRUT,DIROUT,PSOEDIT,PSOERXD,PSODRUG,PSODONE,PSODELINS
 ;
 S VDRG=$$GET1^DIQ(52.49,PSOIEN,3.2,"I") Q:'VDRG
 S VAOI=$$GET1^DIQ(50,VDRG,2.1,"I") Q:'VAOI
 S PSODRUG("OI")=VAOI,PSOERXD("IND")=$$GET1^DIQ(52.49,PSOIEN,29),PSOERXD("INDO")=$$GET1^DIQ(52.49,PSOIEN,29.2)
 S PSODFN=+$$GET1^DIQ(52.49,PSOIEN,.05,"I")
 S PSOEDIT=1,DIE="^PS(52.49,",DA=PSOIEN
 ;
 I '$P($G(^PS(55,PSODFN,"LAN")),"^") D  ;patient does not have other indication
 . D INDICAT^PSODIR(.PSOERXD)
 . I $G(X)="" S PQUIT=1 Q
 . I $G(PSOERXD("DFLG")) S PQUIT=1 Q
 . I $G(X)="@" S (PSOERXD("IND"),PSOERXD("INDF"))=$G(X)
 . S DR="29////"_$G(PSOERXD("IND"))_";29.1////"_$G(PSOERXD("INDF"))
 . D ^DIE K DIE,DR,DA
 E  D  ;patient contain other language indication for use
 . D SIND^PSODIR(.PSOERXD,1)
 . I $G(X)="" S PQUIT=1 Q
 . I $G(PSOERXD("DFLG")) S PQUIT=1 Q
 . I $G(X)="@" D
 . . I $G(PSOERXD("IND"))'="",$G(PSOERXD("INDO"))'="" Q  ;user answered no or entered ^ to the delete prompt confirmation
 . . S (PSOERXD("IND"),PSOERXD("INDF"),PSOERXD("INDO"))=$G(X)
 . S DR="29////"_$G(PSOERXD("IND"))_";29.1////"_$G(PSOERXD("INDF"))_";29.2////"_$G(PSOERXD("INDO"))
 . D ^DIE K DIE,DR,DA
 Q
