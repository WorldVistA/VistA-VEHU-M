PSO750PI ;BHAM/MFR - PSO*7*750 POST INSTALL; 10/01/2022 11:24Am
 ;;7.0;OUTPATIENT PHARMACY;**750**;DEC 1997;Build 6
 ;
EN ; Entry Point
 N HOPIEN,RRPIEN,DONE,IEN,CHHOPIEN
 D BMES^XPDUTL("Starting post-install for PSO*7*750 at "_$$FMTE^XLFDT($$NOW^XLFDT()))
 ;
 ; Fixing issue with PSO LM HIDDEN OTHER #2 protocol (Re-attaching PSO ERX SINGLE RXRENEWAL REQUEST protocol)
 S HOPIEN=+$O(^ORD(101,"B","PSO LM HIDDEN OTHER #2",0))
 S RRPIEN=+$O(^ORD(101,"B","PSO ERX SINGLE RXRENEWAL REQUEST",0))
 S (DONE,IEN)=0
 F  S IEN=$O(^ORD(101,HOPIEN,10,IEN)) Q:'IEN  D  I DONE Q
 . S CHHOPIEN=+$G(^ORD(101,HOPIEN,10,IEN,0))
 . I $D(^ORD(101,CHHOPIEN,0)) Q
 . K ^ORD(101,HOPIEN,10,IEN,0)
 . K ^ORD(101,HOPIEN,10,"B",CHHOPIEN)
 . I $G(RRPIEN) D
 . . S ^ORD(101,HOPIEN,10,IEN,0)=RRPIEN_"^RR"
 . . S ^ORD(101,HOPIEN,10,"B",RRPIEN,IEN)=""
 . . S DONE=1
 I HOPIEN,RRPIEN,'$D(^ORD(101,HOPIEN,10,"B",RRPIEN)) D
 . S IEN=$O(^ORD(101,HOPIEN,10,9999),-1)+1
 . S ^ORD(101,HOPIEN,10,IEN,0)=RRPIEN_"^RR"
 . S ^ORD(101,HOPIEN,10,"B",RRPIEN,IEN)=""
 ;
 I HOPIEN S XQORM=HOPIEN_";ORD(101," D XREF^XQORM
 ;
 ;
 D BMES^XPDUTL("Post-install for PSO*7*750 completed successfully at "_$$FMTE^XLFDT($$NOW^XLFDT()))
 Q
