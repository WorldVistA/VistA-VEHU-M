PSOERXR1 ;ALB/BWF - eRx Provider Validation - ListMan ; 8/3/2016 5:14pm
 ;;7.0;OUTPATIENT PHARMACY;**467,520,527,581,545,700**;DEC 1997;Build 261
 ;
EN ; -- main entry point for PSO ERX HOLDING QUEUE
 D EN^VALM("PSO ERX PROVIDER VALIDATION")
 Q
 ;
LMHDR ; ListMan Header Code
 D SHOW^VALM,HDR^PSOERXR1
 S XQORM("??")="D HELP^VALM2,HDR^PSOERXR1"
 Q
 ;
HDR ; -- header code
 N AMATCH,VPROIEN,VALUSER,VALDTTM,MATCH,HDR
 S AMATCH=$$GET1^DIQ(52.49,PSOIEN,1.2,"I"),VPROIEN=$$GET1^DIQ(52.49,PSOIEN,2.3,"I")
 S VALUSER=$$GET1^DIQ(52.49,PSOIEN,1.8,"E"),VALDTTM=$$GET1^DIQ(52.49,PSOIEN,1.9,"I")
 S VALMHDR(1)="eRx Reference #: "_IOINHI_$$GET1^DIQ(52.49,PSOIEN,.01,"E")_IOINORM
 D INSTR^VALM1("eRx Patient: "_IOINHI_$$GET1^DIQ(52.49,PSOIEN,.04,"E")_IOINORM,32,2)
 S MATCH=$S(AMATCH=1:"AUTO-MATCHED",AMATCH=2:"AUTO-MATCHED/EDITED",VPROIEN:"MANUALLY-MATCHED",1:"")
 I VALUSER'="",MATCH'="" S MATCH=MATCH_" | VALIDATED by "_$E(VALUSER,1,19)_" on "_$$FMTE^XLFDT(VALDTTM,"2Y")
 I MATCH="" S MATCH="NOT MATCHED"
 S $E(MATCH,81)="" D INSTR^VALM1("Status: "_IOINHI_MATCH_IOINORM,1,3)
 S HDR="",$E(HDR,15)="ERX PROVIDER",$E(HDR,40)="|",$E(HDR,54)="VISTA PROVIDER"
 S $E(HDR,81)="" D INSTR^VALM1(IORVON_IOUON_HDR_IORVOFF_IOINORM,1,4)
 Q
 ;
INIT ;
 Q:'$G(PSOIEN)  K ^XTMP("PSOERXR1",$J)
 ; - Resetting list to NORMAL video attributes
 D RESET^PSOERUT0()
 S LINE=1
 ;
 D SETPROV^PSOERUT1("LM",PSOIEN,,"PSOERXR1")
 ;
 S VALMCNT=LINE-1
 S EDTYP="PR"
 ; - Saving NORMAL video attributes to be reset later
 I LINE>$G(LASTLINE) D
 . F I=($G(LASTLINE)+1):1:LINE D SAVE^VALM10(I)
 . S LASTLINE=LINE
 D VIDEO^PSOERUT0()
 Q
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 K EDTYP,@VALMAR
 Q
 ;
EXPND ; -- expand code
 Q
