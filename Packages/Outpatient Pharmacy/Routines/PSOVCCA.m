PSOVCCA ;BIR/JLC - VCC PRESCRIPTION REFILL APIS ; Jun 06, 2021@14:00
 ;;7.0;OUTPATIENT PHARMACY;**642**;DEC 1997;Build 9
 ;
 Q
AP1(PSORET,PSODFN,PSORX,PSOUSER) ;ACCEPT REQUEST
 ; Input:  PSODFN (required) - Patient IEN Number
 ;         PSORX  (required) - Prescription Number
 ;         PSOUSER (required) - User requesting refill
 ; Output: PSORET - Return Value
 ;         See IA# 3768 for description and values
 ;
 N PSRX,PSRXD,IEN,PSORR,PSOICN,SITE,PSOSITE,ERR
 I $G(PSODFN)="" S PSORET=-4 G QUITAP1
 S PSOICN=+$$GETICN^MPIF001(PSODFN)
 I +$G(PSOICN)=-1 S PSORET=-4 G QUITAP1
 I $G(PSORX)="" S PSORET=-3 G QUITAP1
 I $O(^PSRX("B",PSORX,""))="" S PSORET=-3 G QUITAP1
 I '$D(^PSRX("B",PSORX)) S PSORET=-3 G QUITAP1
 S PSRX=$O(^PSRX("B",PSORX,"")),PSRXD=$G(^PSRX(PSRX,0))
 I PSRXD="" S PSORET=-3 G QUITAP1
 I $P(PSRXD,"^",2)'=PSODFN S PSORET=-5 G QUITAP1
 D REF^PSOATRFV(PSRX,PSOUSER,.ERR)
 I $D(ERR) S PSORET=0 Q
 S PSORET=1
QUITAP1 Q
