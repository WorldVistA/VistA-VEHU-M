QAP2P08 ; ALB/MJB - SET QAP OPTIONS OUT OF ORDER ;
 ;;2.0;SURVEY GENERATOR;**8**;Jun 20, 1995;Build 9
 ;Reference to XPDMENU supported by ICR 1157
 Q
 ;
MAIN(QAP) ; Step through options, pull IEN from B index and update
 ;QAP IS THE MENU RESTORE FLAG : 1=restore, anything else will place out of order
 N DA,DIE,DR,DO,QAPSOPT,QAPSD0,QAPMSG,QAPI,QAPTXT  K X,Y
 S QAP=$G(QAP),QAPTXT=$S(QAP=1:"@",1:"Option has been placed out of order")
 F QAPI=1:1 S QAPSOPT=$P($T(OPTS+QAPI^QAP2P08),";",3) Q:QAPSOPT=""  D
 . S QAPSD0=$O(^DIC(19,"B",QAPSOPT,0)) I QAPSD0="" S QAPMSG="OPTION "_QAPSOPT_" NOT FOUND" D EN^DDIOL(QAPMSG) Q
 . Q:$P($G(^DIC(19,QAPSD0,0)),"^")'=QAPSOPT
 . S QAPMSG="SETTING "_QAPSOPT_" TO "_$S(QAP=1:"ACTIVE...",1:"OUT OF ORDER...")
 . D EN^DDIOL(QAPMSG),OUT^XPDMENU(QAPSOPT,QAPTXT)
 .Q
 D CLEAN^DILF
 Q
OPTS ; Options to disable
 ;;QAP CLEAR SURVEY
 ;;QAP COPY SURVEY
 ;;QAP COUNT SURVEY PARTICIPANTS
 ;;QAP DELETE SURVEY
 ;;QAP DEMOGRAPHIC PRINT
 ;;QAP EDIT INCOMPLETE SURVEY
 ;;QAP EXPORT A SURVEY
 ;;QAP FIX RESPONSE
 ;;QAP GENERATE SURVEY STATISTICS
 ;;QAP IMPORT A SURVEY
 ;;QAP PARTICIPANT MENU
 ;;QAP PARTICIPATE
 ;;QAP POPULATE DEMOGRAPHIC FILE
 ;;QAP PRINT A SURVEY
 ;;QAP PRINT ALL INDIV RESPONSES
 ;;QAP RELEASE/DISABLE A SURVEY
 ;;QAP SURVEY MAINTENANCE
 ;;QAP USER PRINT HARDCOPY
 Q
