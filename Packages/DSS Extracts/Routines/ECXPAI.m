ECXPAI ;ALB/JAP,BIR/DMA,PTD-PAI Extract from File 45.9 ; 10/30/96 14:25
 ;;3.0;DSS EXTRACTS;**8,20**;Dec 22, 1997
BEG ;entry point from option
 D SETUP I ECFILE="" Q
 D ^ECXTRAC,^ECXKILL
 Q
 ;
START ; start package specific extract
 N DFN,QFLG,SSN,NAM,RACE
 S QFLG=0
 S ECED=ECED+.3,ECD=ECSD1
 F  S ECD=$O(^DG(45.9,"AA",ECD)),ECF=0 Q:'ECD  Q:ECD>ECED  F  S ECF=$O(^DG(45.9,"AA",ECD,ECF)) Q:'ECF  I $D(^DG(45.9,ECF,0)) S EC=^(0),DFN=+EC,ECTD=$P(EC,U,7) I $D(^DPT(DFN,0)) S EC=^(0) D  Q:QFLG
 .S ECDA=0 F  S ECDA=+$O(^DGPM("APCA",DFN,ECDA)),ECTD1=ECTD-.0001 Q:'ECDA  I $D(^DGPM(ECDA,0)) F  S ECTD1=+$O(^DGPM("APCA",DFN,ECDA,ECTD1)) Q:'ECTD1  Q:ECTD1>(ECTD+.3)  D  Q:QFLG
 ..S EC1=0 F  S EC1=+$O(^DGPM("APCA",DFN,ECDA,ECTD1,EC1)) Q:'EC1  I $D(^DGPM(EC1,0)),$P(^(0),U,2)'=3 S ECADM=$P(^DGPM(ECDA,0),U) D  Q:QFLG
 ...S NAM=$E($P($P(EC,U),",")_"    ",1,4),SSN=$P(EC,U,9),RACE=$P($G(^DIC(10,+$P(EC,U,6),0)),U,2)
 ...D FILE
 Q
 ;
FILE ;file record
 ;node0
 ;fac^dfn^ssn^name^i/o^day^admission date^admission time^admission/transfer date^admission/transfer time^race
 ;node1
 ;mpi^dss dept
 N DA,DIK
 S EC7=$O(^ECX(ECFILE,999999999),-1)
 S ECODEZ="" I EC7>0 S ECODEZ=^ECX(ECFILE,EC7,0)
 S ECODE=U_U_DFN_U_SSN_U_NAM_U_"3"_U_$$ECXDATE^ECXUTL(ECD,ECXYM)
 S ECODE=ECODE_U_$$ECXDATE^ECXUTL(ECADM,ECXYM)_U_$$ECXTIME^ECXUTL(ECADM)_U_$$ECXDATE^ECXUTL(ECTD1,ECXYM)_U_$$ECXTIME^ECXUTL(ECTD1)_U_RACE
 S ECODE1=U
 Q:$P(ECODE,U,4,14)=$P(ECODEZ,U,4,14)
 I $P(ECODE,U,3,7)=$P(ECODEZ,U,5,9),$P(ECODE,U,10)=$P(ECODEZ,U,12),$P(ECODE,U,8,9)'=$P(ECODEZ,10,11) D  Q
 .S $P(^ECX(ECFILE,EC7,0),U,10,11)=$P(ECODE,U,8,9)
 S EC7=EC7+1
 S ^ECX(ECFILE,EC7,0)=EC7_U_EC23_ECODE,^ECX(ECFILE,EC7,1)=ECODE1,ECRN=ECRN+1
 S DA=EC7,DIK="^ECX("_ECFILE_"," D IX^DIK K DIK,DA
 I $D(ZTQUEUED),ECRN>499,'(ECRN#500),$$S^%ZTLOAD S QFLG=1
 Q
SETUP ;Set required input for ECXTRAC
 S ECHEAD="PAS"
 D ECXDEF^ECXUTL2(ECHEAD,.ECPACK,.ECGRP,.ECFILE,.ECRTN,.ECPIECE,.ECVER)
 Q
 ;
QUE ; entry point for the background requeuing handled by ECXTAUTO
 D SETUP,QUE^ECXTAUTO,^ECXKILL Q
