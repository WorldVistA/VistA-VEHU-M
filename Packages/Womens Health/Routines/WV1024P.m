WV1024P ;ISP/RFR - PATCH 24 INSTALLATION TASKS;03/25/2020
 ;;1.0;WOMEN'S HEALTH;**24**;Sep 30, 1998;Build 582
 I '$$PATCH^XPDUTL("WV*1.0*24") D
 .N X
 .F X=42,45,46,47 D
 ..I $P($G(^WV(790.2,X,0)),U)'="" D
 ...W !,"WV Procedure Type entry found at IEN: "_X S XPDABORT=2
 Q
 ;
PRE ;PRE-INSTALL ACTIONS
 D BMES^XPDUTL("  Deleting data dictionaries:")
 D MES^XPDUTL("    WV PREGNANCY LOG (#790.05)")
 N DIU
 S DIU=790.05,DIU(0)="T"
 D EN^DIU2
 D MES^XPDUTL("    WV CASE MANAGER (#790.01)")
 S DIU=790.01,DIU(0)=""
 D EN^DIU2
 D MES^XPDUTL("    CURRENTLY PREGNANT FIELD (#.13) IN WV PATIENT FILE")
 N DIK,DA
 S DIK="^DD(790,",DA=.13,DA(1)=790
 D ^DIK
 D MES^XPDUTL("    EDC FIELD (#.14) IN WV PATIENT FILE")
 S DIK="^DD(790,",DA=.14,DA(1)=790
 D ^DIK
 D MES^XPDUTL("  Finished deleting data dictionaries")
 I $$LKOPT^XPDMENU("WV EDIT PREGNANCY LOG")>0 D
 .D BMES^XPDUTL("  Renaming option WV EDIT PREGNANCY LOG")
 .D RENAME^XPDMENU("WV EDIT PREGNANCY LOG","WV EDIT PREG/LAC STATUS DATA")
 .D MES^XPDUTL("  Finished renaming option WV EDIT PREGNANCY LOG")
 N WVMSGS,WVSTAT
 S WVMSGS(1)=" "
 S WVMSGS(2)="  Removing the Transfer a Case Manager's Patients option"
 S WVMSGS(3)="   from the FILE MAINTENANCE MENU"
 D BMES^XPDUTL(.WVMSGS)
 S WVSTAT=$$DELETE^XPDMENU("WV MENU-FILE MAINTENANCE","WV TRANSFER CASE MANAGER")
 I WVSTAT D MES^XPDUTL("    Option successfully removed")
 I 'WVSTAT D MES^XPDUTL("    Option not removed")
 D SETMRI,GETACC,GETCOM
 D SETBTOMO
 Q
 ;
GETACC ;
 N IEN,NODE,SUB
 S SUB="WV ACCESS#"
 K ^XTMP(SUB)
 S ^XTMP(SUB,0)=$$FMADD^XLFDT(DT,30)_U_DT_U_"WV Patch 24 Accession #"
 S IEN=$O(^WV(790.2,"B","BREAST MRI","")) Q:IEN'>0
 S ^XTMP(SUB,"BREAST MRI")=$P($G(^WV(790.2,IEN,0)),U,6)
 Q
 ;
GETCOM ;
 N DA,DIE,DR,CNT,WVIEN,WVXTMP
 S WVXTMP="WV COMMENTS CONVERSION"
 K ^XTMP(WVXTMP)
 S ^XTMP(WVXTMP,0)=$$FMADD^XLFDT(DT,30)_U_DT_U_"WV Patch 24 Comment Conversion"
 S WVIEN=0 F  S WVIEN=$O(^WV(790.1,WVIEN)) Q:WVIEN'>0  D
 .S CNT=0 F  S CNT=$O(^WV(790.1,WVIEN,10,CNT)) Q:CNT'>0  D
 ..I $P($G(^WV(790.1,WVIEN,10,CNT,1)),U)="" Q
 ..S ^XTMP(WVXTMP,WVIEN,CNT)=$P($G(^WV(790.1,WVIEN,10,CNT,1)),U)
 ..S DA(1)=WVIEN,DA=CNT
 ..S DIE="^WV(790.1,"_DA(1)_",10,",DR="3///@" D ^DIE
 Q
 ;
POST ;POST-INSTALL ACTIONS
 N WVIEN,WVMSGS,WVSTAT,%DT,X,Y,DTOUT,WVPARAMS,WVPARAM,WVINST
 D BMES^XPDUTL("  Clearing field values from existing WV PATIENT file entries:")
 D MES^XPDUTL("    CURRENTLY PREGNANT (#.13)")
 D MES^XPDUTL("    EDC (#.14)")
 S WVIEN=0 F  S WVIEN=$O(^WV(790,WVIEN)) Q:'WVIEN  D
 .I $P(^WV(790,WVIEN,0),U,13)'="" S $P(^(0),U,13)=""
 .I $P(^WV(790,WVIEN,0),U,14)'="" S $P(^(0),U,14)=""
 D MES^XPDUTL("  Finished clearing existing values")
 D CONVERT
 S %DT="P",X="T-1M" D ^%DT
 I $G(Y)>0 S WVPARAMS("WV IMAGING ORDER START DT",1)=1_U_Y
 E  S WVMSGS(1)="  Unable to calculate imaging order start date.",WVMSGS(2)="  Unknown date calculation error." D MES^XPDUTL(.WVMSGS)
 S WVPARAMS("WV COVER SHEET WEBSITES",1)="U. S. MEC for Contraceptive Use"_U_"http://www.cdc.gov/reproductivehealth/unintendedpregnancy/usmec.htm"
 S WVPARAMS("WV COVER SHEET WEBSITES",2)="U. S. SPR for Contraceptive Use"_U_"http://www.cdc.gov/reproductivehealth/unintendedpregnancy/usspr.htm"
 S WVPARAMS("WV ENTERED IN ERROR REASONS",1)=1_U_"Wrong patient"
 S WVPARAM="" F  S WVPARAM=$O(WVPARAMS(WVPARAM)) Q:WVPARAM=""  D
 .S WVMSGS(1)=" ",WVMSGS(2)="  Setting parameter "_WVPARAM_"..."
 .D BMES^XPDUTL(.WVMSGS)
 .S WVINST=0 F  S WVINST=$O(WVPARAMS(WVPARAM,WVINST)) Q:'+WVINST!(+$G(WVSTAT)>0)  D
 ..D EN^XPAR("PKG",WVPARAM,$P(WVPARAMS(WVPARAM,WVINST),U),$P(WVPARAMS(WVPARAM,WVINST),U,2),.WVSTAT)
 ..I +WVSTAT>0 K WVMSGS S WVMSGS(1)="  FAILED",WVMSGS(2)="  "_$P(WVSTAT,U,2) D MES^XPDUTL(.WVMSGS)
 .I +WVSTAT=0 D MES^XPDUTL("  DONE")
 D POSTACC,POSTCOM
 Q
 ;
CONVERT ;CONVERT EXISTING PREGNANCY DATA INTO NEW FORMAT
 I '$D(^WV(790.05)) Q
 I $O(^WV(790.05,0))="" K ^WV(790.05) Q
 D BMES^XPDUTL("  Converting existing pregnancy data...")
 N WVNOALRT,WVIEN,WVFDA,WVPNUM,%DT,X,Y,DTOUT,WVEXIT,WVDFN,WVNODE,WVNIEN,WVERROR,WVMSGS,WVPKG
 S %DT="TS",WVNOALRT=1,WVPKG=+$$FIND1^DIC(9.4,,,"WOMEN'S HEALTH",,"I $P($G(^(0)),U,2)=""WV""","WVERROR")
 I 'WVPKG D  Q
 .S WVMSGS(1)="    ERROR encountered:",WVMSGS(2)="    "_$$FMERROR^WVUTL11(.WVERROR)
 .S WVMSGS(3)="    Could not find the WOMEN'S HEALTH entry in the PACKAGE file (#9.4)."
 .S WVMSGS(4)="    Please contact the national help desk for assistance."
 .D MES^XPDUTL(.WVMSGS)
 S WVIEN=0 F  S WVIEN=$O(^WV(790.05,WVIEN)) Q:'+WVIEN!($D(WVERROR))  D
 .S WVNODE=$G(^WV(790.05,WVIEN,0)),WVDFN=$P(WVNODE,U,2)
 .I '$D(^WV(790,WVDFN,0)) K ^WV(790.05,WVIEN) Q
 .F WVPNUM=1,4 S X=$P(WVNODE,U,WVPNUM) D
 ..I +X'>0 S $P(WVNODE,U,WVPNUM)="" Q
 ..D ^%DT I Y'>0 S $P(WVNODE,U,WVPNUM)=""
 .I $P(WVNODE,U)="" K ^WV(790.05,WVIEN) Q
 .S WVFDA(790.05,"+1,"_WVDFN_",",.01)=$P(WVNODE,U),WVFDA(790.05,"+1,"_WVDFN_",",3)=WVPKG
 .S WVFDA(790.05,"+1,"_WVDFN_",",21)=$P(WVNODE,U,3)
 .S:$P(WVNODE,U,4)'="" WVFDA(790.05,"+1,"_WVDFN_",",42)=$P(WVNODE,U,4)
 .D UPDATE^DIE(,"WVFDA","WVNIEN","WVERROR")
 .I $D(WVERROR) D  Q
 ..S WVMSGS(1)="    ERROR encountered while converting record #"_WVIEN_":"
 ..S WVMSGS(2)="    "_$$FMERROR^WVUTL11(.WVERROR)
 ..S WVMSGS(3)="    The data conversion is incomplete; please"
 ..S WVMSGS(4)="    contact the national help desk for assistance."
 ..D MES^XPDUTL(.WVMSGS)
 .S $P(^WV(790,WVDFN,4,WVNIEN(1),0),U,2)=""
 .K WVNIEN
 I '$D(WVERROR) K ^WV(790.05) D MES^XPDUTL("  DONE")
 Q
 ;
POSTACC ;
 N ACCESS,FDA,IEN,MSG,SUB
 S SUB="WV ACCESS#"
 S IEN=$O(^WV(790.2,"B","BREAST MRI","")) Q:IEN'>0
 I '$D(^XTMP(SUB)) D  Q
 .S FDA(790.2,"42,",.06)=""
 .D FILE^DIE("","FDA","MSG")
 S ACCESS=$G(^XTMP(SUB,"BREAST MRI")) I ACCESS="" Q
 S IEN=$O(^WV(790.2,"B","BREAST MRI","")) Q:IEN'>0
 S FDA(790.2,"42,",.06)=ACCESS
 D FILE^DIE("","FDA","MSG")
 I $D(MSG) D MES^XPDUTL("Error Updating BREAST MRI Last Accession #") Q
 K ^XTMP(SUB)
 Q
 ;
POSTCOM ;
 N COM,DA,DIE,DR,WVIEN,WVXTMP
 S WVXTMP="WV COMMENTS CONVERSION"
 I '$D(^XTMP(WVXTMP)) Q
 S DA(1)=0 F  S DA(1)=$O(^XTMP(WVXTMP,DA(1))) Q:DA(1)'>0  D
 .S DA=0 F  S DA=$O(^XTMP(WVXTMP,DA(1),DA)) Q:DA'>0  D
 ..S COM=$G(^XTMP(WVXTMP,DA(1),DA)) Q:COM=""
 ..S DIE="^WV(790.1,"_DA(1)_",10,",DR="15///^S X=COM" D ^DIE
 K ^XTMP(WVXTMP)
 Q
 ;
GETLIST(ARRAY,WHAT) ;
 N LINE
 I WHAT="PURPOSE" D  Q
 .F LINE=1:1 Q:$L($T(PURLIST+LINE))<3  D
 ..N TEXT
 ..S TEXT=$P($T(PURLIST+LINE),";;",2)
 ..S ARRAY(TEXT)=""
 I WHAT="TYPE" D  Q
 .F LINE=1:1 Q:$L($T(TYPELIST+LINE))<3  D
 ..N TEXT
 ..S TEXT=$P($T(TYPELIST+LINE),";;",2)
 ..S ARRAY(TEXT)=""
 I WHAT="DIAGNOSIS" D  Q
 .F LINE=1:1 Q:$L($T(DIAGLIST+LINE))<3  D
 ..N TEXT
 ..S TEXT=$P($T(DIAGLIST+LINE),";;",2)
 ..S ARRAY(TEXT)=""
 I WHAT="TRANSLATION" D  Q
 .F LINE=1:1 Q:$L($T(TRANS+LINE))<3  D
 ..N TEXT
 ..S TEXT=$P($T(TRANS+LINE),";;",2)
 ..S ARRAY(TEXT)=""
 Q
 ;
REPRES ;
 N FDA,IEN,NAME,NODE,TEMP,X,VALUE,ID
 S IEN=0 F  S IEN=$O(^XTMP("WV MRI RECORDS","IENS",IEN)) Q:IEN'>0  D
 .I '$D(^WV(790.31,"P",IEN)) Q
 .S ID=0 F  S ID=$O(^WV(790.31,"P",IEN,ID)) Q:ID'>0  D
 ..K TEMP
 ..D GETS^DIQ(790.31,ID,"**","I","TEMP","MSG")
 ..K FDA
 ..S X=.02 F  S X=$O(TEMP(790.31,ID_",",X)) Q:X'>0!(X>.19)  D
 ...I $G(TEMP(790.31,ID_",",X,"I"))'=IEN Q
 ...K FDA
 ...S FDA(790.31,ID_",",X)=42
 ..I '$D(FDA) Q
 ..S FDA(790.31,ID_",",.01)=$G(TEMP(790.31,ID_",",.01,"I"))
 ..D UPDATE^DIE("","FDA","","MSG")
 ..I $D(MSG) D MES^XPDUTL("Error updating file 790.31: "_ID) Q
 Q
 ;
SETMRI ;
 I $P($G(^WV(790.2,42,0)),U)="BREAST MRI" Q
 N FDA,MSG,WVAIEN
 S FDA(790.2,"+1,",.01)="BREAST MRI"
 S WVAIEN(1)=42
 D UPDATE^DIE("","FDA","WVAIEN","MSG")
 I $D(MSG) D MES^XPDUTL("Error setting BREAST MRI entry") Q
 I +$G(WVAIEN(1))'=42 D MES^XPDUTL("BREAST MRI not set to the correct entry") Q
 Q
 ;
SETBTOMO ;
 I $P($G(^WV(790.2,45,0)),U)="BREAST TOMOSYNTHESIS BILAT" Q
 N FDA,MSG,WVAIEN
 S FDA(790.2,"+1,",.01)="BREAST TOMOSYNTHESIS BILAT"
 S WVAIEN(1)=45
 D UPDATE^DIE("","FDA","WVAIEN","MSG")
 I $D(MSG) D MES^XPDUTL("Error setting BREAST TOMOSYNTHESIS BILAT entry") Q
 I +$G(WVAIEN(1))'=45 D MES^XPDUTL("BREAST TOMOSYNTHESIS BILAT not set to the correct entry") Q
 ;
 I $P($G(^WV(790.2,46,0)),U)="BREAST TOMOSYNTHESIS SCREENING" Q
 N FDA,MSG,WVAIEN
 S FDA(790.2,"+1,",.01)="BREAST TOMOSYNTHESIS SCREENING"
 S WVAIEN(1)=46
 D UPDATE^DIE("","FDA","WVAIEN","MSG")
 I $D(MSG) D MES^XPDUTL("Error setting BREAST TOMOSYNTHESIS SCREENING entry") Q
 I +$G(WVAIEN(1))'=46 D MES^XPDUTL("BREAST TOMOSYNTHESIS SCREENING not set to the correct entry") Q
 ;
 I $P($G(^WV(790.2,47,0)),U)="BREAST TOMOSYNTHESIS UNILAT" Q
 N FDA,MSG,WVAIEN
 S FDA(790.2,"+1,",.01)="BREAST TOMOSYNTHESIS UNILAT"
 S WVAIEN(1)=47
 D UPDATE^DIE("","FDA","WVAIEN","MSG")
 I $D(MSG) D MES^XPDUTL("Error setting BREAST TOMOSYNTHESIS UNILAT entry") Q
 I +$G(WVAIEN(1))'=47 D MES^XPDUTL("BREAST TOMOSYNTHESIS UNILAT not set to the correct entry") Q
 Q
 ;
SENDDIAG(ANAME) ;
 N ARRAY
 D GETLIST(.ARRAY,"DIAGNOSIS")
 I $D(ARRAY(ANAME)) Q 1
 Q 0
 ;
SENDDX(ANAME) ;
 N ARRAY
 D GETLIST(.ARRAY,"TRANSLATION")
 I $D(ARRAY(ANAME)) Q 1
 Q 0
 ;
SENDPUR(ANAME) ;
 N ARRAY
 D GETLIST(.ARRAY,"PURPOSE")
 I $D(ARRAY(ANAME)) Q 1
 Q 0
 ;
SENDTYPE(ANAME) ;
 N ARRAY
 D GETLIST(.ARRAY,"TYPE")
 I $D(ARRAY(ANAME)) Q 1
 Q 0
 ;
DIAGLIST ;
 ;;BI-RADS CATEGORY 0
 ;;BI-RADS CATEGORY 1
 ;;BI-RADS CATEGORY 2
 ;;BI-RADS CATEGORY 3
 ;;BI-RADS CATEGORY 4
 ;;BI-RADS CATEGORY 5
 ;;BI-RADS CATEGORY 6
 ;;Abnormal
 ;;No Evidence of Malignancy
 ;;Unsatisfactory for Dx
 Q
 ;
PURLIST ;
 ;;BR 0 BIOPSY ALREADY OBTAIN
 ;;BR 0 CURRENTLY UNDER TREATMENT
 ;;BR 0 REFER FOR BIOPSY
 ;;BR 0 REFER TO ONCOLOGIST
 ;;BR 0 REFER TO SURGEON
 ;;BR 3 BIOPSY ALREADY OBTAIN
 ;;BR 3 CURRENTLY UNDER TREATMENT
 ;;BR 3 REFER FOR BIOPSY
 ;;BR 3 REFER TO ONCOLOGIST
 ;;BR 3 REFER TO SURGEON
 ;;BR 4 BIOPSY ALREADY OBTAIN
 ;;BR 4 CURRENTLY UNDER TREATMENT
 ;;BR 4 REFER FOR BIOPSY
 ;;BR 4 REFER TO ONCOLOGIST
 ;;BR 4 REFER TO SURGEON
 ;;BR 5 BIOPSY ALREADY OBTAIN
 ;;BR 5 CURRENTLY UNDER TREATMENT
 ;;BR 5 REFER FOR BIOPSY
 ;;BR 5 REFER TO ONCOLOGIST
 ;;BR 5 REFER TO SURGEON
 ;;BR 6 BIOPSY ALREADY OBTAIN
 ;;BR 6 CURRENTLY UNDER TREATMENT
 ;;BR 6 REFER FOR BIOPSY
 ;;BR 6 REFER TO ONCOLOGIST
 ;;BR 6 REFER TO SURGEON
 ;;BR ABNORMAL BIOPSY ALREADY OBTAIN
 ;;BR ABNORMAL CONSULT
 ;;BR ABNORMAL CURRENTLY UNDER TREATMENT
 ;;BR ABNORMAL OBTAIN PRIOR FILMS
 ;;BR ABNORMAL REFER FOR BIOPSY
 ;;BR ABNORMAL REFER TO ONCOLOGIST
 ;;BR ABNORMAL REFER TO SURGEON
 ;;BR ABNORMAL, ORDER MRI
 ;;BR ABNORMAL, ORDER ULTRASOUND
 ;;BR BIRAD 0 CONSULT
 ;;BR BIRAD 0 OBTAIN PRIOR FILMS
 ;;BR BIRAD 0, ORDER MRI
 ;;BR BIRAD 0, ORDER ULTRASOUND
 ;;BR BIRAD 0, next MAM 1M
 ;;BR BIRAD 0, next MAM 2M
 ;;BR BIRAD 0, next MAM 3M
 ;;BR BIRAD 0, next MAM 4M
 ;;BR BIRAD 0, next MAM 5M
 ;;BR BIRAD 0, next MAM 6M
 ;;BR BIRAD 1, next MAM 1Y
 ;;BR BIRAD 1, next MAM 2Y
 ;;BR BIRAD 2, next MAM 1Y
 ;;BR BIRAD 2, next MAM 2Y
 ;;BR BIRAD 3 CONSULT
 ;;BR BIRAD 3 OBTAIN PRIOR FILMS
 ;;BR BIRAD 3, ORDER MRI
 ;;BR BIRAD 3, ORDER ULTRASOUND
 ;;BR BIRAD 3, next MAM 1M
 ;;BR BIRAD 3, next MAM 2M
 ;;BR BIRAD 3, next MAM 3M
 ;;BR BIRAD 3, next MAM 4M
 ;;BR BIRAD 3, next MAM 5M
 ;;BR BIRAD 3, next MAM 6M
 ;;BR BIRAD 3, next MAM 12M
 ;;BR BIRAD 4 CONSULT
 ;;BR BIRAD 4 OBTAIN PRIOR FILMS
 ;;BR BIRAD 4, ORDER MRI
 ;;BR BIRAD 4, ORDER ULTRASOUND
 ;;BR BIRAD 4, next MAM 1M
 ;;BR BIRAD 4, next MAM 2M
 ;;BR BIRAD 4, next MAM 3M
 ;;BR BIRAD 4, next MAM 4M
 ;;BR BIRAD 4, next MAM 5M
 ;;BR BIRAD 4, next MAM 6M
 ;;BR BIRAD 5 CONSULT
 ;;BR BIRAD 5 OBTAIN PRIOR FILMS
 ;;BR BIRAD 5, ORDER MRI
 ;;BR BIRAD 5, ORDER ULTRASOUND
 ;;BR BIRAD 5, next MAM 1M
 ;;BR BIRAD 5, next MAM 2M
 ;;BR BIRAD 5, next MAM 3M
 ;;BR BIRAD 5, next MAM 4M
 ;;BR BIRAD 5, next MAM 5M
 ;;BR BIRAD 5, next MAM 6M
 ;;BR BIRAD 6
 ;;BR BIRAD 6 CONSULT
 ;;BR BIRAD 6 OBTAIN PRIOR FILMS
 ;;BR BIRAD 6, next MAM 1M
 ;;BR BIRAD 6, next MAM 2M
 ;;BR BIRAD 6, next MAM 3M
 ;;BR BIRAD 6, next MAM 4M
 ;;BR BIRAD 6, next MAM 5M
 ;;BR BIRAD 6, next MAM 6M
 ;;BR BIRAD 6, ORDER MRI
 ;;BR BIRAD 6, ORDER ULTRASOUND
 ;;BR NOT INDICATED
 ;;BR RESULT ABNORMAL, NEXT MAM 1M
 ;;BR RESULT ABNORMAL, NEXT MAM 2M
 ;;BR RESULT ABNORMAL, NEXT MAM 3M
 ;;BR RESULT ABNORMAL, NEXT MAM 4M
 ;;BR RESULT ABNORMAL, NEXT MAM 5M
 ;;BR RESULT ABNORMAL, NEXT MAM 6M
 ;;BR RESULT NEM, NEXT MAM 1Y
 ;;BR RESULT NEM, NEXT MAM 2Y
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 1M
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 2M
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 3M
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 4M
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 5M
 ;;BR RESULT UNSATISFACTORY, NEXT MAM 6M
 ;;BR UNSATISFACTORY BIOPSY ALREADY OBTAIN
 ;;BR UNSATISFACTORY CONSULT
 ;;BR UNSATISFACTORY CURRENTLY UNDER TREATMENT
 ;;BR UNSATISFACTORY OBTAIN PRIOR FILMS
 ;;BR UNSATISFACTORY REFER FOR BIOPSY
 ;;BR UNSATISFACTORY REFER TO ONCOLOGIST
 ;;BR UNSATISFACTORY REFER TO SURGEON
 ;;BR UNSATISFACTORY, ORDER MRI
 ;;BR UNSATISFACTORY, ORDER ULTRASOUND
 ;;MAM unsatisfactory, need repeat
 ;;BR BIRAD 1, next MAM AGE AT START AGE
 ;;BR BIRAD 1, next MAM AGE 1Y
 ;;BR BIRAD 1, next MAM AGE 2Y
 ;;BR BIRAD 2, next MAM AGE AT START AGE
 ;;BR BIRAD 2, next MAM AGE 1Y
 ;;BR BIRAD 2, next MAM AGE 2Y
 ;;BR NEM, next MAM AGE AT START AGE
 ;;BR NEM, next MAM AGE 1Y
 ;;BR NEM, next MAM AGE 2Y
 ;;BR RETURN FOR MAM AT START AGE
 ;;BR RETURN FOR MAM 1Y
 Q
 ;
TRANS ;
 ;;162
 ;;163
 ;;164
 ;;165
 ;;166
 ;;167
 ;;168
 Q
 ;
TYPELIST ;
 ;;CONVERSATION WITH PATIENT
 ;;LETTER (CERTIFIED)
 ;;LETTER, FIRST
 ;;PHONE CALL, 1ST
 ;;SECURE MESSAGING
 Q
 ;
